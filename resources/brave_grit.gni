# Copyright (c) 2022 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//tools/grit/grit_rule.gni")

brave_grit_resource_id_target = "//brave/resources:brave_resource_ids"
brave_grit_resource_id_file =
    get_label_info(brave_grit_resource_id_target, "target_gen_dir") +
    "/brave_resource_ids"

template("brave_grit") {
  grit(target_name) {
    forward_variables_from(invoker, "*")
    resource_ids = brave_grit_resource_id_file
    if (!defined(deps)) {
      deps = []
    }
    deps += [ brave_grit_resource_id_target ]
  }
}

template("brave_or_default_grit") {
  if (defined(invoker.use_brave_grit) && invoker.use_brave_grit) {
    brave_grit(target_name) {
      forward_variables_from(invoker, "*", TESTONLY_AND_VISIBILITY)
    }
  } else {
    grit(target_name) {
      forward_variables_from(invoker, "*", TESTONLY_AND_VISIBILITY)
    }
  }
}
