import("//build/util/branding.gni")

declare_args() {
  brave_chromium_build = true

  # Release channel support.
  # "beta" for beta release.
  # "dev" for dev channel release.
  # "nightly" for nightly channel release.
  # "" for stable channel release.
  brave_channel = ""

  base_sparkle_update_url = ""

  brave_dsa_file = "dsa_pub.pem"

  brave_product_name = ""
  brave_project_name = ""
  brave_exe = ""
  brave_version_major = ""
  brave_version_minor = ""
  brave_version_build = ""
  brave_version_patch = 0
  chrome_version_string = ""
}

if (base_sparkle_update_url == "") {
  base_sparkle_update_url = "https://updates.bravesoftware.com/sparkle/Brave-Browser"
}
_update_channel = brave_channel
if (_update_channel == "") {
  _update_channel = "stable"
}
brave_feed_url = "$base_sparkle_update_url/$_update_channel/appcast.xml"


brave_version = "$brave_version_major.$brave_version_minor.$brave_version_build"

if (brave_exe == "") {
  brave_exe = brave_project_name
}

brave_dist_dir = "$root_out_dir/dist"
if (is_win) {
  brave_exe = "$brave_exe.exe"
} else if (is_mac) {
  brave_exe = "$chrome_product_full_name.app"
}

brave_icon_dir = "nightly"
if (is_component_build) {
  brave_icon_dir = "dev"
}

brave_platform = "darwin"
if (is_win) {
  brave_platform = "win32"
} else if (is_linux) {
  brave_platform = "linux"
}

brave_product_dir_name_suffix = ""

if (is_official_build) {
  if (brave_channel == "beta") {
    brave_product_dir_name_suffix = "-Beta"
  } else if (brave_channel == "dev") {
    brave_product_dir_name_suffix = "-Dev"
  } else if (brave_channel == "nightly") {
    brave_product_dir_name_suffix = "-Nightly"
  } else {
    assert(brave_channel == "", "Unknown channel name")
  }
} else {
  brave_product_dir_name_suffix = "-Development"
}

brave_product_dir_name = "BraveSoftware/Brave-Browser$brave_product_dir_name_suffix"

if (is_mac) {
  brave_target_app_name = "Brave-Browser$brave_product_dir_name_suffix"
}
