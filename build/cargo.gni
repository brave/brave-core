# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

if (is_mac) {
  import("//build/config/mac/mac_sdk.gni")
}

# Runs a script to compile a local Cargo package and all of its dependencies
template("cargo_build") {
  action(target_name) {
    script = "//brave/script/cargo.py"

    # make sure rebuilds when files change
    forward_variables_from(invoker, "*")
    if (!defined(outputs)) {
      outputs = []
    }
    if (defined(output)) {
      outputs += [ output ]
    }

    args = [
      "--rustup_path=" + rebase_path("//brave/build/rustup/", root_build_dir),
      "--cargo_path=" + rebase_path("//brave/build/rustup/", root_build_dir),
      "--manifest_path=" + rebase_path(manifest_path, root_build_dir),
      "--build_path=" + rebase_path(build_path, root_build_dir),
      "--target=" + target,
      "--is_debug=$is_debug"
    ]

    if (is_mac) {
      args += [ "--mac_deployment_target=" + mac_deployment_target ]
    }

    if (defined(invoker.rust_flags)) {
      args += [ "--rust_flags=$rust_flags" ]
    }

  }
}
