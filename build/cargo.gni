# Copyright (c) 2020 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/build/rust/config.gni")
import("//build/config/clang/clang.gni")

if (is_mac) {
  import("//build/config/mac/mac_sdk.gni")
}

if (is_ios) {
  import("//build/config/ios/ios_sdk_overrides.gni")
}

rust_lib_build_path = "$root_build_dir/brave_rust/out/$current_cpu"
rust_lib_output_path =
    "$rust_lib_build_path/${cargo_lib_prefix}brave_rust${cargo_lib_suffix}"

template("rust_ffi") {
  static_library(target_name) {
    libs = [ rust_lib_output_path ]

    if (is_mac) {
      frameworks = [ "Security.framework" ]

      if (defined(invoker.frameworks)) {
        frameworks += invoker.frameworks
      }
    } else if (is_win) {
      libs += [ "bcrypt.lib" ]
    }

    deps = [ "//brave/build/rust:rust_lib" ]
    if (is_apple) {
      deps += [
        "//base",  # for rust_eh_personality_impl
        "//brave/build/rust:generate_unexport_list",
      ]
    }
    if (defined(invoker.deps)) {
      deps += invoker.deps
    }

    public_deps = [ "//brave/build/rust:cxx_cppdeps" ]
    if (defined(invoker.public_deps)) {
      public_deps += invoker.public_deps
    }

    configs -= [ "//build/config/compiler:chromium_code" ]
    configs += [ "//build/config/compiler:no_chromium_code" ]

    forward_variables_from(invoker,
                           "*",
                           [
                             "deps",
                             "frameworks",
                             "public_deps",
                           ])
  }
}

template("rust_crate") {
  action(target_name) {
    script = "//brave/script/stamp.py"

    forward_variables_from(invoker, [ "deps" ])

    if (defined(invoker.visibility)) {
      visibility = invoker.visibility
    } else {
      visibility = [ "//brave/build/rust:*" ]
    }

    inputs = [ script ]

    inputs += invoker.inputs

    _stamp = "${target_gen_dir}/${target_name}.stamp"

    args = [
      "--stamp",
      rebase_path(_stamp, root_build_dir),
    ]

    outputs = [ _stamp ]
  }
}

# Runs a script to compile a local Cargo package and all of its dependencies
template("cargo_build") {
  group_target = target_name
  cargo_build_target = target_name + "_cargo_build"

  _rustflags = default_rustflags

  if (defined(invoker.rustflags)) {
    _rustflags += invoker.rustflags
  }

  action(cargo_build_target) {
    script = "//brave/script/cargo.py"

    cargo_bin = "$rustup_home/bin/cargo"
    if (is_win) {
      cargo_bin = cargo_bin + ".exe"
    }

    force_rebuild_inputs = [
      script,
      cargo_bin,
      "Cargo.toml",
      "Cargo.lock",
    ]

    if (is_apple) {
      toolchain_home =
          "$rustup_home/toolchains/$brave_rust_version-x86_64-apple-darwin"
      std_src_home = "$toolchain_home/lib/rustlib/src/rust/library/std/src"
      force_rebuild_inputs += [ "$std_src_home/personality/gcc.rs" ]
    }
    inputs = force_rebuild_inputs
    forward_variables_from(invoker, "*", [ "inputs" ])

    if (defined(invoker.inputs)) {
      inputs += invoker.inputs
    }

    outputs = [ rust_lib_output_path ]

    if (!defined(target)) {
      target = rustc_target  # defined by //brave/build/rust/config.gni,
    }

    args = [
      "--exe=" + rebase_path(cargo_bin),
      "--rustup_home=" + rebase_path(rustup_home),
      "--manifest_path=" + rebase_path(manifest_path, root_build_dir),
      "--build_path=" + rebase_path(rust_lib_build_path, root_build_dir),
      "--target=" + target,
      "--clang_bin_path=" + rebase_path("$clang_base_path/bin"),
      "--is_debug=$is_debug",
      "--profile=$cargo_profile",
    ]

    # See exclude_unwind_tables
    # TODO(bridiver) Investigate whether we should use unwind on android
    if (is_official_build) {
      args += [ "--panic=abort" ]
    } else {
      args += [ "--panic=unwind" ]
    }

    if (rustc_toolchain != "") {
      args += [ "--toolchain=$rustc_toolchain" ]
    }

    if (is_mac) {
      args += [ "--mac_deployment_target=" + mac_deployment_target ]
    }

    if (is_ios) {
      args += [ "--ios_deployment_target=" + ios_deployment_target ]
      args += [ "--features=" + "ios" ]
    } else {
      args += [ "--features=" + "chromium" ]
    }

    foreach(input, force_rebuild_inputs) {
      args += [
        "--inputs",
        rebase_path(input),
      ]
    }

    if (defined(_rustflags)) {
      foreach(flag, _rustflags) {
        args += [
          "--rust_flag",
          flag,
        ]
      }
    }
  }

  # TODO(bridiver) - is this still needed?
  group(group_target) {
    public_deps = [ ":$cargo_build_target" ]
  }
}

# Runs a script to generate C bindings for rust library
template("cargo_cbindgen") {
  action(target_name) {
    script = "//brave/script/cargo_cbindgen.py"

    forward_variables_from(invoker,
                           [
                             "config",
                             "sources",
                             "output",
                             "visibility",
                           ])

    assert(defined(config), "Must specify cbindgen.toml config to use")
    assert(defined(sources), "Must specify sources to generate bindings for")
    assert(defined(output), "Must specify output header file")

    sources += [ config ]
    outputs = [ output ]

    args = [
      "--rustup_home=" + rebase_path(rustup_home),
      "--cargo_home=" + rebase_path(rustup_home),
      "--config=" + rebase_path(config, root_build_dir),
      "--path=" + rebase_path(get_path_info(config, "dir"), root_build_dir),
      "--output=" + rebase_path(output, root_build_dir),
    ]
  }
}
