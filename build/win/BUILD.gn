import("//brave/build/config.gni")
import("//third_party/widevine/cdm/widevine.gni")

group("brave") {
  public_deps = [
    ":copy_exe",
    ":copy_pdb",
  ]
}

copy("copy_exe") {
  sources = [
    "$root_out_dir/chrome.exe",
  ]

  outputs = [
    "$root_out_dir/brave.exe",
  ]

  deps = [
    "//chrome:reorder_imports"
  ]
}

copy("copy_pdb") {
  sources = [
    "$root_out_dir/chrome.exe.pdb",
  ]

  outputs = [
    "$root_out_dir/brave.exe.pdb",
  ]

  deps = [
    "//chrome:reorder_imports"
  ]
}

action("create_signed_installer") {
  script = "//brave/script/create-signed-installer.py"
  inputs = [
    "$root_out_dir/mini_installer.exe"
  ]
  outputs = [
    "$root_out_dir/brave_installer.exe",
    "$root_out_dir/$brave_installer_exe",
  ]
  out_dir = rebase_path(root_out_dir, "//")
  args = [
    "--root_out_dir=$out_dir",
    "--brave_installer_exe=$brave_installer_exe",
  ]

  if (skip_signing) { args += [ "--skip_signing" ] }

  deps = [
    "//chrome/installer/mini_installer"
  ]
}

if (enable_widevine_cdm_host_verification) {
  widevine_sign_file("sign_brave_exe_for_widevine") {
    file = "$root_out_dir/brave.exe"
    flags = 1  # blessed binary
    signature_file = "$root_out_dir/chrome.exe.sig"
    deps = [
      ":copy_exe",
    ]
  }

  widevine_sign_file("sign_chrome_dll_for_widevine") {
    file = "$root_out_dir/chrome.dll"
    signature_file = "$root_out_dir/chrome.dll.sig"
    deps = [
      "//chrome:main_dll",
    ]
  }

  widevine_sign_file("sign_chrome_child_dll_for_widevine") {
    file = "$root_out_dir/chrome_child.dll"
    signature_file = "$root_out_dir/chrome_child.dll.sig"
    deps = [
      "//chrome:chrome_child",
    ]
  }
}
