import("//brave/build/config.gni")

config("defines") {
  defines = brave_service_key_defines
  defines += brave_updater_defines
}

config("brave_chromium_src_support") {
  # Add max priority "//brave/chromium_src" include search path to be able to
  # redirect Chromium #include into our file if it exists.
  # For example, to override //base/macros.h, the overriden file must be
  # placed at //brave/chromium_src/base/macros.h.
  include_dirs = [ "//brave/chromium_src" ]

  # Add lowest priority "//.." include search path to be able to include original
  # Chromium files using "src/" prefix.
  # Adding the search path using "cflags" instead of "include_dirs" is important,
  # because it should have the lowest priority to not break compile steps when a
  # path clash is possible: //base/macros.h vs //third_party/v8/src/base/macros.h.
  relative_root_dir = rebase_path("//..", root_build_dir)
  cflags = [ "-I${relative_root_dir}" ]
}

config("compiler") {
  configs = [
    ":brave_chromium_src_support",
    ":defines",
  ]
}

# do not expose this as a public config anywhere
config("version") {
  defines = [ "BRAVE_CHROMIUM_VERSION=\"$chrome_version_string\"" ]
}
