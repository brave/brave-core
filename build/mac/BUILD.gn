import("//brave/build/config.gni")
import("//build/config/mac/base_rules.gni")
import("//build/util/version.gni")
import("//chrome/common/features.gni")
import("//third_party/widevine/cdm/widevine.gni")

declare_args() {
  # find with `security find-identity -v -p codesigning`
  mac_signing_identifier = ""
  mac_installer_signing_identifier = ""
  mac_signing_keychain = "login"
  mac_signing_output_prefix = "signing"
}

_packaging_dir = "$root_out_dir/$chrome_product_full_name Packaging"
keychain_db = getenv("HOME") + "/Library/Keychains/${mac_signing_keychain}.keychain-db"

_target_app_path = "$root_out_dir/$mac_signing_output_prefix/" + string_replace("$chrome_product_full_name", " ", "") + "-$chrome_version_full/$brave_exe"

group("brave") {}

action("sign_app") {
  script = "//build/gn_run_binary.py"
  shell_script = "//brave/build/mac/sign_app.sh"

  deps = [
    "//brave:chrome_app",
    "//chrome/installer/mac"
  ]

  if (is_official_build) {
    if (brave_channel == "") {
      provisioning_profile = "//brave/build/mac/release.provisionprofile"
    } else {
      provisioning_profile = "//brave/build/mac/${brave_channel}.provisionprofile"
    }
    is_development = "False"
  } else {
    # an empty dummy file just to simplify things
    provisioning_profile = "//brave/build/mac/dummy.provisionprofile"
    is_development = "True"
  }

  brave_app = "$root_build_dir/$brave_exe"

  inputs = [
    shell_script,
    brave_app,
    provisioning_profile,
    "$_packaging_dir/app-entitlements.plist",
  ]
  outputs = [ _target_app_path ]
  args = [
    rebase_path(shell_script, root_out_dir),

    # Use absolute paths needed by codesign
    rebase_path("$root_build_dir"),
    rebase_path("$root_out_dir/$mac_signing_output_prefix"),
    rebase_path(_packaging_dir),
    is_development,
    rebase_path(provisioning_profile, root_out_dir),
    keychain_db,
    mac_signing_identifier,
  ]
}

action("create_pkg") {
  output = "$root_out_dir/unsigned/$brave_pkg"
  script = "//build/gn_run_binary.py"
  shell_script = "//brave/build/mac/create_pkg.sh"
  inputs = [
    script,
    shell_script,
  ]
  outputs = [ output ]
  args = [
    rebase_path(shell_script, root_build_dir),
    rebase_path(_target_app_path, root_build_dir),
    rebase_path("//brave/build/mac/pkg-scripts"),
    rebase_path("$root_out_dir/Brave Browser Component.plist"),
    "unsigned/$brave_pkg",
  ]

  deps = [":sign_app"]
}

action("sign_pkg") {
  script = "//build/gn_run_binary.py"
  shell_script = "sign_pkg.sh"
  inputs = [
    script,
    shell_script,
    "$root_out_dir/unsigned/$brave_pkg",
  ]
  outputs = [ "${root_out_dir}/$mac_signing_output_prefix/$brave_pkg" ]
  args = [
    rebase_path(shell_script, root_build_dir),
    rebase_path("$root_out_dir/unsigned/$brave_pkg"),
    rebase_path("$root_out_dir/$brave_pkg"),
    keychain_db,
    mac_installer_signing_identifier,
  ]

  deps = [":create_pkg"]
}

action("create_dmg") {
  output = "$root_out_dir/unsigned/$brave_dmg"
  script = "//build/gn_run_binary.py"
  shell_script = "//chrome/installer/mac/pkg-dmg"
  inputs = [
    script,
    shell_script,
  ]
  outputs = [ output ]
  args = [
    rebase_path(shell_script, root_build_dir),
    "--source", "/var/empty",
    "--target", "unsigned/$brave_dmg",
    "--format", "UDBZ",
    "--verbosity", "0",
    "--volname", "$chrome_product_full_name",
    "--copy", rebase_path(_target_app_path, root_build_dir),
    "--tempdir", "/tmp",
    "--symlink", "/Applications",
  ]

  deps = [":sign_app"]
}

action("create_unsigned_dmg") {
  brave_app = "$root_build_dir/$brave_exe"
  output = "$root_out_dir/unsigned_dmg/$brave_dmg"
  script = "//build/gn_run_binary.py"
  shell_script = "//chrome/installer/mac/pkg-dmg"
  inputs = [
    script,
    shell_script,
  ]
  outputs = [ output ]
  args = [
    rebase_path(shell_script, root_build_dir),
    "--source", "/var/empty",
    "--target", "unsigned_dmg/$brave_dmg",
    "--format", "UDBZ",
    "--verbosity", "0",
    "--volname", "$chrome_product_full_name",
    "--copy", rebase_path(brave_app, root_build_dir),
    "--tempdir", "/tmp",
    "--symlink", "/Applications",
  ]

  deps = [
    "//brave:chrome_app",
    "//chrome/installer/mac"
  ]

  if (enable_widevine_cdm_host_verification) {
    deps += [ ":sign_chrome_framework_for_widevine" ]
  }
}

action("sign_dmg") {
  script = "//build/gn_run_binary.py"
  shell_script = "sign_dmg.sh"
  inputs = [
    script,
    shell_script,
    "$root_out_dir/unsigned/$brave_dmg",
  ]
  outputs = [ "${root_out_dir}/$mac_signing_output_prefix/$brave_dmg" ]
  args = [
    rebase_path(shell_script, root_build_dir),
    rebase_path("$root_out_dir/unsigned/$brave_dmg"),
    rebase_path("$root_out_dir/$brave_dmg"),
    keychain_db,
    mac_signing_identifier,
    "-r=designated => identifier \"$chrome_product_full_name\" and certificate leaf = H\"$mac_signing_identifier\"",
  ]

  deps = [":create_dmg"]
}

if (enable_widevine_cdm_host_verification) {
  action("sign_chrome_framework_for_widevine") {
    script = "//third_party/widevine/scripts/signature_generator.py"

    chrome_framework_name = chrome_product_full_name + " Framework"
    target_app_path = "$_target_app_path"
    if (skip_signing) {
      target_app_path = "$root_out_dir/$chrome_product_full_name.app"
    }

    if (new_mac_bundle_structure) {
      brave_version_path = "$target_app_path/Contents/Frameworks/$chrome_framework_name.framework/Versions/$chrome_version_full"
      file = "$brave_version_path/$chrome_framework_name"
      signature_file = "$brave_version_path/Resources/$chrome_framework_name.sig"
    } else {
      brave_version_path = "$target_app_path/Contents/Versions/$chrome_version_full"
      file = "$brave_version_path/$chrome_framework_name.framework/Versions/Current/$chrome_framework_name"
      signature_file = "$brave_version_path/Widevine Resources.bundle/Contents/Resources/$chrome_framework_name.sig"
    }

    flags = 1  # blessed binary

    sources = [
      "$root_out_dir/$brave_exe"
    ]

    outputs = [
      "$signature_file",
    ]

    args = [
      "--input_file",
      rebase_path("$file", root_build_dir),
      "--output_file",
      rebase_path("$signature_file", root_build_dir),
      "--flags",
      "$flags",
      "--certificate",
      getenv("SIGN_WIDEVINE_CERT"),
      "--private_key",
      getenv("SIGN_WIDEVINE_KEY"),
      "--private_key_passphrase",
      getenv("SIGN_WIDEVINE_PASSPHRASE"),
    ]

    deps = [ "//brave:chrome_app" ]

    if (!skip_signing) {
      deps += [ ":sign_app" ]
    }
  }
}

group("create_dist_mac") {
  deps = [ ":sign_dmg" ]
}
