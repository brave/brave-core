# Copyright (c) 2024 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/build/config.gni")
import("//brave/build/mac/config.gni")
import("//brave/updater/config.gni")
import("//build/util/branding.gni")
import("//chrome/common/features.gni")
import("//chrome/updater/branding.gni")
import("//chrome/version.gni")
import("//components/crx_file/crx3.gni")
import("//third_party/widevine/cdm/widevine.gni")

packaging_dir = "$brave_packaging_base_dir/" +
                string_replace("$chrome_product_full_name Packaging", " ", "_")

unsigned_app_path = "$root_out_dir/$brave_exe"
if (is_universal_binary) {
  arm64_arch_app_path = unsigned_app_path
  x64_arch_app_path = "$root_out_dir/x64/$brave_exe"
  unsigned_app_path = "$brave_packaging_base_dir/$brave_exe"
}

unsigned_dmg_path = "$root_out_dir/$mac_unsigned_output_prefix/$brave_dmg"
unsigned_pkg_path = "$root_out_dir/$mac_unsigned_output_prefix/$brave_pkg"

signed_app_path =
    "$root_out_dir/$mac_signing_output_prefix/$brave_channel/$brave_exe"
if (is_release_channel) {
  signed_app_path = "$root_out_dir/$mac_signing_output_prefix/stable/$brave_exe"
}

signed_dmg_path = "$root_out_dir/$mac_signed_output_prefix/$brave_dmg"
signed_pkg_path = "$root_out_dir/$mac_signed_output_prefix/$brave_pkg"

app_path = signed_app_path
if (skip_signing) {
  app_path = unsigned_app_path
}

notarized_dmg_path = "$root_out_dir/$mac_notarized_output_prefix/$brave_dmg"
notarized_pkg_path = "$root_out_dir/$mac_notarized_output_prefix/$brave_pkg"

target_dmg_path = "$root_out_dir/$brave_dmg"

if (is_universal_binary) {
  action("universalize") {
    script = "//chrome/installer/mac/universalizer.py"

    args = [
      rebase_path(arm64_arch_app_path),
      rebase_path(x64_arch_app_path),
      rebase_path(unsigned_app_path),
    ]

    inputs = [
      script,
      arm64_arch_app_path,
      x64_arch_app_path,
    ]

    outputs = [ unsigned_app_path ]

    public_deps = [
      ":copy_x64",
      "//chrome:chrome_app",
    ]
  }

  # placeholder script to allow manual copying of binary into output dir
  action("copy_x64") {
    script = "//brave/installer/mac/copy_x64_binary.py"

    x64_root_out_dir = string_replace(root_out_dir, "_arm64", "")
    x64_arch_src_path = "$x64_root_out_dir/$brave_exe"

    args = [
      rebase_path(x64_arch_src_path),
      rebase_path(x64_arch_app_path),
    ]

    inputs = [
      script,
      x64_arch_src_path,
    ]

    outputs = [ x64_arch_app_path ]
  }
} else {
  group("universalize") {
    public_deps = [ "//chrome:chrome_app" ]
  }
}

group("brave") {
  exe_path = "$app_path/Contents/MacOS/$chrome_product_full_name"
  write_file("$root_out_dir/brave_helper",
             [ string_replace(rebase_path(exe_path), " ", "\\ ") ])
}

ds_store_file_name = "DS_Store"
dmg_icon_file_name = "dmg"

if (!is_release_channel) {
  ds_store_file_name = "DS_Store.$brave_channel"
  dmg_icon_file_name = "dmg-$brave_channel"
}

dmg_background_image = rebase_path("//brave/build/mac/dmg-background.png")
ds_store = rebase_path("//brave/build/mac/$ds_store_file_name")
dmg_icon = rebase_path("//brave/build/mac/$dmg_icon_file_name.icns")

action("generate_dsa_sig_for_dmg") {
  script = "generate_sig.py"

  if (build_sparkle) {
    sign_update_path = "$root_out_dir/old_dsa_scripts/sign_update"
  } else {
    sign_update_path = "//build/mac_files/sparkle_binaries/sign_update_dsa"
  }
  output_dmg_dsa_path = "$root_out_dir/$brave_dmg.dsa"

  args = [
    "--sign-update",
    rebase_path("$sign_update_path"),
    "--sign-key-file",
    "$sparkle_dsa_private_key_file",
    "--target",
    rebase_path("$target_dmg_path"),
    "--output",
    rebase_path("$output_dmg_dsa_path"),
    "--dsa",
  ]

  inputs = [
    script,
    "$sign_update_path",
    "$target_dmg_path",
  ]

  outputs = [ "$output_dmg_dsa_path" ]

  deps = [ ":copy_dmg_pkg" ]

  if (build_sparkle) {
    deps += [ "//brave/vendor/sparkle:copy_old_sign_update" ]
  }
}

action("generate_eddsa_sig_for_dmg") {
  script = "generate_sig.py"

  if (build_sparkle) {
    sign_update_path = "$root_out_dir/sign_update"
  } else {
    sign_update_path = "//build/mac_files/sparkle_binaries/sign_update"
  }

  output_dmg_eddsa_path = "$root_out_dir/$brave_dmg.eddsa"

  args = [
    "--sign-update",
    rebase_path("$sign_update_path"),
    "--sign-key",
    "$sparkle_eddsa_private_key",
    "--target",
    rebase_path("$target_dmg_path"),
    "--output",
    rebase_path("$output_dmg_eddsa_path"),
    "--eddsa",
  ]

  inputs = [
    script,
    "$sign_update_path",
    "$target_dmg_path",
  ]

  outputs = [ "$output_dmg_eddsa_path" ]

  deps = [ ":copy_dmg_pkg" ]

  if (build_sparkle) {
    deps += [ "//brave/vendor/sparkle:build_sparkle_framework" ]
  }
}

action("build_delta_installer") {
  script = "build_delta_installer.py"

  if (build_sparkle) {
    binary_delta_path = "$root_out_dir/BinaryDelta"
  } else {
    binary_delta_path = "//build/mac_files/sparkle_binaries/BinaryDelta"
  }

  output_delta_path = "$root_out_dir/$brave_delta"

  args = [
    "--binary-delta",
    rebase_path("$binary_delta_path"),
    "--root-out-dir",
    rebase_path("$root_out_dir"),
    "--old-dmg",
    rebase_path("$root_out_dir/$last_chrome_installer"),
    "--new-dmg",
    rebase_path("$target_dmg_path"),
    "--delta-output",
    rebase_path("$output_delta_path"),
  ]

  inputs = [
    script,
    "$binary_delta_path",
    "$target_dmg_path",
  ]

  outputs = [ "$output_delta_path" ]

  deps = [ ":copy_dmg_pkg" ]

  if (build_sparkle) {
    deps += [ "//brave/vendor/sparkle:build_sparkle_framework" ]
  }
}

action("generate_eddsa_sig_for_delta") {
  script = "generate_sig.py"

  sign_update_path = "//build/mac_files/sparkle_binaries/sign_update"
  target_delta_path = "$root_out_dir/$brave_delta"
  output_delta_eddsa_path = "$root_out_dir/$brave_delta.eddsa"

  args = [
    "--sign-update",
    rebase_path("$sign_update_path"),
    "--sign-key",
    "$sparkle_eddsa_private_key",
    "--target",
    rebase_path("$target_delta_path"),
    "--output",
    rebase_path("$output_delta_eddsa_path"),
    "--eddsa",
  ]

  inputs = [
    script,
    "$sign_update_path",
    "$target_delta_path",
  ]

  outputs = [ "$output_delta_eddsa_path" ]

  deps = [ ":build_delta_installer" ]
}

group("finalize_app") {
  public_deps = [ "//chrome/installer/mac" ]

  if (skip_signing) {
    public_deps += [ ":universalize" ]
  } else {
    public_deps += [ ":sign_app" ]
  }
}

action("sign_app") {
  script = "//build/gn_run_binary.py"
  shell_script = "//brave/build/mac/sign_app.sh"

  if (is_official_build) {
    if (is_release_channel) {
      provisioning_profile = "//brave/build/mac/release.provisionprofile"
    } else {
      provisioning_profile =
          "//brave/build/mac/${brave_channel}.provisionprofile"
    }
    is_development = "False"
  } else {
    # an empty dummy file just to simplify things
    provisioning_profile = "//brave/build/mac/dummy.provisionprofile"
    is_development = "True"
  }

  notarize_argument = "False"
  if (notarize) {
    notarize_argument = "True"
  }

  args = [
    rebase_path(shell_script, root_out_dir),

    # Use absolute paths needed by codesign
    rebase_path(get_path_info(unsigned_app_path, "dir")),
    rebase_path("$root_out_dir/$mac_signing_output_prefix"),
    rebase_path(packaging_dir),
    is_development,
    rebase_path(provisioning_profile, root_out_dir),
    mac_signing_identifier,
    notarize_argument,
    notary_user,
    notary_password,
    chrome_mac_team_id,
  ]

  inputs = [
    script,
    shell_script,
    unsigned_app_path,
    provisioning_profile,
    "$packaging_dir/app-entitlements.plist",
    "//brave/script/signing_helper.py",
  ]

  outputs = [ signed_app_path ]

  deps = [
    ":copies",
    ":universalize",
  ]

  if (brave_enable_updater) {
    deps += [ ":updater_parts_script" ]
  }
}

copy("copies") {
  visibility = [ ":sign_app" ]

  sources = [ "//brave/app/helper-entitlements.plist" ]

  public_deps = [ "//chrome/installer/mac" ]

  outputs = [ "$packaging_dir/{{source_file_part}}" ]
}

copy("updater_parts_script") {
  visibility = [ ":sign_app" ]

  sources = [ "//chrome/updater/mac/signing/parts.py" ]

  outputs = [ "$packaging_dir/signing/updater_parts.py" ]
}

if (brave_enable_updater) {
  generated_pkg_scripts = "$target_gen_dir/pkg-scripts"
}

action("create_pkg") {
  script = "//build/gn_run_binary.py"
  shell_script = "//brave/build/mac/create_pkg.sh"

  args = [
    rebase_path(shell_script, root_build_dir),
    rebase_path(app_path, root_build_dir),
  ]

  if (brave_enable_updater) {
    args += [ rebase_path(generated_pkg_scripts) ]
  } else {
    args += [ rebase_path("//brave/build/mac/pkg-scripts") ]
  }

  args += [
    rebase_path("$root_out_dir/Brave Browser Component.plist"),
    "$mac_unsigned_output_prefix/$brave_pkg",
  ]

  inputs = [
    script,
    app_path,
    shell_script,
  ]

  outputs = [ unsigned_pkg_path ]

  deps = [ ":finalize_app" ]

  if (brave_enable_updater) {
    deps += [ ":postinstall" ]
  }
}

if (brave_enable_updater) {
  action("postinstall") {
    visibility = [ ":create_pkg" ]

    script = "create_postinstall.py"

    output = "$generated_pkg_scripts/postinstall"

    args = [
      rebase_path("//chrome/installer/mac/pkg_postinstall.in"),
      brave_exe,
      "Contents/Frameworks/$chrome_product_full_name Framework.framework",
      rebase_path(output),
    ]

    inputs = [
      script,
      "//chrome/installer/mac/pkg_postinstall.in",
    ]

    outputs = [ output ]
  }
}

action("sign_pkg") {
  script = "//build/gn_run_binary.py"
  shell_script = "sign_pkg.sh"

  args = [
    rebase_path(shell_script, root_build_dir),
    rebase_path(unsigned_pkg_path),
    rebase_path(signed_pkg_path),
    keychain_db,
    mac_installer_signing_identifier,
  ]

  inputs = [
    script,
    shell_script,
    unsigned_pkg_path,
  ]

  outputs = [ "${root_out_dir}/$mac_signed_output_prefix/$brave_pkg" ]

  deps = [ ":create_pkg" ]
}

action("create_dmg") {
  if (host_os == "mac") {
    script = "//build/gn_run_binary.py"
    shell_script = "//chrome/installer/mac/pkg-dmg"
    args = [ rebase_path(shell_script, root_build_dir) ]
    inputs = [ shell_script ]
  } else {
    script = "cross-compile/pkg-dmg-linux.py"
    args = []
    inputs = []
  }

  args += [
    "--source",
    "/var/empty",
    "--target",
    "$mac_unsigned_output_prefix/$brave_dmg",
    "--format",
    "ULMO",
    "--verbosity",
    "2",
    "--volname",
    "$chrome_product_full_name",
    "--copy",
    rebase_path(app_path, root_build_dir),
    "--tempdir",
    "/tmp",
    "--mkdir",
    ".background",
    "--copy",
    "$dmg_background_image:/.background/dmg-background.png",
    "--copy",
    "$ds_store:/.DS_Store",
    "--icon",
    "$dmg_icon",
    "--symlink",
    "/Applications",
  ]

  inputs += [
    script,
    app_path,
  ]

  outputs = [ unsigned_dmg_path ]

  deps = [ ":finalize_app" ]

  if (brave_enable_updater) {
    args += [
      "--copy",
      rebase_path(
          "$root_out_dir/$browser_product_name Packaging/updater/.install",
          root_build_dir),
    ]
    deps += [ "//chrome/updater/mac:browser_install_script" ]
  }
}

action("sign_dmg") {
  script = "//build/gn_run_binary.py"
  shell_script = "sign_dmg.sh"

  args = [
    rebase_path(shell_script, root_build_dir),
    rebase_path(unsigned_dmg_path),
    rebase_path(signed_dmg_path),
    keychain_db,
    mac_signing_identifier,
    "-r=designated => identifier \"$chrome_product_full_name\" and certificate leaf = H\"$mac_signing_identifier\"",
  ]

  inputs = [
    script,
    shell_script,
    unsigned_dmg_path,
  ]

  outputs = [ "${root_out_dir}/$mac_signed_output_prefix/$brave_dmg" ]

  deps = [ ":create_dmg" ]
}

group("finalize_dmg_pkg") {
  deps = []
  if (skip_signing) {
    deps += [ ":create_dmg" ]
  } else {
    if (mac_installer_signing_identifier == "") {
      deps += [ ":sign_app" ]
    } else {
      deps += [ ":copy_dmg_pkg" ]
      if (sparkle_dsa_private_key_file != "") {
        deps += [ ":generate_dsa_sig_for_dmg" ]
      }

      if (sparkle_eddsa_private_key != "") {
        deps += [ ":generate_eddsa_sig_for_dmg" ]
      }

      if (build_delta_installer && last_chrome_installer != "") {
        deps += [ ":build_delta_installer" ]
        if (sparkle_eddsa_private_key != "") {
          deps += [ ":generate_eddsa_sig_for_delta" ]
        }
      }
    }
  }
}

copy("copy_notarize_script") {
  sources = [ "notarize_dmg_pkg.py" ]

  outputs = [ "$packaging_dir/{{source_file_part}}" ]
}

action("notarize_dmg_pkg") {
  script = "$packaging_dir/notarize_dmg_pkg.py"

  if (is_official_build) {
    development_arg = ""
    if (is_release_channel) {
      provisioning_profile = "//brave/build/mac/release.provisionprofile"
    } else {
      provisioning_profile =
          "//brave/build/mac/${brave_channel}.provisionprofile"
    }
  } else {
    development_arg = "--development"
    provisioning_profile = "//brave/build/mac/dummy.provisionprofile"
  }

  args = [
    "--identity",
    mac_signing_identifier,
    string_join("=",
                [
                  "--notary-arg",
                  "--apple-id",
                  notary_user,
                ]),
    "--notary-arg=--password",
    "--notary-arg",
    notary_password,
    "--notary-arg=--team-id",
    "--notary-arg",
    chrome_mac_team_id,
    development_arg,
    "--dmg",
    rebase_path(signed_dmg_path),
    "--pkg",
    rebase_path(signed_pkg_path),
    "--signed",
    rebase_path("$root_out_dir/$mac_signed_output_prefix"),
    "--outdir",
    rebase_path("$root_out_dir/$mac_notarized_output_prefix"),
    "--pkgdir",
    rebase_path(packaging_dir),
    "--mac_provisioning_profile",
    rebase_path(provisioning_profile),
  ]

  inputs = [
    script,
    signed_dmg_path,
    signed_pkg_path,
  ]

  outputs = [
    notarized_dmg_path,
    notarized_pkg_path,
  ]

  deps = [
    ":copy_notarize_script",
    ":sign_dmg",
    ":sign_pkg",
  ]
}

copy("copy_dmg_pkg") {
  if (notarize) {
    sources = [
      notarized_dmg_path,
      notarized_pkg_path,
    ]
    deps = [
      ":copy_notarize_script",
      ":notarize_dmg_pkg",
    ]
  } else if (skip_signing) {
    sources = [
      unsigned_dmg_path,
      unsigned_pkg_path,
    ]
    deps = [
      ":create_dmg",
      ":create_pkg",
      ":finalize_dmg_pkg",
    ]
  } else {
    sources = [
      signed_dmg_path,
      signed_pkg_path,
    ]
    deps = [
      ":sign_dmg",
      ":sign_pkg",
    ]
  }

  outputs = [ "$root_out_dir/{{source_file_part}}" ]
}

if (brave_enable_updater) {
  crx3("crx") {
    inputs = [ target_dmg_path ]
    output = "$root_out_dir/$chrome_product_full_name.crx3"
    base_dir = get_path_info(target_dmg_path, "dir")

    # The private key file is not in Git for obvious reasons. It needs to be
    # copied into this directory manually. Changing the private key requires
    # also changing kBravePublisherKeyHash in crx_verifier.cc.
    key = "//brave/updater/crx-private-key.der"
    deps = [ ":copy_dmg_pkg" ]
  }
  action("sign_updater") {
    deps = [
      "//chrome/updater/mac:updater_bundle",
      "//chrome/updater/mac:updater_install_script",
      "//chrome/updater/mac/installer:metainstaller",
      "//chrome/updater/mac/signing",
      "//chrome/updater/tools:BraveUpdaterUtil",
    ]
    script = "sign_updater.py"
    output_dir = "$root_out_dir/$mac_signed_output_prefix"
    output_file = "$updater_product_full_name-$chrome_version_full.zip"
    args = [
      rebase_path("$root_out_dir/Updater Packaging/sign_updater.py",
                  root_build_dir),
      rebase_path(output_dir),
      output_file,
      "--input",
      rebase_path(root_out_dir),
      "--identity",
      mac_signing_identifier,
      "--installer-identity",
      mac_installer_signing_identifier,
    ]
    if (notarize) {
      args += [
        "--notarize",
        "--notary-arg=--apple-id",
        "--notary-arg=$notary_user",
        "--notary-arg=--password",
        "--notary-arg=$notary_password",
        "--notary-arg=--team-id",
        "--notary-arg=$chrome_mac_team_id",
      ]
    }
    outputs = [ "$output_dir/$output_file" ]
    inputs = [ script ]
  }
}
