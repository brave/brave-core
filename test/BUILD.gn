import("//brave/build/config.gni")
import("//brave/components/brave_rewards/browser/buildflags/buildflags.gni")
import("//testing/test.gni")
import("//third_party/widevine/cdm/widevine.gni")

group("fix_brave_test_install_name") {
  testonly = true
  deps = [
    "//brave/vendor/challenge_bypass_ristretto_ffi:test_install_name"
  ]
}

static_library("brave_test_support_unit") {
  testonly = true
  sources = [
    "base/brave_unit_test_suite.cc",
    "base/brave_unit_test_suite.h",
    "base/run_all_unittests.cc",
  ]

  public_deps = [
    "//chrome/test:test_support",
    "//base",
    "//chrome:resources",
    "//chrome:strings",
    "//chrome/browser",
    "//chrome/common",
  ]

  if (is_win) {
    deps = [
      "//chrome/install_static/test:test_support",
    ]
  }
}

test("brave_unit_tests") {
  sources = [
    "//brave/app/brave_command_line_helper_unittest.cc",
    "//brave/browser/autocomplete/brave_autocomplete_provider_client_unittest.cc",
    "//brave/browser/autoplay/autoplay_permission_context_unittest.cc",
    "//brave/browser/brave_resources_util_unittest.cc",
    "//brave/browser/brave_stats_updater_unittest.cc",
    "//brave/browser/download/brave_download_item_model_unittest.cc",
    "//brave/browser/ui/views/brave_status_bubble_views_unittest.cc",
    "//brave/browser/tor/mock_tor_profile_service_impl.cc",
    "//brave/browser/tor/mock_tor_profile_service_impl.h",
    "//brave/browser/tor/mock_tor_profile_service_factory.cc",
    "//brave/browser/tor/mock_tor_profile_service_factory.h",
    "//brave/browser/metrics/metrics_reporting_util_unittest_linux.cc",
    "//brave/browser/net/brave_ad_block_tp_network_delegate_helper_unittest.cc",
    "//brave/browser/net/brave_common_static_redirect_network_delegate_helper_unittest.cc",
    "//brave/browser/net/brave_httpse_network_delegate_helper_unittest.cc",
    "//brave/browser/net/brave_network_delegate_base_unittest.cc",
    "//brave/browser/net/brave_referrals_network_delegate_helper_unittest.cc",
    "//brave/browser/net/brave_site_hacks_network_delegate_helper_unittest.cc",
    "//brave/browser/net/brave_static_redirect_network_delegate_helper_unittest.cc",
    "//brave/browser/net/brave_tor_network_delegate_helper_unittest.cc",
    "//brave/browser/profiles/tor_unittest_profile_manager.cc",
    "//brave/browser/profiles/tor_unittest_profile_manager.h",
    "//brave/browser/profiles/brave_profile_manager_unittest.cc",
    "//brave/browser/resources/settings/reset_report_uploader_unittest.cc",
    "//brave/browser/resources/settings/brandcode_config_fetcher_unittest.cc",
    "//brave/browser/themes/brave_theme_service_unittest.cc",
    "//brave/chromium_src/chrome/browser/external_protocol/external_protocol_handler_unittest.cc",
    "//brave/chromium_src/chrome/browser/history/history_utils_unittest.cc",
    "//brave/chromium_src/chrome/browser/shell_integration_unittest_mac.cc",
    "//brave/chromium_src/chrome/browser/signin/account_consistency_disabled_unittest.cc",
    "//brave/chromium_src/chrome/browser/ui/bookmarks/brave_bookmark_context_menu_controller_unittest.cc",
    "//brave/chromium_src/components/metrics/enabled_state_provider_unittest.cc",
    "//brave/chromium_src/components/search_engines/brave_template_url_prepopulate_data_unittest.cc",
    "//brave/chromium_src/components/search_engines/brave_template_url_service_util_unittest.cc",
    "//brave/chromium_src/components/version_info/brave_version_info_unittest.cc",
    "//brave/chromium_src/net/cookies/brave_canonical_cookie_unittest.cc",
    "//brave/common/brave_content_client_unittest.cc",
    "//brave/common/importer/brave_mock_importer_bridge.cc",
    "//brave/common/importer/brave_mock_importer_bridge.h",
    "//brave/common/shield_exceptions_unittest.cc",
    "//brave/common/tor/tor_test_constants.cc",
    "//brave/common/tor/tor_test_constants.h",
    "//brave/components/assist_ranker/ranker_model_loader_impl_unittest.cc",
    "//brave/components/brave_shields/browser/ad_block_regional_service_unittest.cc",
    "//brave/components/brave_shields/browser/https_everywhere_recently_used_cache_unittest.cpp",
    "//brave/components/brave_sync/bookmark_order_util_unittest.cc",
    "//brave/components/brave_sync/brave_sync_service_unittest.cc",
    "//brave/components/brave_sync/client/bookmark_change_processor_unittest.cc",
    "//brave/components/brave_webtorrent/browser/net/brave_torrent_redirect_network_delegate_helper_unittest.cc",
    "//brave/components/invalidation/fcm_unittest.cc",
    "//brave/components/gcm_driver/gcm_unittest.cc",
    "//brave/components/invalidation/push_client_channel_unittest.cc",
    "//brave/components/omnibox/browser/topsites_provider_unittest.cc",
    "//brave/components/rappor/log_uploader_unittest.cc",
    "//brave/third_party/libaddressinput/chromium/chrome_metadata_source_unittest.cc",
    "//chrome/common/importer/mock_importer_bridge.cc",
    "//chrome/common/importer/mock_importer_bridge.h",
    "../browser/importer/chrome_profile_lock_unittest.cc",
    "../utility/importer/chrome_importer_unittest.cc",
    "../utility/importer/brave_importer_unittest.cc",
    "../utility/importer/firefox_importer_unittest.cc",
    "../../components/domain_reliability/test_util.cc",
    "../../components/domain_reliability/test_util.h",
  ]

  if (brave_rewards_enabled) {
    sources += [
      "//brave/vendor/bat-native-ledger/src/bat/ledger/internal/bat_get_media_unittest.cc",
      "//brave/vendor/bat-native-ledger/src/bat/ledger/internal/bat_get_media_unittest.h",
      "//brave/vendor/bat-native-ledger/src/bat/ledger/internal/bat_helper_unittest.cc",
      "//brave/vendor/bat-native-ledger/src/bat/ledger/internal/bat_helper_unittest.h",
      "//brave/vendor/bat-native-ledger/src/bat/ledger/internal/bat_publishers_unittest.cc",
      "//brave/vendor/bat-native-ledger/src/bat/ledger/internal/bat_publishers_unittest.h",
      "//brave/vendor/bat-native-ledger/src/bat/ledger/internal/test/niceware_partial_unittest.cc",
      "//brave/components/brave_rewards/browser/publisher_info_database_unittest.cc",
      "//brave/components/brave_rewards/browser/rewards_service_impl_unittest.cc",
      "//brave/vendor/bat-native-ads/src/bat/ads/internal/ads_is_mobile_unittest.cc",
      "//brave/vendor/bat-native-ads/src/bat/ads/internal/ads_tabs_unittest.cc",
      "//brave/vendor/bat-native-ads/src/bat/ads/internal/ads_client_mock.cc",
      "//brave/vendor/bat-native-ads/src/bat/ads/internal/ads_client_mock.h",
      "//brave/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_create_confirmation_request_unittest.cc",
      "//brave/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_fetch_payment_token_request_unittest.cc",
      "//brave/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_get_signed_tokens_request_unittest.cc",
      "//brave/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_redeem_payment_tokens_request_unittest.cc",
      "//brave/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_request_signed_tokens_request_unittest.cc",
      "//brave/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_security_helper_unittest.cc",
      "//brave/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_string_helper_unittest.cc",
      "//brave/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_unblinded_tokens_unittest.cc",
      "//brave/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_client_mock.cc",
      "//brave/vendor/bat-native-confirmations/src/bat/confirmations/internal/confirmations_client_mock.h",
      "//brave/vendor/bat-native-usermodel/test/usermodel_unittest.cc",
    ]
  }

  # On Windows, brave_install_static_unittests covers channel test.
  if (is_mac || is_linux) {
    sources += [
      "//brave/common/brave_channel_info_unittest.cc",
    ]
  }

  if (is_win || is_mac) {
    sources += [
      "//brave/chromium_src/chrome/browser/component_updater/widevine_cdm_component_installer_unittest.cc",
    ]
  }

  if (is_mac) {
    sources += [
      "//brave/chromium_src/chrome/common/chrome_constants_unittest_mac.cc",
    ]
  }

  data = [
    "data/",
  ]

  configs += [
    "//brave/build/geolocation",
  ]

  if (is_linux) {
    configs += [
      "//brave/build/linux:linux_channel_names",
    ]
  }

  deps = [
    "//brave/components/brave_rewards/browser:testutil",
    "//brave/components/brave_sync:testutil",
    "//brave/vendor/bat-native-rapidjson",
    "//chrome:browser_dependencies",
    "//chrome:child_dependencies",
    "//chrome/test:test_support",
    "//components/prefs",
    "//components/prefs:test_support",
    "//components/version_info",
    "//content/test:test_support",
    "//components/signin/core/browser",
    "//components/sync_preferences",
    "//content/public/common",
    "//third_party/cacheinvalidation",
  ]

  if (brave_rewards_enabled) {
    deps += [
      "//brave/vendor/bat-native-usermodel",
      "//brave/vendor/bat-native-ads",
      "//brave/vendor/bat-native-confirmations",
      "//brave/vendor/challenge_bypass_ristretto_ffi",
      "//brave/vendor/bat-native-ledger",
    ]

    configs += [ "//brave/vendor/bat-native-ledger:internal_config" ]
    configs += [ "//brave/vendor/bat-native-ads:internal_config" ]
    configs += [ "//brave/vendor/bat-native-confirmations:internal_config" ]
  }

  if (bundle_widevine_cdm) {
    sources += [
      "//brave/browser/widevine/brave_widevine_bundle_manager_unittest.cc",
    ]
    deps += [ "//third_party/widevine/cdm:headers" ]
  }

  public_deps = [
    "//base",
    "//base/test:test_support",
    "//brave:browser_dependencies",
    "//brave/browser",
    "//brave/common",
    "//brave/components/content_settings/core/browser",
    "//brave/renderer",
    "//brave/utility",
    ":brave_test_support_unit",
    "//testing/gtest",
  ]

  deps += [
    "//brave/browser/safebrowsing",
  ]

  include_dirs = []

  if (is_win) {
    sources += [
      "//brave/chromium_src/chrome/install_static/brave_install_details_unittest.cc",
      "//brave/chromium_src/chrome/install_static/brave_install_modes_unittest.cc",
      "//brave/chromium_src/chrome/install_static/brave_install_util_unittest.cc",
      "//brave/chromium_src/chrome/install_static/brave_product_install_details_unittest.cc",
      "//brave/chromium_src/chrome/install_static/brave_user_data_dir_win_unittest.cc",
      "//brave/chromium_src/chrome/installer/mini_installer/brave_mini_installer_unittest.cc",
    ]
    include_dirs += [ "$target_gen_dir" ]
    deps += [
      "//base",
      "//base/test:run_all_unittests",
      "//base/test:test_support",
      "//chrome/install_static:install_static_util",
      "//chrome/install_static/test:test_support",
      "//chrome/installer/mini_installer:lib",
      "//testing/gmock",
      "//testing/gtest",
    ]
  }
}

group("brave_browser_tests_deps") {
  if (brave_chromium_build) {
    # force these to build for tests
    deps = [
      "//brave:browser_dependencies",
      "//brave:child_dependencies",
      "//brave:brave",
    ]
  }
}

static_library("browser_test_support") {
  testonly = true
  deps = [
    "//chrome/test:test_support",
  ]

  sources = [
    "base/brave_test_launcher_delegate.cc",
    "base/brave_test_launcher_delegate.h",
  ]
}

static_library("browser_tests_runner") {
  testonly = true

  deps = [
    ":browser_test_support",
  ]

  sources = [
    "base/browser_tests_main.cc",
  ]
}

test("brave_browser_tests") {
  sources = [
    "//brave/app/brave_main_delegate_browsertest.cc",
    "//brave/browser/autocomplete/brave_autocomplete_provider_client_browsertest.cc",
    "//brave/browser/brave_scheme_load_browsertest.cc",
    "//brave/chromium_src/chrome/browser/google/chrome_google_url_tracker_client_browsertest.cc",
    "//brave/chromium_src/components/content_settings/core/browser/brave_content_settings_registry_browsertest.cc",
    "//brave/chromium_src/third_party/blink/renderer/modules/battery/navigator_batterytest.cc",
    "//brave/chromium_src/third_party/blink/renderer/modules/bluetooth/navigator_bluetoothtest.cc",
    "//brave/chromium_src/third_party/blink/renderer/modules/plugins/navigator_pluginstest.cc",
    "//brave/common/extensions/brave_pdf_download_browsertest.cc",
    "//brave/browser/autoplay/autoplay_permission_context_browsertest.cc",
    "//brave/browser/brave_content_browser_client_browsertest.cc",
    "//brave/browser/brave_features_browsertest.cc",
    "//brave/browser/brave_profile_prefs_browsertest.cc",
    "//brave/browser/brave_resources_browsertest.cc",
    "//brave/browser/brave_stats_updater_browsertest.cc",
    "//brave/browser/browsing_data/brave_clear_browsing_data_browsertest.cc",
    "//brave/browser/devtools/brave_devtools_ui_bindings_browsertest.cc",
    "//brave/browser/extensions/brave_tor_client_updater_browsertest.cc",
    "//brave/browser/extensions/brave_extension_functional_test.cc",
    "//brave/browser/extensions/brave_extension_functional_test.h",
    "//brave/browser/extensions/api/brave_shields_api_browsertest.cc",
    "//brave/browser/extensions/api/brave_theme_api_browsertest.cc",
    "//brave/browser/extensions/brave_theme_event_router_browsertest.cc",
    "//brave/browser/net/brave_network_delegate_browsertest.cc",
    "//brave/browser/net/brave_network_delegate_hsts_fingerprinting_browsertest.cc",
    "//brave/browser/renderer_context_menu/brave_mock_render_view_context_menu.cc",
    "//brave/browser/renderer_context_menu/brave_mock_render_view_context_menu.h",
    "//brave/browser/renderer_context_menu/brave_spelling_menu_observer_browsertest.cc",
    "//brave/browser/search_engines/search_engine_provider_service_browsertest.cc",
    "//brave/browser/ui/content_settings/brave_autoplay_blocked_image_model_browsertest.cc",
    "//brave/browser/ui/views/brave_actions/brave_actions_container_browsertest.cc",
    "//brave/browser/ui/views/profiles/brave_profile_chooser_view_browsertest.cc",
    "//brave/browser/ui/webui/brave_new_tab_ui_browsertest.cc",
    "//brave/browser/ui/webui/brave_welcome_ui_browsertest.cc",
    "//brave/browser/ui/toolbar/brave_app_menu_model_browsertest.cc",
    "//brave/common/brave_channel_info_browsertest.cc",
    "//brave/components/brave_shields/browser/ad_block_service_browsertest.cc",
    "//brave/components/brave_shields/browser/https_everywhere_service_browsertest.cc",
    "//brave/components/brave_shields/browser/tracking_protection_service_browsertest.cc",
    "//brave/browser/extensions/brave_component_loader_browsertest.cc",
    "//brave/browser/extensions/brave_extension_provider_browsertest.cc",
    "//brave/renderer/brave_content_settings_observer_browsertest.cc",
    "//brave/renderer/brave_content_settings_observer_autoplay_browsertest.cc",
    "//brave/renderer/brave_content_settings_observer_flash_browsertest.cc",
    "//brave/browser/themes/brave_theme_service_browsertest.cc",
    "//chrome/browser/extensions/browsertest_util.cc",
    "//chrome/browser/extensions/browsertest_util.h",
    "//chrome/browser/extensions/extension_browsertest.cc",
    "//chrome/browser/extensions/extension_browsertest.h",
    "//chrome/browser/extensions/extension_function_test_utils.cc",
    "//chrome/browser/extensions/extension_function_test_utils.h",
    "//chrome/browser/extensions/updater/extension_cache_fake.cc",
    "//chrome/browser/extensions/updater/extension_cache_fake.h",
  ]

  deps = []
  if (bundle_widevine_cdm || enable_widevine_cdm_component) {
    sources += [
      "//brave/browser/widevine/widevine_permission_request_browsertest.cc",
    ]
  }

  if (brave_rewards_enabled) {
    sources += [
      "//brave/components/brave_rewards/browser/rewards_notification_service_browsertest.cc",
      "//brave/components/brave_rewards/browser/rewards_service_browsertest.cc",
    ]

    deps += [
      "//brave/vendor/bat-native-ads",
      "//brave/vendor/bat-native-confirmations",
      "//brave/vendor/bat-native-ledger",
    ]

    configs += [ "//brave/vendor/bat-native-ledger:internal_config" ]
    configs += [ "//brave/vendor/bat-native-ads:internal_config" ]
    configs += [ "//brave/vendor/bat-native-confirmations:internal_config" ]
  }

  if (is_win || is_linux) {
    sources += [
      "//brave/browser/renderer_context_menu/brave_spelling_options_submenu_observer_browsertest.cc",
    ]
  }

  # API tests
  sources += [
    "//brave/browser/extensions/brave_shields_apitest.cc",
    "//chrome/browser/extensions/extension_apitest.cc",
    "//chrome/browser/extensions/extension_apitest.h",
  ]

  if (is_linux && is_official_build) {
    sources += [
      # BraveViewsDelegateLinuxBrowserTest only works in release(official) build.
      "//brave/browser/ui/views/brave_views_delegate_linux_browsertest.cc",
    ]
    configs += [
      "//brave/build/linux:linux_channel_names",
    ]
  }

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  deps += [
    "//chrome/browser/ui",
    "//ppapi/buildflags",
    ":brave_browser_tests_deps",
    ":browser_tests_runner",
    "//testing/gmock",
  ]
  data_deps = [
    "//ppapi:ppapi_tests",
    "//ppapi:power_saver_test_plugin",
  ]
}
