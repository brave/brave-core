# Copyright (c) 2024 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

source_set("policy") {
  sources = [
    "brave_browser_policy_provider.cc",
    "brave_browser_policy_provider.h",
    "brave_profile_policy_provider.cc",
    "brave_profile_policy_provider.h",
  ]

  deps = [
    "//base",
    "//brave/components/brave_origin",
    "//brave/components/constants",
    "//components/policy/core/browser",
    "//components/policy/core/common",
    "//components/prefs",
  ]
}

# See `//components/policy/BUILD.gn` for more info about how these files
# are generated. Also see:
# `//brave/chromium_src/components/policy/resources/policy_templates.py`
# for Brave specific group policy definitions.

if (is_win) {
  action("pack_policy_templates") {
    chrome_pack_policy_templates = "//components/policy:pack_policy_templates"
    deps = [
      "//components/policy:generate_policy_templates",
      "//components/policy:policy_templates",
      chrome_pack_policy_templates,
    ]
    script = "pack_policy_templates.py"
    chrome_policy_templates_zip =
        get_label_info(chrome_pack_policy_templates, "root_out_dir") +
        "/policy_templates.zip"
    inputs = [ chrome_policy_templates_zip ]
    output_zip_file = "$root_out_dir/brave_policy_templates.zip"
    outputs = [ output_zip_file ]
    args = [
      rebase_path(chrome_policy_templates_zip, root_build_dir),
      rebase_path(output_zip_file, root_build_dir),
    ]
  }
}
