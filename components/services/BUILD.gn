import("//brave/components/brave_ads/browser/buildflags/buildflags.gni")
import("//brave/components/brave_rewards/browser/buildflags/buildflags.gni")

# Per-profile services.
source_set("brave_content_browser_overlay_manifest") {
  sources = [
    "brave_content_browser_overlay_manifest.cc",
    "brave_content_browser_overlay_manifest.h",
  ]

  deps = [
    "//base",
    "//services/service_manager/public/cpp",
  ]

  if (brave_ads_enabled) {
    deps += [
      "//brave/components/services/bat_ads/public/cpp:manifest",
    ]
  }

  if (brave_rewards_enabled) {
    deps += [
      "//brave/components/services/bat_ledger/public/cpp:manifest",
    ]
  }

  if (!is_android) {
    deps += [
      "//brave/utility/tor/public/cpp:manifest",
    ]
  }
}

source_set("brave_content_packaged_service_overlay_manifest") {
  sources = [
    "brave_content_packaged_service_overlay_manifest.cc",
    "brave_content_packaged_service_overlay_manifest.h",
  ]

  deps = [
    "//base",
    "//services/service_manager/public/cpp",
  ]

  if (brave_ads_enabled) {
    deps += [
      "//brave/components/services/bat_ads/public/cpp:manifest",
    ]
  }

  if (brave_rewards_enabled) {
    deps += [
      "//brave/components/services/bat_ledger/public/cpp:manifest",
    ]
  }

  if (!is_android) {
    deps += [
      "//brave/utility/tor/public/cpp:manifest",
    ]
  }
}

group("brave_content_manifest_overlays") {
  deps = [
    ":brave_content_browser_overlay_manifest",
    ":brave_content_packaged_service_overlay_manifest",
  ]
}
