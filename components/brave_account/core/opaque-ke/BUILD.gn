# Copyright (c) 2024 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/components/common/typescript.gni")

wasm_pack_target_dir = "$root_build_dir/wasm_pack"
wasm_pack_exe = "$wasm_pack_target_dir/release/wasm-pack"
wasm_pack_src = "//brave/tools/crates/wasm_pack/v0_13/crate"
if (host_os == "win") {
  wasm_pack_exe += ".exe"
}

action("build_wasm_pack") {
  script = "//build/gn_run_binary.py"

  cargo_exe = "//third_party/rust-toolchain/bin/cargo"
  if (host_os == "win") {
    cargo_exe += ".exe"
  }

  inputs = [
    cargo_exe,
    "$wasm_pack_src/Cargo.lock",
  ]

  outputs = [ wasm_pack_exe ]

  args = [
    rebase_path(cargo_exe),
    "build",
    "--release",
    "--manifest-path",
    rebase_path("$wasm_pack_src/Cargo.toml"),
    "--target-dir",
    rebase_path(wasm_pack_target_dir),
  ]
}

action("build_opaque_ke") {
  deps = [ ":build_wasm_pack" ]

  script = "//build/gn_run_binary.py"

  inputs = [
    wasm_pack_exe,
    "Cargo.lock",
  ]

  outputs = [
    "$target_gen_dir/pkg/opaque_ke_bg.js",
    "$target_gen_dir/pkg/opaque_ke_bg.wasm",
    "$target_gen_dir/pkg/opaque_ke_bg.wasm.d.ts",
    "$target_gen_dir/pkg/opaque_ke.d.ts",
    "$target_gen_dir/pkg/opaque_ke.js",
    "$target_gen_dir/pkg/package.json",
  ]

  args = [
    rebase_path(wasm_pack_exe),
    "build",
    rebase_path("."),
    "--out-dir",
    rebase_path("$target_gen_dir/pkg"),
    "--",
    "--target-dir",
    rebase_path("$target_gen_dir/target"),
  ]
}

transpile_web_ui("opaque_ke") {
  public_deps = [ ":build_opaque_ke" ]

  generate_grdp = true
  resource_path_prefix = "brave"

  entry_points = [ [
        "opaque_ke",
        rebase_path("$target_gen_dir/pkg/opaque_ke.js"),
      ] ]

  resource_name = "opaque_ke"
}
