# Copyright (c) 2023 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/components/common/typescript.gni")
import("//tools/grit/preprocess_if_expr.gni")

transpile_web_ui("ai_chat_ui") {
  output_module = false
  resource_name = "ai_chat_ui"
  entry_points = [
    [
      "chat_ui",
      rebase_path("page/chat_ui.tsx"),
    ],
    [
      "untrusted_conversation_frame",
      rebase_path(
          "untrusted_conversation_frame/untrusted_conversation_frame.tsx"),
    ],
  ]

  data = [
    "//brave/components/ai_chat/resources/",
    "//brave/components/common/",
  ]

  deps = [
    "//brave/components/ai_chat/core/common/mojom:mojom_js",
    "//brave/components/resources:strings",
  ]
}

preprocess_if_expr("preprocess_html") {
  visibility = [ ":resources_grit" ]
  in_folder = "."
  in_files = [
    "page/ai_chat_ui.html",
    "untrusted_conversation_frame/untrusted_conversation_frame.html",
  ]
  out_folder = "$target_gen_dir/preprocessed"
}

pack_web_resources("resources") {
  resource_name = "ai_chat_ui"
  output_dir = "$root_gen_dir/brave/components/ai_chat/resources"
  deps = [
    ":ai_chat_ui",
    ":preprocess_html",
  ]
}
