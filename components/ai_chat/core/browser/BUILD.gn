# Copyright (c) 2023 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/components/ai_chat/core/common/buildflags/buildflags.gni")
import("//brave/components/text_recognition/common/buildflags/buildflags.gni")

assert(enable_ai_chat)

static_library("browser") {
  sources = [
    "ai_chat_credential_manager.cc",
    "ai_chat_credential_manager.h",
    "ai_chat_feedback_api.cc",
    "ai_chat_feedback_api.h",
    "ai_chat_metrics.cc",
    "ai_chat_metrics.h",
    "constants.cc",
    "constants.h",
    "conversation_driver.cc",
    "conversation_driver.h",
    "engine/conversation_api_client.cc",
    "engine/conversation_api_client.h",
    "engine/engine_consumer.cc",
    "engine/engine_consumer.h",
    "engine/engine_consumer_claude.cc",
    "engine/engine_consumer_claude.h",
    "engine/engine_consumer_conversation_api.cc",
    "engine/engine_consumer_conversation_api.h",
    "engine/engine_consumer_llama.cc",
    "engine/engine_consumer_llama.h",
    "engine/engine_consumer_oai.cc",
    "engine/engine_consumer_oai.h",
    "engine/oai_api_client.cc",
    "engine/oai_api_client.h",
    "engine/remote_completion_client.cc",
    "engine/remote_completion_client.h",
    "model_service.cc",
    "model_service.h",
    "utils.cc",
    "utils.h",
  ]

  if (is_android) {
    sources += [
      "android/ai_chat_iap_subscription_android.cc",
      "android/ai_chat_iap_subscription_android.h",
    ]
  }

  public_deps = [ "//base" ]

  deps = [
    "//brave/brave_domains",
    "//brave/components/ai_chat/core/common",
    "//brave/components/ai_chat/core/common/buildflags",
    "//brave/components/ai_chat/core/common/mojom",
    "//brave/components/api_request_helper",
    "//brave/components/brave_service_keys",
    "//brave/components/brave_stats/browser",
    "//brave/components/constants",
    "//brave/components/l10n/common",
    "//brave/components/p3a_utils",
    "//brave/components/resources:strings_grit",
    "//brave/components/sidebar/common",
    "//brave/components/skus/common:mojom",
    "//brave/components/text_recognition/common/buildflags",
    "//brave/components/time_period_storage",
    "//components/keyed_service/core",
    "//components/os_crypt/sync:os_crypt",
    "//components/prefs",
    "//components/user_prefs",
    "//net/traffic_annotation",
    "//services/data_decoder/public/cpp",
    "//services/network/public/cpp",
    "//services/service_manager/public/cpp",
    "//third_party/abseil-cpp:absl",
    "//third_party/re2",
    "//ui/base",
    "//url",
  ]

  if (enable_text_recognition) {
    deps += [ "//brave/components/text_recognition/browser" ]
  }
}

if (!is_ios) {
  source_set("unit_tests") {
    testonly = true
    sources = [
      "ai_chat_credential_manager_unittest.cc",
      "ai_chat_metrics_unittest.cc",
      "conversation_driver_unittest.cc",
      "engine/conversation_api_client_unittest.cc",
      "engine/engine_consumer_claude_unittest.cc",
      "engine/engine_consumer_conversation_api_unittest.cc",
      "engine/engine_consumer_llama_unittest.cc",
      "engine/engine_consumer_oai_unittest.cc",
      "engine/oai_api_client_unittest.cc",
      "model_service_unittest.cc",
    ]

    deps = [
      ":test_support",
      "//base/test:test_support",
      "//brave/components/ai_chat/core/browser",
      "//brave/components/ai_chat/core/common",
      "//brave/components/ai_chat/core/common/mojom",
      "//brave/components/api_request_helper",
      "//brave/components/resources:strings_grit",
      "//brave/components/skus/browser",
      "//brave/components/skus/common",
      "//brave/components/skus/common:mojom",
      "//components/os_crypt/sync:test_support",
      "//components/prefs:test_support",
      "//components/sync_preferences:test_support",
      "//content/test:test_support",
      "//net/traffic_annotation:test_support",
      "//services/data_decoder/public/cpp:test_support",
      "//services/network:test_support",
      "//services/network/public/cpp:cpp",
      "//testing/gtest:gtest",
    ]
  }
}

source_set("test_support") {
  testonly = true
  sources = [
    "engine/mock_remote_completion_client.cc",
    "engine/mock_remote_completion_client.h",
  ]

  deps = [
    "//brave/components/ai_chat/core/browser",
    "//services/network/public/cpp",
    "//testing/gmock",
  ]
}
