# Copyright (c) 2024 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/build/config.gni")
import("//build/buildflag_header.gni")
import("//testing/test.gni")

declare_args() {
  key_id = ""
}

if (is_official_build) {
  assert(key_id != "")
}

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "KEY_ID=\"$key_id\"" ]
}

static_library("brave_service_keys") {
  sources = [
    "service_key_utils.cc",
    "service_key_utils.h",
  ]

  deps = [
    ":buildflags",
    "//base",
    "//crypto:crypto",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "service_key_utils_unittest.cc" ]

  deps = [
    ":brave_service_keys",
    "//testing/gtest",
  ]
}
