import("//brave/components/brave_shields/browser/buildflags/buildflags.gni")
import("//brave/components/brave_perf_predictor/browser/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")
import("//brave/vendor/adblock_rust_ffi/config.gni")

source_set("browser") {
  public_deps = [
    "buildflags",
    ":adblock_libs"
  ]

  sources = [
    "ad_block_base_service.cc",
    "ad_block_base_service.h",
    "ad_block_custom_filters_service.cc",
    "ad_block_custom_filters_service.h",
    "ad_block_regional_service.cc",
    "ad_block_regional_service.h",
    "ad_block_regional_service_manager.cc",
    "ad_block_regional_service_manager.h",
    "ad_block_service.cc",
    "ad_block_service.h",
    "ad_block_service_helper.cc",
    "ad_block_service_helper.h",
    "adblock_stub_response.cc",
    "adblock_stub_response.h",
    "base_brave_shields_service.cc",
    "base_brave_shields_service.h",
    "brave_shields_p3a.cc",
    "brave_shields_p3a.h",
    "brave_shields_util.cc",
    "brave_shields_util.h",
    "brave_shields_web_contents_observer_android.cc",
    "brave_shields_web_contents_observer.cc",
    "brave_shields_web_contents_observer.h",
    "cookie_pref_service.cc",
    "cookie_pref_service.h",
    "https_everywhere_recently_used_cache.h",
    "https_everywhere_service.cc",
    "https_everywhere_service.h",
    "referrer_whitelist_service.cc",
    "referrer_whitelist_service.h",
    "tracking_protection_service.cc",
    "tracking_protection_service.h",
  ]

  if (brave_stp_enabled) {
    sources += [
      "tracking_protection_helper.cc",
      "tracking_protection_helper.h",
    ]
  }

  deps = [
    "//base",
    "//brave/components/brave_component_updater/browser",
    "//brave/components/brave_shields/common",
    "//brave/components/content_settings/core/browser",
    "//brave/content:common",
    "//brave/vendor/adblock_rust_ffi:adblock_ffi",
    "//chrome/common",
    "//components/content_settings/core/common",
    "//components/content_settings/core/browser",
    "//components/keyed_service/core",
    "//components/prefs",
    "//components/sessions",
    "//content/public/browser",
    "//net",
    "//third_party/blink/public/mojom:mojom_platform_headers",
    "//third_party/leveldatabase",
    "//url",
  ]

  if (enable_brave_perf_predictor) {
    deps += [
      "//brave/components/brave_perf_predictor/browser",
    ]
  }
}

if (is_mac) {
  bundle_data("adblock_libs") {
    sources = [
      adblock_lib_path,
    ]
    outputs = [
      "{{bundle_contents_dir}}/Libraries/{{source_file_part}}",
    ]
    public_deps = [
      "//brave/vendor/adblock_rust_ffi:adblock",
    ]
  }
} else {
  group("adblock_libs") {}
}
