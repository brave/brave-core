import("//brave/components/p3a/buildflags.gni")
import("//build/buildflag_header.gni")
import("//testing/test.gni")

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [
    "BRAVE_P3A_ENABLED=$brave_p3a_enabled",
    "P3A_JSON_UPLOAD_URL=\"$p3a_json_upload_url\"",
    "P2A_JSON_UPLOAD_URL=\"$p2a_json_upload_url\"",
    "P3A_STAR_UPLOAD_URL=\"$p3a_star_upload_url\"",
    "P2A_STAR_UPLOAD_URL=\"$p2a_star_upload_url\"",
    "STAR_RANDOMNESS_HOST=\"$star_randomness_host\"",
  ]
}

static_library("p3a") {
  sources = [
    "brave_p2a_protocols.cc",
    "brave_p2a_protocols.h",
    "brave_p3a_config.cc",
    "brave_p3a_config.h",
    "brave_p3a_message_manager.cc",
    "brave_p3a_message_manager.h",
    "brave_p3a_metric_log_store.cc",
    "brave_p3a_metric_log_store.h",
    "brave_p3a_new_uploader.cc",
    "brave_p3a_new_uploader.h",
    "brave_p3a_rotation_scheduler.cc",
    "brave_p3a_rotation_scheduler.h",
    "brave_p3a_scheduler.cc",
    "brave_p3a_scheduler.h",
    "brave_p3a_service.cc",
    "brave_p3a_service.h",
    "brave_p3a_star.cc",
    "brave_p3a_star.h",
    "brave_p3a_star_log_store.cc",
    "brave_p3a_star_log_store.h",
    "brave_p3a_star_randomness_meta.cc",
    "brave_p3a_star_randomness_meta.h",
    "brave_p3a_star_randomness_points.cc",
    "brave_p3a_star_randomness_points.h",
    "brave_p3a_switches.h",
    "brave_p3a_utils.h",
    "histograms_braveizer.cc",
    "histograms_braveizer.h",
    "metric_names.h",
    "network_annotations.cc",
    "network_annotations.h",
    "p3a_message.cc",
    "p3a_message.h",
    "pref_names.cc",
    "pref_names.h",
  ]

  deps = [
    "//base",
    "//brave/components/brave_prochlo:prochlo_proto",
    "//brave/components/brave_referrals/common",
    "//brave/components/brave_stats/browser",
    "//brave/components/nested_star:cxx",
    "//brave/components/nitro_utils",
    "//brave/components/p3a:buildflags",
    "//brave/components/p3a_utils",
    "//brave/components/version_info",
    "//brave/vendor/brave_base",
    "//components/metrics",
    "//components/prefs",
    "//content/public/browser",
    "//content/public/common",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
    "//third_party/protobuf:protobuf_lite",
    "//url",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "brave_p3a_metric_log_store_unittest.cc",
    "brave_p3a_star_log_store_unittest.cc",
  ]
  deps = [
    "//base/test:test_support",
    "//brave/components/p3a",
    "//testing/gtest",
  ]
}
