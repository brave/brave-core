import("//brave/components/common/typescript.gni")
import("//build/config/locales.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

assert(enable_extensions)

pack_web_resources("extension_generated_resources") {
  resource_name = "brave_rewards_panel"
  output_dir =
      "$root_gen_dir/brave/components/brave_rewards/resources/extension"
  deps = [ "brave_rewards:brave_rewards_panel" ]
}

grit("static_resources") {
  source = "extension_static_resources.grd"
  outputs = [
    "grit/brave_rewards_extension_resources_map.cc",
    "grit/brave_rewards_extension_resources_map.h",
    "grit/brave_rewards_extension_resources.h",
    "brave_rewards_extension_static.pak",
  ]

  deps = []

  resource_ids = "//brave/browser/resources/resource_ids"

  output_dir =
      "$root_gen_dir/brave/components/brave_rewards/resources/extension"
}

repack("resources") {
  deps = [
    ":extension_generated_resources",
    ":static_resources",
  ]

  sources = [
    "$root_gen_dir/brave/components/brave_rewards/resources/extension/brave_rewards_extension_static.pak",
    "$root_gen_dir/brave/components/brave_rewards/resources/extension/brave_rewards_panel_generated.pak",
  ]

  output = "$root_gen_dir/brave/components/brave_rewards/resources/extension/brave_rewards_extension_resources.pak"
}

if (is_mac) {
  group("framework_bundle_data") {
    deps = []
    foreach(locale, platform_pak_locales) {
      deps += [ ":framework_bundle_data_${locale}" ]
    }
  }

  foreach(locale, platform_pak_locales) {
    bundle_data("framework_bundle_data_${locale}") {
      locale = string_replace(locale, "-", "_")
      source_locale = string_replace(locale, "nb", "no")
      sources = [ "brave_rewards/_locales/$source_locale/messages.json" ]
      outputs = [ "{{bundle_resources_dir}}/brave_rewards/_locales/$locale/{{source_file_part}}" ]
    }
  }
} else {
  group("locales") {
    deps = []
    foreach(locale, platform_pak_locales) {
      deps += [ ":locales_${locale}" ]
    }
  }
  foreach(locale, platform_pak_locales) {
    copy("locales_${locale}") {
      locale = string_replace(locale, "-", "_")
      source_locale = string_replace(locale, "nb", "no")
      sources = [ "brave_rewards/_locales/${source_locale}/messages.json" ]
      outputs = [ "$root_out_dir/resources/brave_rewards/_locales/${locale}/{{source_file_part}}" ]
    }
  }
}
