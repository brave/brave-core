import("//brave/components/brave_rewards/browser/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("browser") {
  # Remove when https://github.com/brave/brave-browser/issues/10618 is resolved
  check_includes = false
  public_deps = [
    "buildflags",
  ]

  sources = [
    "android_util.cc",
    "android_util.h",
    "switches.cc",
    "switches.h",
    "auto_contribution_props.cc",
    "auto_contribution_props.h",
    "balance_report.cc",
    "balance_report.h",
    "rewards_notification_service.cc",
    "rewards_notification_service.h",
    "rewards_notification_service_observer.h",
    "content_site.cc",
    "content_site.h",
    "contribution_info.cc",
    "contribution_info.h",
    "contribution_publisher.cc",
    "contribution_publisher.h",
    "rewards_p3a.cc",
    "rewards_p3a.h",
    "rewards_parameters.cc",
    "rewards_parameters.h",
    "rewards_service.cc",
    "rewards_service.h",
    "rewards_service_observer.h",
    "file_util.cc",
    "file_util.h",
    "logging_util.cc",
    "logging_util.h",
    "logging.h",
    "promotion.cc",
    "promotion.h",
    "pending_contribution.cc",
    "pending_contribution.h",
    "publisher_banner.cc",
    "publisher_banner.h",
    "rewards_internals_info.cc",
    "rewards_internals_info.h",
    "balance.cc",
    "balance.h",
    "external_wallet.cc",
    "external_wallet.h",
    "rewards_protocol_handler.h",
    "rewards_protocol_handler.cc",
    "static_values.h",
    "monthly_report.cc",
    "monthly_report.h",
    "transaction_report_info.cc",
    "transaction_report_info.h",
    "contribution_report_info.cc",
    "contribution_report_info.h",
    "rewards_database.h",
    "rewards_database.cc",
    "event_log.h",
    "event_log.cc",
  ]

  deps = [
    "//base",
    "//brave/base",
    "//brave/components/brave_rewards/common",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//components/sessions",
    # for profile.h
    "//components/domain_reliability",
    "//content/public/browser",
    "//content/public/common",
    "//services/network/public/mojom",
  ]

  if (!is_android) {
    # on android, one shot is part of base lib
    deps += [
      # for one shot event
      "//extensions/common",
    ]
  }

  if (brave_rewards_enabled) {
    sources += [
      "net/network_delegate_helper.cc",
      "net/network_delegate_helper.h",
      "rewards_service_impl.cc",
      "rewards_service_impl.h",
      "publisher_info_backend.cc",
      "publisher_info_backend.h",
      "rewards_notification_service_impl.cc",
      "rewards_notification_service_impl.h",
    ]

    deps += [
      "//brave/components/brave_ads/browser/buildflags",
      "//brave/components/resources",
      "//brave/components/services/bat_ledger/public/cpp",
      "//mojo/public/cpp/bindings",
      "//net",
      "//services/network/public/cpp",
      "//services/service_manager/public/cpp",
      "//url",
    ]

    public_deps += [
      "//brave/vendor/bat-native-ledger:headers",
    ]

    if (is_android) {
      deps += [ "//brave/components/safetynet:android" ]
    }
  }
}

source_set("testutil") {
  testonly = true
  # Remove when https://github.com/brave/brave-browser/issues/10618 is resolved
  check_includes = false

  deps = [
    "//brave/vendor/bat-native-ledger:headers",
    ":browser",
    "//base",
    "//content/public/browser",
    "//chrome/test:test_support",
    "//testing/gtest",
  ]

  sources = [
    "test_util.cc",
    "test_util.h",
  ]
}
