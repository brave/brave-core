import("//brave/components/brave_rewards/browser/buildflags/buildflags.gni")
import("//brave/vendor/bat-native-ledger/config.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("browser") {
  # Remove when https://github.com/brave/brave-browser/issues/10618 is resolved
  check_includes = false
  public_deps = [ "buildflags" ]

  defines = [
    "BITFLYER_CLIENT_SECRET=\"$bitflyer_client_secret\"",
    "BITFLYER_STAGING_URL=\"$bitflyer_staging_url\"",
  ]

  sources = [
    "android_util.cc",
    "android_util.h",
    "file_util.cc",
    "file_util.h",
    "logging.h",
    "logging_util.cc",
    "logging_util.h",
    "rewards_notification_service.cc",
    "rewards_notification_service.h",
    "rewards_notification_service_observer.h",
    "rewards_p3a.cc",
    "rewards_p3a.h",
    "rewards_protocol_handler.cc",
    "rewards_protocol_handler.h",
    "rewards_service.cc",
    "rewards_service.h",
    "rewards_service_observer.h",
    "static_values.h",
    "switches.cc",
    "switches.h",
  ]

  deps = [
    "//base",
    "//brave/components/brave_rewards/common",

    # for profile.h
    "//components/domain_reliability",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//components/sessions",
    "//content/public/browser",
    "//content/public/common",
    "//services/network/public/mojom",
  ]

  if (!is_android) {
    # on android, one shot is part of base lib
    deps += [
      # for one shot event
      "//extensions/common",
    ]
  }

  if (brave_rewards_enabled) {
    sources += [
      "net/network_delegate_helper.cc",
      "net/network_delegate_helper.h",
      "rewards_notification_service_impl.cc",
      "rewards_notification_service_impl.h",
      "rewards_service_impl.cc",
      "rewards_service_impl.h",
    ]

    deps += [
      "//brave/app:brave_generated_resources_grit",
      "//brave/components/brave_ads/browser/buildflags",
      "//brave/components/resources",
      "//brave/components/services/bat_ledger/public/cpp",
      "//brave/vendor/bat-native-ads",
      "//mojo/public/cpp/bindings",
      "//net",
      "//services/network/public/cpp",
      "//services/service_manager/public/cpp",
      "//url",
    ]

    public_deps += [ "//brave/vendor/bat-native-ledger:headers" ]

    if (is_android) {
      deps += [ "//brave/components/safetynet:android" ]
    }
  }
}

source_set("testutil") {
  testonly = true
  deps = [
    ":browser",
    "//base",
    "//brave/browser:browser_process",
    "//brave/vendor/bat-native-ledger:headers",
    "//chrome/test:test_support",
    "//content/public/browser",
    "//testing/gtest",
  ]

  sources = [
    "test_util.cc",
    "test_util.h",
  ]
}
