import("//brave/components/brave_rewards/browser/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("browser") {
  public_deps = [
    "buildflags",
  ]

  sources = [
    "android_util.cc",
    "android_util.h",
    "switches.cc",
    "switches.h",
    "auto_contribution_props.cc",
    "auto_contribution_props.h",
    "balance_report.cc",
    "balance_report.h",
    "rewards_notification_service.cc",
    "rewards_notification_service.h",
    "rewards_notification_service_observer.h",
    "content_site.cc",
    "content_site.h",
    "rewards_p3a.cc",
    "rewards_p3a.h",
    "rewards_service.cc",
    "rewards_service.h",
    "rewards_service_factory.cc",
    "rewards_service_factory.h",
    "rewards_service_observer.h",
    "wallet_properties.cc",
    "wallet_properties.h",
    "promotion.cc",
    "promotion.h",
    "pending_contribution.cc",
    "pending_contribution.h",
    "publisher_banner.cc",
    "publisher_banner.h",
    "reconcile_info.cc",
    "reconcile_info.h",
    "rewards_internals_info.cc",
    "rewards_internals_info.h",
    "balance.cc",
    "balance.h",
    "external_wallet.cc",
    "external_wallet.h",
    "rewards_protocol_handler.h",
    "rewards_protocol_handler.cc",
    "static_values.h",
    "monthly_report.cc",
    "monthly_report.h",
    "transaction_report_info.cc",
    "transaction_report_info.h",
    "contribution_report_info.cc",
    "contribution_report_info.h",
  ]

  deps = [
    "//base",
    "//brave/base",
    "//brave/components/brave_rewards/common",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//components/sessions",
    # for profile.h
    "//components/domain_reliability",
    "//content/public/browser",
    "//content/public/common",
    "//services/network/public/mojom",
    "//third_party/leveldatabase",
  ]

  if (!is_android) {
    # on android, one shot is part of base lib
    deps += [
      # for one shot event
      "//extensions/common",
    ]
  }

  if (brave_rewards_enabled) {
    sources += [
      "database/database_activity_info.cc",
      "database/database_activity_info.h",
      "database/database_contribution_info.cc",
      "database/database_contribution_info.h",
      "database/database_contribution_info_publishers.cc",
      "database/database_contribution_info_publishers.h",
      "database/database_contribution_queue.cc",
      "database/database_contribution_queue.h",
      "database/database_contribution_queue_publishers.cc",
      "database/database_contribution_queue_publishers.h",
      "database/database_media_publisher_info.cc",
      "database/database_media_publisher_info.h",
      "database/database_pending_contribution.cc",
      "database/database_pending_contribution.h",
      "database/database_promotion.cc",
      "database/database_promotion.h",
      "database/database_promotion_creds.cc",
      "database/database_promotion_creds.h",
      "database/database_publisher_info.cc",
      "database/database_publisher_info.h",
      "database/database_recurring_tip.cc",
      "database/database_recurring_tip.h",
      "database/database_server_publisher_amounts.cc",
      "database/database_server_publisher_amounts.h",
      "database/database_server_publisher_banner.cc",
      "database/database_server_publisher_banner.h",
      "database/database_server_publisher_info.cc",
      "database/database_server_publisher_info.h",
      "database/database_server_publisher_links.cc",
      "database/database_server_publisher_links.h",
      "database/database_table.cc",
      "database/database_table.h",
      "database/database_unblinded_token.cc",
      "database/database_unblinded_token.h",
      "database/database_util.cc",
      "database/database_util.h",
      "database/publisher_info_database.cc",
      "database/publisher_info_database.h",
      "net/network_delegate_helper.cc",
      "net/network_delegate_helper.h",
      "rewards_service_impl.cc",
      "rewards_service_impl.h",
      "publisher_info_backend.cc",
      "publisher_info_backend.h",
      "rewards_fetcher_service_observer.cc",
      "rewards_fetcher_service_observer.h",
      "rewards_notification_service_impl.cc",
      "rewards_notification_service_impl.h",
    ]

    if (enable_extensions) {
      sources += [
        "extension_rewards_notification_service_observer.cc",
        "extension_rewards_notification_service_observer.h",
        "extension_rewards_service_observer.cc",
        "extension_rewards_service_observer.h",
      ]
    }

    deps += [
      "//brave/components/brave_ads/browser/buildflags",
      "//brave/components/resources",
      "//brave/components/services/bat_ledger/public/cpp",
      "//brave/vendor/bat-native-ledger",
      "//mojo/public/cpp/bindings",
      "//net",
      "//services/network/public/cpp",
      "//services/service_manager/public/cpp",
      "//url",
    ]

    if (is_android) {
      deps += [ "android" ]
    }
  }
}

source_set("testutil") {
  testonly = true

  deps = [
    "//brave/vendor/bat-native-ledger",
    ":browser",
    "//base",
    "//content/public/browser",
    "//chrome/test:test_support",
    "//testing/gtest",
  ]

  sources = [
    "test_util.cc",
    "test_util.h",
  ]
}
