# Copyright (c) 2023 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  sources = [ "brave_rewards_panel.mojom" ]

  public_deps = [ "//mojo/public/mojom/base" ]
}

mojom("core_interfaces") {
  generate_java = true
  sources = [
    "core.mojom",
    "core_database.mojom",
    "core_endpoints.mojom",
  ]

  public_deps = [ "//mojo/public/mojom/base" ]
}

mojom("core_types") {
  generate_java = true
  sources = [ "core_types.mojom" ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "brave_rewards.mojom.ConnectExternalWalletResult"
          cpp = "base::expected<void, brave_rewards::mojom::ConnectExternalWalletError>"
          move_only = true
        },
        {
          mojom = "brave_rewards.mojom.FetchBalanceResult"
          cpp = "base::expected<brave_rewards::mojom::BalancePtr, brave_rewards::mojom::FetchBalanceError>"
          move_only = true
        },
        {
          mojom = "brave_rewards.mojom.GetExternalWalletResult"
          cpp = "base::expected<brave_rewards::mojom::ExternalWalletPtr, brave_rewards::mojom::GetExternalWalletError>"
          move_only = true
        },
      ]

      traits_headers = [ "core_types_mojom_traits.h" ]
      traits_sources = [ "core_types_mojom_traits.cc" ]
    },
  ]
}
