# Copyright (c) 2023 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//mojo/public/tools/bindings/mojom.gni")

mojom("mojom") {
  sources = [
    "rewards_panel.mojom",
    "tip_panel.mojom",
  ]

  deps = [
    ":rewards_interfaces",
    ":rewards_types",
  ]

  public_deps = [ "//mojo/public/mojom/base" ]
}

mojom("rewards_interfaces") {
  generate_java = true
  sources = [
    "rewards.mojom",
    "rewards_database.mojom",
    "rewards_endpoints.mojom",
  ]

  public_deps = [ "//mojo/public/mojom/base" ]
}

mojom("rewards_types") {
  generate_java = true
  sources = [ "rewards_types.mojom" ]

  cpp_typemaps = [
    {
      types = [
        {
          mojom = "brave_rewards.mojom.ConnectExternalWalletResult"
          cpp = "base::expected<void, mojom::ConnectExternalWalletError>"
          move_only = true
        },
        {
          mojom = "brave_rewards.mojom.FetchBalanceResult"
          cpp = "base::expected<mojom::BalancePtr, mojom::FetchBalanceError>"
          move_only = true
        },
        {
          mojom = "brave_rewards.mojom.GetExternalWalletResult"
          cpp = "base::expected<mojom::ExternalWalletPtr, mojom::GetExternalWalletError>"
          move_only = true
        },
      ]

      traits_headers = [ "rewards_types_mojom_traits.h" ]
      traits_sources = [ "rewards_types_mojom_traits.cc" ]
    },
  ]
}

mojom("interfaces") {
  sources = [ "rewards_engine.mojom" ]

  public_deps = [ "//mojo/public/mojom/base" ]

  deps = [
    "//brave/components/brave_rewards/common/mojom:rewards_interfaces",
    "//brave/components/brave_rewards/common/mojom:rewards_types",
  ]
}
