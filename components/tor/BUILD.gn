import("//brave/components/tor/buildflags/buildflags.gni")

source_set("tor") {
  public_deps = [ "//brave/components/tor/buildflags" ]

  sources = [
    "tor_constants.cc",
    "tor_constants.h",
    "tor_launcher_observer.h",
    "tor_switches.h",
  ]

  deps = [
    ":pref_names",
    "//base",
  ]

  if (enable_tor) {
    sources += [
      "brave_tor_client_updater.cc",
      "brave_tor_client_updater.h",
      "onion_location_navigation_throttle.cc",
      "onion_location_navigation_throttle.h",
      "onion_location_tab_helper.cc",
      "onion_location_tab_helper.h",
      "service_sandbox_type.h",
      "tor_control.cc",
      "tor_control.h",
      "tor_control_event.cc",
      "tor_control_event.h",
      "tor_control_event_list.h",
      "tor_file_watcher.cc",
      "tor_file_watcher.h",
      "tor_launcher_factory.cc",
      "tor_launcher_factory.h",
      "tor_navigation_throttle.cc",
      "tor_navigation_throttle.h",
      "tor_profile_service.cc",
      "tor_profile_service.h",
      "tor_profile_service_impl.cc",
      "tor_profile_service_impl.h",
      "tor_tab_helper.cc",
      "tor_tab_helper.h",
    ]
  }

  deps += [
    "//brave/components/brave_component_updater/browser",
    "//brave/components/resources:strings",
    "//brave/components/services/tor/public/interfaces",
    "//components/pref_registry",
    "//components/prefs",
    "//components/user_prefs",
    "//content/public/browser",
    "//extensions/common:common_constants",
    "//net",
    "//services/service_manager",
    "//third_party/re2",
    "//url",
  ]
}

source_set("utils") {
  sources = [
    "tor_utils.cc",
    "tor_utils.h",
  ]

  deps = [
    ":tor",
    "//base",
    "//chrome/common:constants",
    "//components/prefs",
  ]
}

source_set("pref_names") {
  sources = [
    "pref_names.cc",
    "pref_names.h",
  ]
}

source_set("tor_unit_tests") {
  testonly = true
  if (enable_tor) {
    sources = [
      "tor_control_unittest.cc",
      "tor_file_watcher_unittest.cc",
    ]

    deps = [
      "//base/test:test_support",
      "//brave/components/tor",
      "//content/public/browser",
      "//content/test:test_support",
      "//testing/gtest",
    ]
  }
}

source_set("test_support") {
  testonly = true
  sources = [
    "mock_tor_launcher_factory.cc",
    "mock_tor_launcher_factory.h",
  ]

  deps = [
    ":tor",
    "//base",
    "//testing/gmock",
  ]
}
