import("//brave/build/config.gni")
import("//brave/components/brave_adaptive_captcha/buildflags/buildflags.gni")

source_set("browser") {
  sources = [
    "ads_p2a.cc",
    "ads_p2a.h",
    "ads_service.cc",
    "ads_service.h",
    "ads_service_observer.h",
    "ads_storage_cleanup.cc",
    "ads_storage_cleanup.h",
    "component_updater/component_info.cc",
    "component_updater/component_info.h",
    "component_updater/component_util.cc",
    "component_updater/component_util.h",
    "component_updater/components.h",
    "component_updater/resource_component.cc",
    "component_updater/resource_component.h",
    "component_updater/resource_component_observer.h",
    "component_updater/resource_info.h",
    "frequency_capping_helper.cc",
    "frequency_capping_helper.h",
  ]

  deps = [
    "//base",
    "//brave/components/brave_ads/common",
    "//brave/components/brave_ads/resources",
    "//brave/components/brave_component_updater/browser",
    "//brave/components/l10n/browser",
    "//brave/components/l10n/common",
    "//brave/components/p3a_utils",
    "//brave/components/time_period_storage",
    "//components/keyed_service/core",
    "//components/pref_registry",
    "//components/prefs",
    "//components/sessions",
    "//sql",
    "//url",
  ]

  public_deps = [
    "//brave/components/brave_adaptive_captcha/buildflags",
    "//brave/vendor/bat-native-ads",
  ]

  if (brave_adaptive_captcha_enabled) {
    sources += [ "ads_tooltips_delegate.h" ]
    deps += [ "//brave/components/brave_adaptive_captcha" ]
  }

  # TODO(https://github.com/brave/brave-browser/issues/24163)
  check_includes = false

  sources += [
    "ads_service_impl.cc",
    "ads_service_impl.h",
    "device_id.cc",
    "device_id.h",
    "service_sandbox_type.h",
  ]

  deps += [
    "//brave/app:brave_generated_resources_grit",
    "//brave/browser/profiles:util",
    "//brave/common",
    "//brave/components/brave_federated",
    "//brave/components/brave_rewards/browser",
    "//brave/components/brave_rewards/common",
    "//brave/components/brave_today/common",
    "//brave/components/rpill/common",
    "//brave/components/services/bat_ads/public/cpp",
    "//chrome/browser/notifications",
    "//chrome/browser/profiles:profile",
    "//components/history/core/browser",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//net",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
    "//ui/base",
    "//ui/message_center/public/cpp",
  ]

  public_deps += [
    "//brave/components/services/bat_ads/public/interfaces",
    "//brave/vendor/bat-native-ledger",
    "//services/network/public/mojom",
  ]
}

source_set("test_support") {
  testonly = true

  public_deps = [ ":browser" ]

  deps = [ "//testing/gmock" ]

  sources = [
    "mock_ads_service.cc",
    "mock_ads_service.h",
  ]
}
