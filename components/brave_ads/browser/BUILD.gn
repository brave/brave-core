import("//brave/components/brave_ads/browser/buildflags/buildflags.gni")

source_set("browser") {
  public_deps = [
    "buildflags",
  ]

  sources = [
    "ads_service.h",
    "ads_service_factory.cc",
    "ads_service_factory.h",
    "ads_tab_helper.cc",
    "ads_tab_helper.h",
  ]

  deps = [
    "//base",
    "//brave/components/brave_ads/common",
    "//components/dom_distiller/content/browser",
    "//components/dom_distiller/core",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//components/prefs",
    "//components/pref_registry",
    "//components/sessions",
    "//url",
    # for profile.h
    "//components/domain_reliability",
    "//content/public/browser",
    "//content/public/common",
  ]

  if (brave_ads_enabled) {
    sources += [
      "ad_notification.cc",
      "ad_notification.h",
      "ads_service_impl.cc",
      "ads_service_impl.h",
      "bundle_state_database.cc",
      "bundle_state_database.h",
    ]

    deps += [
      "//brave/vendor/bat-native-ads",
      "//brave/components/brave_ads/resources",
      "//components/history/core/browser",
      "//components/history/core/common",
      "//components/wifi",
      "//net",
      "//ui/base",
      "//ui/message_center/public/cpp",
    ]
  }
}
