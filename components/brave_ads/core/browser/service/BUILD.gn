# Copyright (c) 2024 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/components/brave_ads/buildflags/buildflags.gni")

source_set("service") {
  sources = [
    "ads_service.cc",
    "ads_service.h",
    "ads_service_callback.h",
    "ads_service_observer.h",
  ]

  public_deps = [
    "//base",
    "//brave/components/brave_ads/core/mojom",
    "//brave/components/services/bat_ads/public/interfaces",
    "//components/keyed_service/core",
    "//mojo/public/cpp/bindings",
  ]
}

source_set("test_support") {
  testonly = true

  public_deps = [
    ":service",
    "//brave/components/brave_ads/core/mojom",
    "//testing/gmock",
  ]

  sources = [
    "ads_service_mock.cc",
    "ads_service_mock.h",
  ]
}

# TODO: Will be separated into its own targets.
if (enable_brave_ads) {
  source_set("network") {
    sources = [
      "network_client.cc",
      "network_client.h",
      "network_client_callback.h",
      "network_client_util.cc",
      "network_client_util.h",
      "oblivious_http_client_impl.cc",
      "oblivious_http_client_impl.h",
      "oblivious_http_constants.h",
      "oblivious_http_feature.cc",
      "oblivious_http_feature.h",
      "oblivious_http_key_config.cc",
      "oblivious_http_key_config.h",
    ]

    public_deps = [
      "//base",
      "//brave/components/brave_ads/core/mojom",
      "//services/network/public/cpp",
    ]

    deps = [
      "//brave/components/brave_ads/core/public:headers",
      "//components/prefs",
      "//mojo/public/cpp/bindings",
      "//net",
      "//services/network/public/mojom",
      "//url",
    ]
  }

  source_set("new_tab_page_ad") {
    sources = [
      "new_tab_page_ad_prefetcher.cc",
      "new_tab_page_ad_prefetcher.h",
    ]

    public_deps = [
      "//base",
      "//brave/components/brave_ads/core/mojom",
    ]

    deps = [ "//brave/components/brave_ads/core/browser/service" ]
  }

  source_set("virtual_pref") {
    sources = [
      "virtual_pref_provider.cc",
      "virtual_pref_provider.h",
      "virtual_pref_provider_util.cc",
      "virtual_pref_provider_util.h",
    ]

    public_deps = [ "//base" ]

    deps = [
      "//base/version_info",
      "//brave/components/brave_ads/core/public:headers",
      "//brave/components/ntp_background_images/common",
      "//brave/components/skus/browser",
      "//components/prefs",
    ]
  }

  source_set("unit_tests") {
    testonly = true

    sources = [
      "network_client_util_unittest.cc",
      "new_tab_page_ad_prefetcher_unittest.cc",
      "oblivious_http_feature_unittest.cc",
      "virtual_pref_provider_unittest.cc",
    ]

    deps = [
      ":network",
      ":new_tab_page_ad",
      ":service",
      ":test_support",
      ":virtual_pref",
      "//base",
      "//base/test:test_support",
      "//base/version_info",
      "//brave/components/brave_ads/core",
      "//brave/components/brave_ads/core/public:test_support",
      "//brave/components/ntp_background_images/common",
      "//brave/components/skus/browser",
      "//components/prefs:test_support",
      "//net",
      "//testing/gmock",
      "//testing/gtest",
    ]
  }
}  # end of if (enable_brave_ads)
