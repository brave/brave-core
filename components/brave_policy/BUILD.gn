# Copyright (c) 2024 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/components/ai_chat/core/common/buildflags/buildflags.gni")
import("//brave/components/brave_talk/buildflags/buildflags.gni")
import("//brave/components/brave_vpn/common/buildflags/buildflags.gni")
import("//brave/components/speedreader/common/buildflags/buildflags.gni")

static_library("policy_pref_interceptor") {
  sources = [
    "policy_pref_interceptor.cc",
    "policy_pref_interceptor.h",
  ]

  deps = [
    "//base",
    "//brave/components/ai_chat/core/common/buildflags",
    "//brave/components/brave_news/common",
    "//brave/components/brave_rewards/core",
    "//brave/components/brave_talk/buildflags",
    "//brave/components/brave_vpn/common/buildflags",
    "//brave/components/brave_wallet/browser:pref_names",
    "//brave/components/constants",
    "//brave/components/playlist/core/common",
    "//brave/components/speedreader/common/buildflags",
    "//components/prefs",
  ]

  if (enable_ai_chat) {
    deps += [ "//brave/components/ai_chat/core/common" ]
  }

  if (enable_brave_vpn) {
    deps += [ "//brave/components/brave_vpn/common" ]
  }

  if (enable_speedreader) {
    deps += [ "//brave/components/speedreader:pref_names" ]
  }

  if (enable_brave_talk) {
    deps += [ "//brave/components/brave_talk" ]
  }
}

static_library("brave_policy") {
  sources = [
    "brave_browser_policy_provider.cc",
    "brave_browser_policy_provider.h",
    "brave_policy_profile_id.cc",
    "brave_profile_policy_provider.cc",
    "brave_profile_policy_provider.h",
  ]

  deps = [
    ":brave_policy_observer",
    "//base",
    "//brave/components/brave_origin",
    "//brave/components/brave_policy/ad_block_only_mode",
    "//components/policy/core/browser",
    "//components/policy/core/common",
    "//components/prefs",
  ]
}

source_set("brave_policy_observer") {
  sources = [ "brave_policy_observer.h" ]

  public_deps = [ "//base" ]
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "brave_browser_policy_provider_unittest.cc",
    "brave_policy_conversions_unittest.cc",
    "brave_policy_source_unittest.cc",
    "brave_profile_policy_provider_unittest.cc",
    "policy_pref_interceptor_unittest.cc",
  ]

  deps = [
    ":brave_policy",
    ":policy_pref_interceptor",
    "//base",
    "//base/test:test_support",
    "//brave/components/brave_news/common",
    "//brave/components/brave_rewards/core",
    "//brave/components/resources:strings",
    "//components/enterprise",
    "//components/policy/core/browser",
    "//components/policy/core/common",
    "//components/policy/core/common:test_support",
    "//components/policy/proto",
    "//components/prefs:test_support",
    "//components/strings:components_strings_grit",
    "//testing/gtest",
  ]
}
