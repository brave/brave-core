# Copyright (c) 2019 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/browser/ethereum_remote_client/buildflags/buildflags.gni")
import("//brave/components/ai_rewriter/common/buildflags/buildflags.gni")
import("//brave/components/brave_vpn/common/buildflags/buildflags.gni")
import("//brave/components/playlist/common/buildflags/buildflags.gni")
import("//brave/components/playlist/common/buildflags/buildflags.gni")
import("//brave/components/speedreader/common/buildflags/buildflags.gni")
import("//brave/components/tor/buildflags/buildflags.gni")
import("//brave/resources/brave_grit.gni")
import("//build/config/locales.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/repack.gni")

brave_grit("static_resources") {
  source = "brave_components_resources.grd"

  output_name = "brave_components_resources_new"

  outputs = [
    "grit/brave_components_resources.h",
    "brave_components_static.pak",
  ]
  deps = [
    ":strings",
    "//brave/components/brave_rewards/resources",
    "//brave/components/brave_shields/resources:bundle_js",
    "//brave/components/decentralized_dns/content/resources:bundle_js",
    "//brave/components/request_otr/resources:bundle_js",
  ]

  if (enable_extensions) {
    deps += [ "//brave/components/brave_extension/extension/brave_extension" ]
  }

  if (enable_tor) {
    deps += [ "//brave/components/tor/resources" ]
  }

  defines = [
    "enable_brave_vpn=$enable_brave_vpn",
    "enable_playlist_webui=$enable_playlist_webui",
    "enable_playlist=$enable_playlist",
    "enable_speedreader=$enable_speedreader",
  ]

  output_dir = "$root_gen_dir/components"
}

repack("resources") {
  deps = [
    ":static_resources",
    "//brave/components/ai_chat/resources",
    "//brave/components/ai_chat/resources/custom_site_distiller_scripts:generated_resources",
    "//brave/components/ai_rewriter/common/buildflags",
    "//brave/components/brave_ads/browser/resources:generated_resources",
    "//brave/components/skus/browser/resources:generated_resources",
  ]
  sources = [
    "$root_gen_dir/brave/components/ai_chat/resources/ai_chat_ui_generated.pak",
    "$root_gen_dir/brave/components/ai_chat/resources/custom_site_distiller_scripts/custom_site_distiller_scripts_generated.pak",
    "$root_gen_dir/brave/components/brave_ads/browser/resources/ads_internals_generated.pak",
    "$root_gen_dir/brave/components/skus/browser/resources/skus_internals_generated.pak",
    "$root_gen_dir/components/brave_components_static.pak",
  ]

  if (!is_ios) {
    deps += [
      "//brave/components/brave_adblock_ui:generated_resources",
      "//brave/components/brave_adblock_ui/adblock_internals:generated_resources",
      "//brave/components/cosmetic_filters/resources/data:generated_resources",
    ]

    sources += [
      "$root_gen_dir/brave/components/brave_adblock/adblock_internals/resources/brave_adblock_internals_generated.pak",
      "$root_gen_dir/brave/components/brave_adblock/resources/brave_adblock_generated.pak",
      "$root_gen_dir/brave/components/cosmetic_filters/resources/cosmetic_filters_generated.pak",
    ]
  }

  if (!is_android && !is_ios) {
    deps += [
      "//brave/components/brave_new_tab_ui:generated_resources",
      "//brave/components/brave_news/browser/resources:generated_resources",
      "//brave/components/brave_news/browser/resources:generated_resources",
      "//brave/components/brave_private_new_tab_ui/resources/page:generated_resources",
      "//brave/components/brave_shields/resources/cookie_list_opt_in:cookie_list_opt_in_generated",
      "//brave/components/brave_shields/resources/panel:brave_shields_panel_generated",
      "//brave/components/brave_welcome_ui:generated_resources",
      "//brave/components/speedreader/resources/panel:generated_resources",
      "//brave/components/webcompat_reporter/ui:generated_resources",
    ]

    sources += [
      "$root_gen_dir/brave/components/brave_new_tab/resources/brave_new_tab_generated.pak",
      "$root_gen_dir/brave/components/brave_news/browser/resources/brave_news_internals_generated.pak",
      "$root_gen_dir/brave/components/brave_private_new_tab/resources/page/brave_private_new_tab_generated.pak",
      "$root_gen_dir/brave/components/brave_shields/resources/cookie_list_opt_in/cookie_list_opt_in_generated.pak",
      "$root_gen_dir/brave/components/brave_shields/resources/panel/brave_shields_panel_generated.pak",
      "$root_gen_dir/brave/components/brave_welcome/resources/brave_welcome_generated.pak",
      "$root_gen_dir/brave/components/speedreader/resources/panel/brave_speedreader_toolbar_generated.pak",
      "$root_gen_dir/brave/components/webcompat_reporter/resources/webcompat_reporter_generated.pak",
    ]
  }

  if (enable_ai_rewriter) {
    deps +=
        [ "//brave/components/ai_rewriter/resources/page:generated_resources" ]
    sources += [ "$root_gen_dir/brave/components/ai_rewriter/resources/page/ai_rewriter_ui_generated.pak" ]
  }

  if (!is_ios) {
    deps += [ "//brave/components/brave_wallet_ui:resources" ]
    sources += [
      "$root_gen_dir/brave/components/brave_wallet/resources/brave_wallet.pak",
    ]
  }

  deps += [ "//brave/components/brave_wallet/resources:ethereum_provider_generated_resources" ]
  sources += [ "$root_gen_dir/brave/components/brave_wallet/resources/brave_wallet_script_generated.pak" ]

  if (enable_brave_vpn_panel) {
    deps += [ "//brave/components/brave_vpn/resources/panel:resources" ]
    sources += [
      "$root_gen_dir/brave/components/brave_vpn/brave_vpn_panel_resources.pak",
    ]
  }

  if (ethereum_remote_client_enabled) {
    if (!is_android && !is_ios) {
      sources += [ "$root_gen_dir/brave/components/ethereum_remote_client_page/resources/ethereum_remote_client_page_generated.pak" ]
      deps += [ "//brave/browser/ui/webui/ethereum_remote_client:ethereum_remote_client_page_generated" ]
    }
  }

  if (enable_playlist_webui) {
    deps +=
        [ "//brave/components/playlist/browser/resources:generated_resources" ]
    sources += [ "$root_gen_dir/brave/components/playlist/browser/resources/playlist_generated.pak" ]
  }

  if (!is_android && !is_ios) {
    deps += [
      "//brave/browser/resources/settings/shortcuts_page:generated_resources",
    ]
    sources += [ "$root_gen_dir/brave/browser/resources/settings/shortcuts_page/commands_generated.pak" ]
  }

  output = "$root_gen_dir/components/brave_components_resources.pak"
}

brave_grit("strings") {
  source = "brave_components_strings.grd"

  outputs = [ "grit/brave_components_strings.h" ]

  defines = [
    "enable_speedreader=$enable_speedreader",
    "ethereum_remote_client_enabled=$ethereum_remote_client_enabled",
    "enable_tor=$enable_tor",
    "enable_brave_vpn=$enable_brave_vpn",
    "enable_playlist=$enable_playlist",
  ]

  foreach(locale, all_chrome_locales) {
    outputs += [ "brave_components_strings_$locale.pak" ]
  }

  output_dir = "$root_gen_dir/components"
}

action("about_credits") {
  script = "//brave/script/check_npm_licenses.py"

  inputs = [ "//brave/package.json" ]

  outputs = [ "$root_gen_dir/npm_licenses.checked" ]

  args = [ rebase_path("$root_gen_dir/npm_licenses.checked") ]
}
