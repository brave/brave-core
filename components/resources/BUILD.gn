import("//brave/components/brave_perf_predictor/browser/buildflags/buildflags.gni")
import("//brave/components/brave_rewards/browser/buildflags/buildflags.gni")
import("//brave/components/brave_wallet/browser/buildflags/buildflags.gni")
import("//brave/components/speedreader/buildflags.gni")
import("//build/config/locales.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/repack.gni")

grit("static_resources") {
  source = "brave_components_resources.grd"

  output_name = "brave_components_resources_new"

  outputs = [
    "grit/brave_components_resources.h",
    "brave_components_static.pak",
  ]
  deps = [
    ":strings",
  ]

  if (enable_extensions) {
    deps += [
      "//brave/components/brave_extension/extension/brave_extension",
    ]
  }
  if (brave_rewards_enabled) {
    deps += [
      "//brave/components/brave_rewards/resources",
    ]
    if (enable_extensions) {
      deps += [
        "//brave/components/brave_rewards/resources/extension:resources",
      ]
    }
  }

  defines = [
    "enable_brave_perf_predictor=$enable_brave_perf_predictor",
    "enable_speedreader=$enable_speedreader",
  ]

  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
  ]

  output_dir = "$root_gen_dir/components"
  resource_ids = "//brave/browser/resources/resource_ids"
}

repack("resources") {
  deps = [
    ":static_resources",
    "//brave/components/brave_adblock_ui:generated_resources",
    "//brave/components/webcompat_reporter/ui:generated_resources",
    "//brave/components/brave_new_tab_ui:generated_resources",
    "//brave/components/brave_welcome_ui:generated_resources",
  ]

  sources = [
    "$root_gen_dir/components/brave_components_static.pak",
    "$root_gen_dir/brave/components/brave_adblock/resources/brave_adblock_generated.pak",
    "$root_gen_dir/brave/components/webcompat_reporter/resources/webcompat_reporter_generated.pak",
    "$root_gen_dir/brave/components/brave_new_tab/resources/brave_new_tab_generated.pak",
    "$root_gen_dir/brave/components/brave_welcome/resources/brave_welcome_generated.pak",
  ]

  if (brave_wallet_enabled) {
    deps += [
      "//brave/components/brave_wallet_ui:generated_resources",
    ]
    sources += [
      "$root_gen_dir/brave/components/brave_wallet/resources/brave_wallet_generated.pak",
    ]
  }

  output = "$root_gen_dir/components/brave_components_resources.pak"
}

grit("strings") {
  source = "brave_components_strings.grd"

  outputs = [
    "grit/brave_components_strings.h",
  ]

  defines = [
    "enable_speedreader=$enable_speedreader",
  ]

  foreach(locale, locales_with_fake_bidi) {
    outputs += [ "brave_components_strings_$locale.pak" ]
  }

  output_dir = "$root_gen_dir/components"
  resource_ids = "//brave/browser/resources/resource_ids"
}
