# Copyright (c) 2021 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//brave/components/ntp_background_images/buildflags/buildflags.gni")

static_library("browser") {
  sources = [
    "features.cc",
    "features.h",
    "new_tab_takeover_infobar_util.cc",
    "new_tab_takeover_infobar_util.h",
    "ntp_background_images_component_installer.cc",
    "ntp_background_images_component_installer.h",
    "ntp_background_images_data.cc",
    "ntp_background_images_data.h",
    "ntp_background_images_service.cc",
    "ntp_background_images_service.h",
    "ntp_background_images_update_util.cc",
    "ntp_background_images_update_util.h",
    "ntp_p3a_helper.h",
    "ntp_p3a_util.cc",
    "ntp_p3a_util.h",
    "ntp_sponsored_images_data.cc",
    "ntp_sponsored_images_data.h",
    "sponsored_images_component_data.cc",
    "sponsored_images_component_data.h",
    "switches.h",
    "url_constants.h",
  ]

  if (!is_ios) {
    sources += [
      "ntp_background_images_source.cc",
      "ntp_background_images_source.h",
      "ntp_sponsored_image_source.cc",
      "ntp_sponsored_image_source.h",
      "ntp_sponsored_rich_media_ad_event_handler.cc",
      "ntp_sponsored_rich_media_ad_event_handler.h",
      "ntp_sponsored_rich_media_source.cc",
      "ntp_sponsored_rich_media_source.h",
      "ntp_sponsored_source_util.cc",
      "ntp_sponsored_source_util.h",
      "view_counter_model.cc",
      "view_counter_model.h",
      "view_counter_service.cc",
      "view_counter_service.h",
    ]
  }

  deps = [
    "//base",
    "//brave/components/brave_ads/core",
    "//brave/components/brave_component_updater/browser",
    "//brave/components/brave_rewards/core",
    "//brave/components/constants",
    "//brave/components/ntp_background_images/buildflags",
    "//brave/components/ntp_background_images/common",
    "//brave/components/p3a_utils",
    "//brave/components/time_period_storage",
    "//components/component_updater",
    "//components/content_settings/core/browser",
    "//components/content_settings/core/common",
    "//components/keyed_service/core",
    "//components/metrics",
    "//components/pref_registry:pref_registry",
    "//components/prefs",
    "//components/variations",
    "//components/variations/service",
    "//crypto",
    "//services/network/public/cpp",
    "//ui/gfx/geometry",
    "//url",
  ]

  public_deps = [ "//brave/components/ntp_background_images/browser/mojom" ]

  if (is_ios) {
    deps += [ "//brave/extensions:common" ]
  } else {
    deps += [
      "//brave/components/brave_ads/browser",
      "//brave/components/brave_referrals/browser",
      "//content/public/browser",
      "//content/public/common",
      "//net",
    ]
  }

  if (enable_custom_background) {
    sources += [
      "brave_ntp_custom_background_service.cc",
      "brave_ntp_custom_background_service.h",
      "ntp_custom_images_source.cc",
      "ntp_custom_images_source.h",
    ]
  }
}

source_set("test_support") {
  testonly = true

  deps = [
    ":browser",
    "//base",
    "//brave/components/brave_ads/core/mojom",
    "//testing/gmock",
  ]

  sources = [
    "ntp_background_images_service_waiter.cc",
    "ntp_background_images_service_waiter.h",
    "ntp_p3a_helper_mock.cc",
    "ntp_p3a_helper_mock.h",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [ "new_tab_takeover_infobar_util_unittest.cc" ]

  deps = [
    ":browser",
    "//base",
    "//base/test:test_support",
    "//brave/components/brave_ads/core",
    "//brave/components/brave_rewards/core",
    "//brave/components/ntp_background_images/common",
    "//components/sync_preferences:test_support",
    "//testing/gtest",
  ]
}
