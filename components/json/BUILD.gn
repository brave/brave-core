# Copyright (c) 2022 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//testing/test.gni")

import("//tools/json_schema_compiler/json_features.gni")
import("//tools/json_schema_compiler/json_schema_api.gni")

generated_types("schema") {
  sources = [ "schema.idl" ]

  root_namespace = "json::%(namespace)s"
}

component("json") {
  sources = [
    "json_export.h",
    "json_helper.cc",
    "json_helper.h",
  ]

  defines = [ "JSON_HELPER_IMPLEMENTATION" ]

  deps = [
    "rs:rust_lib",
    "//base",
  ]
}

source_set("brave_json_unit_tests") {
  testonly = true
  sources = [
    "json_64bit_support_unittest.cc",
    "json_parser_unittest.cc",
  ]
  deps = [
    ":schema",
    "//base",
    "//base/test:test_support",
    "//brave/components/json",
    "//testing/gmock",
    "//testing/gtest",
  ]

  if (!is_android) {
    # Android seems to have some issue running these base tests, specially when
    # using base::DIR_TEST_DATA
    sources += [
      "//base/json/json_reader_unittest.cc",
      "//base/json/json_writer_unittest.cc",
    ]
    deps += [ "//third_party/fuzztest" ]
  }
}
