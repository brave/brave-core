import("//brave/components/brave_referrals/buildflags/buildflags.gni")
if (is_android) {
  import("//build/config/android/rules.gni")
}

source_set("browser") {
  # Remove when https://github.com/brave/brave-browser/issues/10646 is resolved
  check_includes = false
  public_deps = [
    "//brave/components/brave_referrals/buildflags",
  ]

  if (enable_brave_referrals) {
    sources = [
      "brave_referrals_service.cc",
      "brave_referrals_service.h",
    ]

    deps = [
      "//base",
      "//brave/common",
      "//brave/components/brave_referrals/common",
      "//brave/components/brave_stats/browser",
      "//brave/vendor/brave_base",
      "//chrome/common",
      "//components/prefs",
      "//content/public/browser",
      "//net",
      "//services/network/public/cpp",
    ]

    if (is_android) {
      sources += [
        "android_brave_referrer.cc",
        "android_brave_referrer.h",
      ]

      deps += [
        ":jni_headers",
        "//brave/components/safetynet:android",
      ]
    }
  }
}

if (is_android) {
  generate_jni("jni_headers") {
    sources = [
      "//brave/android/java/org/chromium/chrome/browser/util/BraveReferrer.java",
    ]
  }
}
