# Copyright (c) 2019 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

if (is_android) {
  import("//build/config/android/rules.gni")
  import("//third_party/jni_zero/jni_zero.gni")
}

assert(!is_ios)

static_library("browser") {
  sources = [
    "brave_referrals_service.cc",
    "brave_referrals_service.h",
  ]

  public_deps = [ "//brave/components/brave_referrals/common" ]

  deps = [
    "//base",
    "//brave/brave_domains",
    "//brave/components/brave_stats/browser",
    "//brave/components/constants",
    "//brave/vendor/brave_base",
    "//components/prefs",
    "//components/url_matcher",
    "//content/public/browser",
    "//net",
    "//services/network/public/cpp",
  ]

  if (is_mac) {
    sources += [
      "file_extended_attribute.cc",
      "file_extended_attribute.h",
    ]
  }

  if (is_android) {
    sources += [
      "android_brave_referrer.cc",
      "android_brave_referrer.h",
    ]

    deps += [
      ":jni_headers",
      "//brave/components/safetynet:android",
    ]
  }
}

if (is_android) {
  generate_jni("jni_headers") {
    sources = [ "//brave/android/java/org/chromium/chrome/browser/util/BraveReferrer.java" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = []
  deps = []

  if (is_mac) {
    sources += [ "file_extended_attribute_unittest.cc" ]
    deps += [
      ":browser",
      "//base",
      "//base/test:test_support",
      "//testing/gtest",
    ]
  }
}
