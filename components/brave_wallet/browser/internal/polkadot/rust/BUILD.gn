# Copyright (c) 2025 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//build/rust/rust_static_library.gni")

group("rust") {
  # This should only be used by internal wallet code
  visibility = [ "//brave/components/brave_wallet/browser/internal:*" ]

  public_deps = [ ":polkadot_headers" ]

  deps = [ ":polkadot_impl" ]
}

source_set("polkadot_headers") {
  visibility = [ ":*" ]

  sources = [ "sr25519.h" ]

  public_deps = [
    "//base",
    "//brave/components/brave_wallet/common",
  ]
}

source_set("polkadot_impl") {
  visibility = [ ":rust" ]

  sources = [ "sr25519.cc" ]

  deps = [
    ":polkadot_headers",
    ":rust_lib",
    "//brave/components/brave_wallet/common",
    "//build/rust:cxx_cppdeps",
  ]
}

rust_static_library("rust_lib") {
  visibility = [ ":polkadot_impl" ]

  crate_name = "sr25519"
  crate_root = "lib.rs"
  allow_unsafe = true
  sources = [ "lib.rs" ]
  cxx_bindings = [ "lib.rs" ]

  deps = [
    "//brave/third_party/rust/blake2b_simd/v1:lib",
    "//brave/third_party/rust/schnorrkel/v0_11:lib",
  ]
}
