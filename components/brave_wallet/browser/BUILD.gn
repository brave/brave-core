import("//brave/build/config.gni")
import("//brave/components/brave_wallet/browser/config.gni")
import("//brave/components/brave_wallet/common/buildflags/buildflags.gni")
import("//build/config/features.gni")

assert(brave_wallet_enabled)

static_library("browser") {
  configs += [ ":infura_config" ]
  include_dirs = [ "//brave/third_party/ethash/src/include" ]

  sources = [
    "asset_ratio_controller.cc",
    "asset_ratio_controller.h",
    "asset_ratio_response_parser.cc",
    "asset_ratio_response_parser.h",
    "brave_wallet_constants.cc",
    "brave_wallet_constants.h",
    "brave_wallet_provider_delegate.h",
    "brave_wallet_provider_impl.cc",
    "brave_wallet_provider_impl.h",
    "brave_wallet_types.cc",
    "brave_wallet_types.h",
    "brave_wallet_utils.cc",
    "brave_wallet_utils.h",
    "eip1559_transaction.cc",
    "eip1559_transaction.h",
    "eip2930_transaction.cc",
    "eip2930_transaction.h",
    "erc_token_list_parser.cc",
    "erc_token_list_parser.h",
    "erc_token_registry.cc",
    "erc_token_registry.h",
    "eth_address.cc",
    "eth_address.h",
    "eth_block_tracker.cc",
    "eth_block_tracker.h",
    "eth_data_builder.cc",
    "eth_data_builder.h",
    "eth_json_rpc_controller.cc",
    "eth_json_rpc_controller.h",
    "eth_nonce_tracker.cc",
    "eth_nonce_tracker.h",
    "eth_pending_tx_tracker.cc",
    "eth_pending_tx_tracker.h",
    "eth_requests.cc",
    "eth_requests.h",
    "eth_response_parser.cc",
    "eth_response_parser.h",
    "eth_transaction.cc",
    "eth_transaction.h",
    "eth_tx_controller.cc",
    "eth_tx_controller.h",
    "eth_tx_state_manager.cc",
    "eth_tx_state_manager.h",
    "hd_key.cc",
    "hd_key.h",
    "hd_keyring.cc",
    "hd_keyring.h",
    "keyring_controller.cc",
    "keyring_controller.h",
    "password_encryptor.cc",
    "password_encryptor.h",
    "pref_names.cc",
    "pref_names.h",
    "rlp_decode.cc",
    "rlp_decode.h",
    "rlp_encode.cc",
    "rlp_encode.h",
    "swap_controller.cc",
    "swap_controller.h",
    "swap_response_parser.cc",
    "swap_response_parser.h",
    "wallet_data_files_installer.cc",
    "wallet_data_files_installer.h",
  ]

  deps = [
    "//base",
    "//base/util/values:values_util",
    "//brave/components/api_request_helper",
    "//brave/components/brave_component_updater/browser",
    "//brave/components/brave_wallet/browser:ethereum_permission_utils",
    "//brave/components/brave_wallet/common",
    "//brave/components/brave_wallet/common:common_constants",
    "//brave/components/brave_wallet/common:mojom",
    "//brave/components/resources:strings_grit",
    "//brave/third_party/bitcoin-core",
    "//brave/third_party/bitcoin-core:secp256k1",
    "//brave/third_party/ethash",
    "//brave/vendor/bip39wally-core-native:bip39wally-core",
    "//components/component_updater",
    "//components/keyed_service/core",
    "//components/prefs",
    "//components/sync_preferences",
    "//crypto",
    "//services/network/public/cpp",
    "//third_party/abseil-cpp:absl",
    "//third_party/boringssl",
    "//ui/base:base",
    "//url",
  ]
}

config("infura_config") {
  defines = [ "BRAVE_INFURA_PROJECT_ID=\"$brave_infura_project_id\"" ]
}

static_library("ethereum_permission_utils") {
  sources = [
    "ethereum_permission_utils.cc",
    "ethereum_permission_utils.h",
  ]

  deps = [
    "//base",
    "//third_party/re2",
    "//url",
  ]
}
