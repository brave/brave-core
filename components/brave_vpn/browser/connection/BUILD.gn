# Copyright (c) 2022 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/components/brave_vpn/common/buildflags/buildflags.gni")

assert(enable_brave_vpn)

source_set("api") {
  sources = [
    "brave_vpn_connection_manager.cc",
    "brave_vpn_connection_manager.h",
    "connection_api_impl.cc",
    "connection_api_impl.h",
  ]

  deps = [
    "//base",
    "//brave/components/brave_vpn/browser/api",
    "//brave/components/brave_vpn/browser/connection:common",
    "//brave/components/brave_vpn/common",
    "//brave/components/brave_vpn/common/buildflags",
    "//brave/components/brave_vpn/common/mojom",
    "//components/prefs",
    "//components/version_info:channel",
    "//net",
    "//services/network/public/cpp",
  ]
}

group("connection") {
  deps = [
    ":api",
    ":common",
  ]
  if (is_win) {
    deps += [ "ikev2/win" ]
  }

  if (is_mac) {
    deps += [ "ikev2/mac" ]
    deps += [ "wireguard/mac" ]
  }
}

source_set("common") {
  sources = [
    "brave_vpn_connection_info.cc",
    "brave_vpn_connection_info.h",
    "brave_vpn_region_data_helper.cc",
    "brave_vpn_region_data_helper.h",
    "brave_vpn_region_data_manager.cc",
    "brave_vpn_region_data_manager.h",
  ]
  deps = [
    "//brave/components/api_request_helper",
    "//brave/components/brave_vpn/browser/api",
    "//brave/components/brave_vpn/common",
    "//brave/components/brave_vpn/common/mojom",
    "//brave/components/skus/browser",
    "//brave/components/skus/common:mojom",
    "//build:buildflag_header_h",
    "//components/prefs",
    "//services/network/public/cpp",
    "//url",
  ]
}

group("unit_tests") {
  testonly = true
  deps = []
  if (!is_android) {
    deps += [
      "ikev2:unit_tests",
      "wireguard:unit_tests",
    ]
  }
}
