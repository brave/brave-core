# Copyright (c) 2022 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//build/buildflag_header.gni")

declare_args() {
  updater_dev_endpoint = ""
  updater_prod_endpoint = ""
}

if (is_official_build) {
  assert(updater_dev_endpoint != "")
  assert(updater_prod_endpoint != "")
}

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [
    "UPDATER_DEV_ENDPOINT=\"$updater_dev_endpoint\"",
    "UPDATER_PROD_ENDPOINT=\"$updater_prod_endpoint\"",
  ]
}

static_library("update_client") {
  sources = [
    "brave_protocol_handler.cc",
    "brave_protocol_handler.h",
    "brave_protocol_serializer_json.cc",
    "brave_protocol_serializer_json.h",
  ]
  deps = [
    "//base",
    "//components/update_client",
  ]
}

source_set("unit_tests") {
  testonly = true
  sources = [ "brave_protocol_handler_unittest.cc" ]
  deps = [
    ":update_client",
    "//base",
    "//components/prefs",
    "//components/prefs:test_support",
    "//components/update_client",
    "//testing/gtest",
    "//third_party/re2",
  ]
}
