# Copyright (c) 2023 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//ui/webui/resources/tools/bundle_js.gni")

# Generate JS bundle for RequestOTR interstitial UI, regradless of the
# `optimize_webui` GN arg, as this UI can't load any JS modules at runtime,
# since it is not a proper WebUI page and therefore is not backed up by any
# WebUIDataSource.
group("bundle_js") {
  public_deps = [ ":bundle_js_decentralized_dns_interstitial" ]
}

bundle_js("bundle_js_decentralized_dns_interstitial") {
  # Using a dummy value for`host` which is ignored anyway since the code being
  # bundled below is not served from any host.
  host = "_ignored_"
  input = rebase_path(".", root_build_dir)
  out_folder = "${target_gen_dir}/decentralized_dns_interstitial"
  js_module_in_files = [ "decentralized_dns_interstitial.js" ]

  interstitials_path =
      rebase_path("${root_gen_dir}/components/security_interstitials/core/",
                  root_build_dir)
  external_paths = [ "chrome://interstitials/|$interstitials_path" ]
  deps = [
    "../../../../../components/security_interstitials/core/common/resources:preprocess",
    "//ui/webui/resources/js:build_ts",
  ]
}
