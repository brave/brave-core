# Copyright (c) 2021 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/components/ai_chat/core/common/buildflags/buildflags.gni")
import("//brave/components/brave_wallet/common/buildflags/buildflags.gni")

# Generate a JSON config file with build flags for Jest tests
generated_file("jest_build_config") {
  outputs = [ "$root_gen_dir/brave/jest_build_config.json" ]

  contents = {
    enable_ai_chat = enable_ai_chat
    enable_brave_wallet = enable_brave_wallet
  }

  output_conversion = "json"
}

action("generate_tsconfig") {
  script = "//brave/script/generate-tsconfig.py"
  deps = [
    ":jest_build_config",

    # Ensure chrome://resources/js file and typescript definitions are
    # generated
    "//ui/webui/resources/js:build_ts",
  ]
  inputs = [
    "//brave/package.json",
    "//brave/build/commands/lib/config.js",
    "//brave/build/commands/lib/genTsConfig.js",
    "//brave/build/commands/scripts/genTsConfig.js",
    "path-map.js",
  ]
  outputs = [ "$root_gen_dir/tsconfig-webpack.json" ]
  args = [ "--root_gen_dir=" + rebase_path(root_gen_dir) ]
}
