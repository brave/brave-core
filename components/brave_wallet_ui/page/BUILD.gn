# Copyright (c) 2021 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# you can obtain one at http://mozilla.org/MPL/2.0/.

import("//brave/components/brave_wallet/common/buildflags/buildflags.gni")

assert(enable_brave_wallet)
import("//brave/components/common/typescript.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//tools/grit/preprocess_if_expr.gni")

transpile_web_ui("brave_wallet_page_ui") {
  output_module = false
  entry_points = [ [
        "brave_wallet_page",
        rebase_path("wallet_page.tsx"),
      ] ]
  resource_name = "brave_wallet_page"
  imports_from = [
    "//brave/components/brave_rewards/resources/",
    "//brave/components/brave_wallet_ui/",
  ]
  deps = [
    "//brave/components/brave_rewards/core/mojom:webui_js",
    "//brave/components/brave_wallet/common:mojom_js",
    "//brave/components/brave_wallet/common:preprocess_mojo",
  ]
}

preprocess_if_expr("preprocess_html") {
  visibility = [
    ":brave_wallet_page_generated_grit",

    # Also used by brave_components_resources.grd
    "//brave/components/resources:static_resources_grit",
  ]
  in_folder = "."
  in_files = [ "wallet_page.html" ]
  out_folder = "$target_gen_dir/preprocessed"
}

pack_web_resources("brave_wallet_page_generated") {
  resource_name = "brave_wallet_page"
  output_dir = "$root_gen_dir/brave/components/brave_wallet_page/resources"
  deps = [
    ":brave_wallet_page_ui",
    ":preprocess_html",
  ]
}
