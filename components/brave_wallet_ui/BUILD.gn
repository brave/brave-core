# Copyright (c) 2021 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# you can obtain one at http://mozilla.org/MPL/2.0/.

import("//brave/components/common/typescript.gni")
import("//chrome/common/features.gni")
import("//mojo/public/tools/bindings/mojom.gni")
import("//tools/grit/preprocess_if_expr.gni")

preprocess_folder = "preprocessed"
preprocess_manifest = "preprocessed_manifest.json"
preprocess_mojo_manifest = "preprocessed_mojo_manifest.json"

mojom("mojo_bindings") {
  sources = [ "wallet_panel.mojom" ]
  public_deps = [ "//mojo/public/mojom/base" ]
}

transpile_web_ui("brave_wallet_ui") {
  entry_points = [ [
        "brave_wallet",
        rebase_path("brave_wallet.tsx"),
      ] ]
  output_dir = "$root_gen_dir/brave/components/brave_wallet/resources"
  resource_name = "brave_wallet"
}

pack_web_resources("page_generated_resources") {
  resource_name = "brave_wallet"
  output_dir = "$root_gen_dir/brave/components/brave_wallet/resources"
  generated_dir = "$root_gen_dir/brave/components/brave_wallet/resources"
  deps = [ ":brave_wallet_ui" ]
}

transpile_web_ui("brave_wallet_panel_ui") {
  entry_points = [ [
        "brave_wallet_panel",
        rebase_path("brave_wallet_panel.tsx"),
      ] ]
  resource_name = "brave_wallet_panel"
  output_dir = "$root_gen_dir/brave/components/brave_wallet/resources"
  deps = [
    ":mojo_bindings_js",
    ":preprocess",
    ":preprocess_mojo",
    ":wallet_panel_api_proxy",
  ]
}

pack_web_resources("panel_generated_resources") {
  resource_name = "brave_wallet_panel"
  generated_dir = "$root_gen_dir/brave/components/brave_wallet/resources"
  output_dir = "$root_gen_dir/brave/components/brave_wallet/resources"
  deps = [ ":brave_wallet_panel_ui" ]
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_manifest"
  in_files = [ "wallet_panel_api_proxy.js" ]
}

preprocess_if_expr("preprocess_mojo") {
  deps = [ ":mojo_bindings_js" ]
  in_folder = "$target_gen_dir"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_mojo_manifest"
  in_files = [ "wallet_panel.mojom-lite.js" ]
}

js_library("wallet_panel_api_proxy") {
  deps = [
    ":mojo_bindings_js_library_for_compile",
    "//ui/webui/resources/js:cr.m",
  ]
}
