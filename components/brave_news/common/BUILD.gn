# Copyright (c) 2021 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/build/config.gni")
import("//brave/components/brave_news/common/buildflags/buildflags.gni")
import("//mojo/public/tools/bindings/mojom.gni")

# The mojom target is always built so TypeScript can import types.
# The C++ implementation is conditionally built based on enable_brave_news.
mojom("mojom") {
  generate_java = true
  sources = [ "brave_news.mojom" ]

  deps = [
    "//mojo/public/mojom/base",
    "//url/mojom:url_mojom_gurl",
  ]
}

if (enable_brave_news) {
  source_set("unit_tests") {
    testonly = true

    sources = [
      "brave_news_policy_unittest.cc",
      "locales_helper_unittest.cc",
      "subscriptions_snapshot_unittest.cc",
    ]

    deps = [
      ":common",
      "//base",
      "//components/sync_preferences:test_support",
      "//testing/gtest",
    ]
  }

  static_library("common") {
    sources = [
      "features.cc",
      "features.h",
      "locales_helper.cc",
      "locales_helper.h",
      "p3a_pref_names.cc",
      "p3a_pref_names.h",
      "pref_names.cc",
      "pref_names.h",
      "subscriptions_snapshot.cc",
      "subscriptions_snapshot.h",
      "switches.h",
      "types.cc",
      "types.h",
    ]

    public_deps = [
      ":mojom",
      "//brave/components/brave_news/common/buildflags",
    ]

    deps = [
      "//base",
      "//brave/components/l10n/common",
      "//brave/components/p3a_utils",
      "//components/prefs",
      "//url",
    ]
  }
}
