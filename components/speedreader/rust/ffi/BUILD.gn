# Copyright 2020 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/. */

import("config.gni")
import("//brave/build/cargo.gni")
import("//brave/test/testing.gni")

config("external_config") {
  include_dirs = [ "include" ]
  defines = [
    "NO_CXXEXCEPTIONS=1"
  ]
}

source_set("speedreader_ffi") {
  public_configs = [ ":external_config" ]

  sources = [
    "speedreader.cc",
    "speedreader.h"
  ]

  deps = [
    "//base",
    ":speedreader",
    ":ffi_bindings",
  ]

  libs = [ speedreader_lib_path ]
}

cargo_build("speedreader") {
  inputs = [
    "../src/src/whitelist.rs",
    "Cargo.toml",
    "Cargo.lock",
    "charbuf.rs",
    "errors.rs",
    "lib.rs",
    "speedreader.rs",
  ]

  output = speedreader_lib_path

  manifest_path = "Cargo.toml"
  build_path = speedreader_build_path
  target = rustc_target
}

cargo_cbindgen("ffi_bindings") {
  config = "cbindgen.toml"
  output = "speedreader_ffi.h"

  sources = [
    "../src/src/whitelist.rs",
    "Cargo.toml",
    "Cargo.lock",
    "cbindgen.toml",
    "charbuf.rs",
    "errors.rs",
    "lib.rs",
    "speedreader.rs",
  ]
}

if (speedreader_shared_lib_install_name != "") {
  fix_testing_install_name("test_speedreader_install_name") {
    current_path = speedreader_shared_lib_install_name
    new_path = "@executable_path/" + rebase_path(speedreader_lib_path, root_build_dir)
  }
} else {
    group("test_speedreader_install_name") {}
}

if (is_mac) {
  bundle_data("speedreader_libs") {
    sources = [
      speedreader_lib_path,
    ]
    outputs = [
      "{{bundle_contents_dir}}/Libraries/{{source_file_part}}",
    ]
    public_deps = [
      ":speedreader",
    ]
  }
} else {
  group("speedreader_libs") {}
}
