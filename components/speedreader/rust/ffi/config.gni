declare_args() {
  use_speedreader_shared_lib = is_mac
  speedreader_flags = ""
}

speedreader_shared_lib_install_name = ""

if (use_speedreader_shared_lib && speedreader_flags == "") {
  if (!is_component_build) {
    speedreader_shared_lib_install_name = "@loader_path/../../Libraries/libspeedreader_ffi.dylib"
    speedreader_flags = "-C link-args=-Wl,-install_name,$speedreader_shared_lib_install_name"
  }
}

speedreader_build_path = "$root_gen_dir/speedreader/out"

speedreader_lib_path = speedreader_build_path

speedreader_target = ""

# See https://forge.rust-lang.org/platform-support.html for possible targets
if (is_win) {
  if (current_cpu == "x86") {
    speedreader_flags += " -C target-feature=+crt-static"
    speedreader_target = "i686-pc-windows-msvc"
  } else if (current_cpu == "x64") {
    speedreader_target = "x86_64-pc-windows-msvc"
  }
} else if (is_mac) {
  if (current_cpu == "x64") {
    speedreader_target = "x86_64-apple-darwin"
  }
} else if (is_linux) {
  if (current_cpu == "x64") {
    speedreader_target = "x86_64-unknown-linux-gnu"
  }
} else if (is_android) {
  if (current_cpu == "arm") {
    speedreader_target = "arm-linux-androideabi"
  } else if (current_cpu == "arm64") {
    speedreader_target = "aarch64-linux-android"
  } else if (current_cpu == "x86") {
    speedreader_target = "i686-linux-android"
  } else if (current_cpu == "x64") {
    speedreader_target = "x86_64-linux-android"
  }
} else if (is_ios) {
  if (current_cpu == "arm64") {
    speedreader_target = "aarch64-apple-ios"
  } else if (current_cpu == "x64") {
    speedreader_target = "x86_64-apple-ios"
  }
}

if (speedreader_target != "") {
  speedreader_lib_path += "/" + speedreader_target
}

if (is_debug) {
  speedreader_lib_path += "/debug"
} else {
  speedreader_lib_path += "/release"
}

if (is_win) {
  speedreader_lib_path += "/speedreader.lib"
} else if (is_linux || is_android || is_ios) {
  speedreader_lib_path += "/libspeedreader_ffi.a"
} else if (is_mac) {
  if (use_speedreader_shared_lib) {
    speedreader_lib_path += "/libspeedreader_ffi.dylib"
  } else {
    speedreader_lib_path += "/libspeedreader_ffi.a"
  }
}
