import("//build/buildflag_header.gni")
import("//brave/components/speedreader/buildflags.gni")
import("//brave/build/config.gni")

buildflag_header("buildflags") {
  header = "buildflags.h"
  enable_speedreader_feature = brave_channel == "nightly"
    || brave_channel == "development"
    || brave_channel == "dev"
    || brave_channel == "beta"
  flags = [
    "ENABLE_SPEEDREADER=$enable_speedreader",
    "ENABLE_SPEEDREADER_FEATURE=$enable_speedreader_feature",
  ]
}

source_set("speedreader") {
  public_deps = [
    ":buildflags",
    "//brave/build/rust",
  ]

  sources = [
    "features.cc",
    "features.h",
    "speedreader_component.cc",
    "speedreader_component.h",
    "speedreader_pref_names.h",
    "speedreader_rewriter_service.cc",
    "speedreader_rewriter_service.h",
    "speedreader_service.cc",
    "speedreader_service.h",
    "speedreader_switches.h",
    "speedreader_test_whitelist.cc",
    "speedreader_test_whitelist.h",
    "speedreader_throttle.cc",
    "speedreader_throttle.h",
    "speedreader_url_loader.cc",
    "speedreader_url_loader.h",
  ]

  deps = [
    "//brave/components/brave_component_updater/browser",
    "//brave/components/resources:static_resources_grit",
    "//brave/components/resources",
    "//brave/components/weekly_storage",
    "//components/keyed_service/core:core",
    "//components/prefs:prefs",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
    "//third_party/blink/public/common",
    "//ui/base",  # For ResourceBundle, consider getting rid of this?
    "//url",
  ]
}
