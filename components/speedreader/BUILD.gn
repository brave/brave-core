import("//build/buildflag_header.gni")
import("//brave/components/speedreader/buildflags.gni")
import("//brave/components/speedreader/rust/ffi/config.gni")

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [
    "ENABLE_SPEEDREADER=$enable_speedreader",
  ]
}

source_set("speedreader") {
  sources = [
    "speedreader_loader.cc",
    "speedreader_loader.h",
    "speedreader_pref_names.h",
    "speedreader_service.cc",
    "speedreader_service.h",
    "speedreader_switches.h",
    "speedreader_test_whitelist.cc",
    "speedreader_test_whitelist.h",
    "speedreader_throttle.cc",
    "speedreader_throttle.h",
    "speedreader_whitelist.cc",
    "speedreader_whitelist.h",
  ]

  deps = [
    "//brave/components/brave_component_updater/browser",
    "//brave/components/resources",
    "//brave/components/speedreader:buildflags",
    "//brave/vendor/adblock_rust_ffi:adblock_ffi",
    "//brave/components/speedreader/rust/ffi:speedreader_ffi",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
    "//ui/base",  # For ResourceBundle, consider getting rid of this?
    "//url",
  ]

  public_deps = [
    ":speedreader_libs"
  ]
}

if (is_mac) {
  bundle_data("speedreader_libs") {
    sources = [
      speedreader_lib_path,
    ]
    outputs = [
      "{{bundle_contents_dir}}/Libraries/{{source_file_part}}",
    ]
    public_deps = [
      "//brave/components/speedreader/rust/ffi:speedreader",
    ]
  }
} else {
  group("speedreader_libs") {}
}
