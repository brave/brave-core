import("//tools/grit/repack.gni")

declare_args() {
  if (is_mac) {
    mac_signing_identifier = ""
  }
}

group("child_dependencies") {
  public_deps = [
    "//brave/renderer",
    "//brave/utility",
    "common"
  ]
}

group("browser_dependencies") {
  public_deps = [
    "//brave/browser",
    "common",
    ":brave_framework_resources",
  ]
}

group("brave") {
  public_deps = [
    "//chrome",
  ]
}

repack("packed_extra_resources") {
  visibility = [ "./*" ]
  sources = [
    "$root_gen_dir/components/brave_components_resources.pak",
    "$target_gen_dir/browser/resources/brave_extension.pak",
  ]

  deps = [
    "//brave/components/resources:brave_components_resources_grit",
    "//brave/browser/resources:brave_extension_grit"
  ]

  deps += [
    "//brave/common/extensions/api"
  ]

  if (is_mac) {
    output = "$root_gen_dir/repack/brave_resources.pak"
  } else {
    output = "$root_out_dir/brave_resources.pak"
  }
}

group("packed_resources") {
  public_deps = [
    ":packed_extra_resources",
  ]
}

brave_framework_public_deps = [
  ":packed_resources",
]

if (is_mac) {
  bundle_data("brave_framework_resources") {
    sources = [
      "$root_gen_dir/repack/brave_resources.pak",
    ]
    public_deps = brave_framework_public_deps

    outputs = [
      "{{bundle_resources_dir}}/{{source_file_part}}",
    ]
  }
} else {
  group("brave_framework_resources") {
    public_deps = brave_framework_public_deps
  }
}

if (is_mac) {
  action("create_dist") {
    output = "$root_out_dir/Brave.dmg"
    script = "//build/gn_run_binary.py"
    shell_script = "//chrome/installer/mac/pkg-dmg"
    inputs = [
      script,
      shell_script,
    ]
    outputs = [ output ]
    args = [
      rebase_path(shell_script, root_build_dir),
      "--source",
      "/var/empty",
      "--target",
      "Brave.dmg",
      "--format",
      "UDBZ",
      "--verbosity",
      "0",
      "--volname",
      "Brave",
      "--copy",
      "Brave.app",
      "--tempdir",
      "/tmp",
    ]
  }
} else {
  group("create_dist") {
    deps = [
      "//chrome/installer/mini_installer"
    ]
  }
}
