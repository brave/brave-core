if (is_android || is_ios) {
  source_set("common") {
    sources = [
      "//extensions/common/error_utils.cc",
      "//extensions/common/error_utils.h",
      "//extensions/common/url_pattern.cc",
      "//extensions/common/url_pattern.h",
      "//extensions/common/url_pattern_set.cc",
      "//extensions/common/url_pattern_set.h",
    ]

    deps = [
      "//base",
      "//components/url_pattern_index",
      "//extensions/buildflags",
      "//net:net",
      "//url",
    ]

    if (is_ios) {
      sources += [
        "//content/public/common/url_constants.cc",
        "//content/public/common/url_constants.h",
      ]
    } else {
      deps += [ "//content/public/common" ]
    }

    public_deps = [
      "//extensions/common:common_constants",

      # Upstream bug: common_constants (above) has a dep on
      # //components/services/app_service/public/mojom, which should have a dep
      # on //ui/gfx/image/mojom, but doesn't, so we have to include it directly.
      "//ui/gfx/image/mojom",
    ]
  }
} else {
  group("common") {
    public_deps = [ "//extensions/common" ]
  }
}
