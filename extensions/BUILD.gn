# Copyright (c) 2018 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//extensions/buildflags/buildflags.gni")

# DO NOT ADD TO THIS LIST
# Please use an alternative like url_pattern_index::UrlPattern,
# base::MatchPattern, base::StartsWith,
# content_settings::ContentSettingsPattern, etc..
deprecated_visibility_allowed = [
  "//brave/components/static_redirect_helper",
  "//brave/components/url_sanitizer/core/browser",
  "//brave/components/debounce/core/browser",
  "//brave/components/ntp_background_images/browser",
  "//brave/components/psst/browser/core",
  "//brave/components/request_otr/browser",
  "//chrome/browser",
]

if (enable_extensions) {
  group("common") {
    visibility = deprecated_visibility_allowed
    public_deps = [ "//extensions/common" ]
  }
} else {
  source_set("common") {
    visibility =
        deprecated_visibility_allowed + [ "//brave/ios/browser/api/url" ]
    sources = [
      "//extensions/common/error_utils.cc",
      "//extensions/common/url_pattern.cc",
      "//extensions/common/url_pattern.h",
      "//extensions/common/url_pattern_set.cc",
      "//extensions/common/url_pattern_set.h",
    ]

    inputs = [
      "//extensions/common/constants.h",
      "//extensions/common/error_utils.h",
    ]

    deps = [
      "//base",
      "//net",
      "//url",
    ]

    if (is_ios) {
      inputs += [ "//content/public/common/url_constants.h" ]
    } else {
      deps += [ "//content/public/common" ]
    }
  }
}
