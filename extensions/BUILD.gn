# Copyright (c) 2018 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//extensions/buildflags/buildflags.gni")

if (is_android || is_ios) {
  source_set("common") {
    sources = [
      "//extensions/common/constants.cc",

      # "//extensions/common/constants.h", this triggers gn check errors in upstream
      "//extensions/common/error_utils.cc",
      "//extensions/common/error_utils.h",
      "//extensions/common/url_pattern.cc",
      "//extensions/common/url_pattern.h",
      "//extensions/common/url_pattern_set.cc",
      "//extensions/common/url_pattern_set.h",
    ]

    deps = [
      "//base",
      "//components/url_pattern_index",
      "//device/vr/buildflags",
      "//extensions/buildflags",
      "//net:net",
      "//url",
    ]

    if (is_ios) {
      sources += [ "//content/public/common/url_constants.h" ]
      deps += [
        "//build:chromecast_buildflags",
        "//build:chromeos_buildflags",
      ]
    } else {
      deps += [ "//content/public/common" ]
    }

    public_deps = [
      # Upstream bug: common_constants (above) has a dep on
      # //components/services/app_service/public/mojom, which should have a dep
      # on //ui/gfx/image/mojom, but doesn't, so we have to include it directly.
      "//ui/gfx/image/mojom",
    ]
  }
} else {
  group("common") {
    public_deps = [ "//extensions/common" ]
  }
}
