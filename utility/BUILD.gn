import("//brave/browser/tor/buildflags/buildflags.gni")
import("//brave/components/brave_ads/browser/buildflags/buildflags.gni")
import("//brave/components/brave_rewards/browser/buildflags/buildflags.gni")
import("//build/config/features.gni")
import("//build/config/ui.gni")

source_set("utility") {
  # Remove when https://github.com/brave/brave-browser/issues/10623 is resolved
  check_includes = false
  visibility = [
    "//chrome/utility/*",
    "//brave/utility/*",
    "//brave:child_dependencies",
    "//brave/test:*"
  ]

  sources = [
    "brave_content_utility_client.cc",
    "brave_content_utility_client.h",
  ]

  deps = [
    "//chrome/common",
    "//content/public/common",
  ]

  if (!is_android) {
    sources += [
      "importer/brave_external_process_importer_bridge.cc",
      "importer/brave_external_process_importer_bridge.h",
      "importer/brave_profile_import_impl.cc",
      "importer/brave_profile_import_impl.h",
      "importer/chrome_importer.cc",
      "importer/chrome_importer.h",
    ]

    deps += [
      "//base",
      "//brave/common/importer:interfaces",
      "//chrome/app:chromium_strings",
      "//components/os_crypt",
      "//components/password_manager/core/browser",
      "//components/webdata/common",
    ]
  }

  if (is_mac) {
    sources += [
      "importer/brave_safari_importer.mm",
      "importer/brave_safari_importer.h",
    ]

    deps += [
      "//base",
      "//sql",
      "//url",
    ]
  }

  if (enable_tor) {
    deps += [ "//brave/components/services/tor" ]
  }

  if (brave_ads_enabled) {
    deps += [ "//brave/components/services/bat_ads:lib" ]
  }

  if (brave_rewards_enabled) {
    deps += [ "//brave/components/services/bat_ledger:lib" ]
  }
}
