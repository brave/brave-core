import("//ui/webui/resources/include_polymer.gni")

# Tell the ts_library at ui/webui/resources:library that
# we need to first put brave's files in to the compile directory
brave_ui_webui_resources_ts_extra_deps =
    [ "//brave/ui/webui/resources:preprocess" ]

#
# Build lists of files for various compiles both for brave's preprocessing and to give chromium's
# typescript, rollup, and GRD compiles.
# Any files which need compile go in this sources.gni file.
# Any static files which are served directly without any manipulation (e.g. fonts and some css)
# should just be placed in BUILD.gn in the static_files variable.
#

# TS files holding a non-Polymer element definition and have an equivalent .html
# file.
brave_web_component_files_native_ts = []

# Files that are passed as input to html_to_wrapper().
brave_native_html_files = []
foreach(f, brave_web_component_files_native_ts) {
  brave_native_html_files += [ string_replace(f, ".ts", ".html") ]
}

# HTML files that live alone
brave_native_html_files += []

# Files that are generated by html_to_wrapper().
brave_native_html_wrapper_files = []
foreach(f, brave_native_html_files) {
  brave_native_html_wrapper_files += [ f + ".ts" ]
}

if (include_polymer) {
  # TS files holding a Polymer element definition and have an equivalent .html
  # file.
  brave_web_component_files_polymer_ts = [
    "br_elements/br_toolbar/br_toolbar.ts",
    "br_elements/br_toolbar/br_toolbar_search_field.ts",
  ]

  # Polymer HTML files that are passed as input to html_to_wrapper().
  brave_polymer_html_files = []
  foreach(f, brave_web_component_files_polymer_ts) {
    brave_polymer_html_files += [ string_replace(f, ".ts", ".html") ]
  }

  # Files that are generated by html_to_wrapper().
  brave_polymer_html_wrapper_files = []
  foreach(f, brave_polymer_html_files) {
    brave_polymer_html_wrapper_files += [ f + ".ts" ]
  }
}

# Files that either dont hold a custom element definition or the custom element
# does not have an equivalent .html file.
brave_non_web_component_files_ts = []
if (include_polymer) {
  brave_non_web_component_files_ts += [
    "i18n_behavior.ts",
    "polymer_overriding.ts",
  ]

  # Files that are passed as input to css_to_wrapper().
  brave_css_files = [
    "br_elements/br_shared_style.css",
    "br_elements/br_shared_vars.css",
    "overrides/cr_button_override.css",
    "overrides/cr_toggle_override.css",
    "page_specific/settings/br_settings_shared.css",
  ]

  # Files that are generated by css_to_wrapper().
  brave_css_wrapper_files = []
  foreach(f, brave_css_files) {
    brave_css_wrapper_files += [ f + ".ts" ]
  }
}

brave_local_files =
    brave_web_component_files_native_ts + brave_native_html_wrapper_files +
    brave_non_web_component_files_ts
if (include_polymer) {
  brave_local_files +=
      brave_web_component_files_polymer_ts + brave_polymer_html_wrapper_files +
      brave_css_wrapper_files
}

# Extra GRDPs to give //ui/webui/resources:build_grd
# (which generates //ui/webui/resources/webui_generated_resources.grd)
brave_resources_extra_grdps = []
brave_resources_extra_grdps_deps = []

# GRDP path should match relative path to webui_generated_resources.grd
brave_resources_extra_grdps_path = "$root_gen_dir/ui/webui/resources"
if (!is_ios) {
  brave_resources_extra_grdps += [
    "$brave_resources_extra_grdps_path/brave_fonts_resources.grdp",
    "$brave_resources_extra_grdps_path/brave_static_resources.grdp",
  ]
  brave_resources_extra_grdps_deps += [ "//brave/ui/webui/resources:grdp" ]
}
