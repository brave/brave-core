# Copyright (c) 2025 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//testing/test.gni")

if (is_win || is_mac || is_linux) {
  test("brave_updater_unittests") {
    testonly = true
    sources = [
      "configurator_unittest.cc",
      "ping_configurator_unittest.cc",
      "run_all_unittests.cc",
    ]
    if (is_mac) {
      sources += [ "activity_impl_util_mac_unittest.cc" ]
    }
    deps = [
      "//base",
      "//base/test:test_support",
      "//brave/components/update_client:test_util",
      "//chrome/updater:base",
      "//chrome/updater:constants_test",
      "//components/prefs:test_support",
      "//components/update_client",
      "//testing/gtest",
    ]
  }
}

if (is_win) {
  action("BraveAdmx") {
    script = "brand_google_admx.py"
    inputs = [ script ]
    sources = [ "//chrome/updater/enterprise/win/google/google.admx" ]
    outputs = [ "$root_build_dir/BraveAdmx/brave.admx" ]
    args = [
      rebase_path(sources[0]),
      rebase_path(outputs[0])
    ]
  }
  action("BraveUpdateAdmx") {
    deps = [ "//chrome/updater/enterprise/win/google:GoogleUpdateAdmxGenerate" ]
    sources = [
      "$root_build_dir/GoogleUpdateAdmx/GoogleUpdate.admx",
      "$root_build_dir/GoogleUpdateAdmx/en-US/GoogleUpdate.adml"
    ]
    outputs = [
      "$root_build_dir/BraveUpdateAdmx/GoogleUpdate.admx",
      "$root_build_dir/BraveUpdateAdmx/en-US/GoogleUpdate.adml"
    ]
    script = "brand_group_policy_template.py"
    inputs = [ script ]
    args = sources + outputs
  }
}
