# Copyright (c) 2022 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/browser/shell_integrations/buildflags/buildflags.gni")
import("//brave/components/brave_vpn/common/buildflags/buildflags.gni")
import("//brave/components/request_otr/common/buildflags/buildflags.gni")
import("//brave/components/speedreader/common/buildflags/buildflags.gni")
import("//brave/components/text_recognition/common/buildflags/buildflags.gni")
import("//brave/resources/brave_grit.gni")
import("//build/config/features.gni")
import("//build/config/locales.gni")

action("generate_command_metadata") {
  command_files = [
    "//chrome/app/chrome_command_ids.h",
    "//brave/app/brave_command_ids.h",
  ]
  cc_template = "command_utils.cc.template"

  sources = command_files + [ cc_template ]

  script = "generate_command_metadata.py"

  output_cc = "$target_gen_dir/command_utils.cc"
  outputs = [ output_cc ]

  args = rebase_path(command_files) + [
           "--output_cc",
           rebase_path(output_cc),
           "--template_cc",
           rebase_path(cc_template),
         ]
}

source_set("command_ids") {
  sources = [ "brave_command_ids.h" ]

  deps = [
    "//base",
    "//chrome/app:command_ids",
    "//components/services/screen_ai/buildflags",
  ]

  if (!is_android) {
    sources +=
        [ "command_utils.h" ] + get_target_outputs(":generate_command_metadata")
    deps += [ ":generate_command_metadata" ]
  }
}

brave_grit("brave_generated_resources_grit") {
  defines = [
    "enable_speedreader=$enable_speedreader",
    "enable_request_otr=$enable_request_otr",
    "enable_brave_vpn=$enable_brave_vpn",
    "enable_pin_shortcut=$enable_pin_shortcut",
    "enable_text_recognition=$enable_text_recognition",
  ]
  source = "brave_generated_resources.grd"
  output_dir = "$root_gen_dir/brave"
  outputs = [
    "grit/brave_generated_resources.h",

    # This is needed for the includes and not the localized messages
    "brave_generated_resources.pak",
  ]

  foreach(locale, all_chrome_locales) {
    outputs += [ "brave_generated_resources_$locale.pak" ]
  }
}

source_set("browser_tests") {
  testonly = true

  if (!is_android && !is_ios) {
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    sources = [
      "brave_main_delegate_browsertest.cc",
      "brave_main_delegate_runtime_flags_browsertest.cc",
      "command_utils_browsertest.cc",
    ]

    deps = [
      ":command_ids",
      "//base",
      "//brave/components/commands/common",
      "//chrome/app:command_ids",
      "//chrome/browser",
      "//chrome/browser/companion/core",
      "//chrome/browser/enterprise/connectors/analysis:features",
      "//chrome/browser/ui",
      "//chrome/common:chrome_features",
      "//chrome/common:non_code_constants",
      "//chrome/test",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//components/autofill/core/common",
      "//components/commerce/core:feature_list",
      "//components/component_updater",
      "//components/embedder_support",
      "//components/history_clusters/core",
      "//components/language/core/common",
      "//components/lens",
      "//components/network_time",
      "//components/omnibox/browser",
      "//components/optimization_guide/core",
      "//components/page_image_service",
      "//components/performance_manager",
      "//components/permissions",
      "//components/privacy_sandbox",
      "//components/reading_list/features:flags",
      "//components/safe_browsing/core/common",
      "//components/search",
      "//components/segmentation_platform/public",
      "//components/send_tab_to_self",
      "//components/subresource_filter/core/common",
      "//components/translate/core/common",
      "//extensions/common",
      "//services/device/public/cpp:device_features",
      "//testing/gtest",
    ]
  }
}

source_set("unit_tests") {
  testonly = true

  if (!is_android && !is_ios) {
    sources = [ "command_utils_unittest.cc" ]
    deps = [
      ":command_ids",
      "//base",
      "//base/test:test_support",
      "//brave/components/commands/common",
      "//chrome/app:command_ids",
      "//chrome/browser/ui/views",
      "//testing/gtest",
    ]
  }
}

group("app") {
}
