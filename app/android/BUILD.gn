import("//brave/build/config.gni")
import("//build/config/python.gni")

python2_action("generate_breakpad_symbols") {
  symbols_dir = "$brave_dist_dir/$brave_product_name.breakpad.syms"
  outputs = [ symbols_dir ]

  package_path = rebase_path(brave_android_output)

  script = "//brave/tools/android/generate_breakpad_symbols.py"
  args = [
    "--symbols-dir=" + rebase_path(symbols_dir),
    "--jobs=16",
    "--build-dir=" + rebase_path(root_out_dir),
    "--package-path=" + package_path,
    "--src-root=" + rebase_path("//"),
    "--clear",
    "--verbose",
  ]

  deps = [
    "//brave/build/android:brave",
    "//third_party/breakpad:dump_syms",
  ]
}

group("symbol_dist_resources") {
  public_deps = [
    ":create_symbol_archive",
    ":generate_breakpad_symbols",
  ]
}

group("dist_resources") {
}

group("create_dist_zips") {
}

action("create_symbol_archive") {
  script = "//build/gn_run_binary.py"
  shell_script = "//brave/build/android/pack_symbols.sh"

  deps = [ "//brave/build/android:brave" ]
  archive_name = "$root_out_dir/dist/Default$target_cpu$target_android_base.tar"

  inputs = [
    script,
    shell_script,
    brave_android_output,
  ]

  outputs = [ archive_name ]

  args = [
    rebase_path(shell_script, root_build_dir),
    rebase_path(archive_name, root_out_dir),
  ]
}
