# Copyright (c) 2019 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/build/config.gni")

if (should_generate_symbols) {
  action("generate_breakpad_symbols") {
    symbols_dir = "$brave_dist_dir/$brave_product_name.breakpad.syms"
    outputs = [ symbols_dir ]

    package_path = rebase_path(brave_android_output)

    script = "//brave/tools/android/generate_breakpad_symbols.py"
    args = [
      "--symbols-dir=" + rebase_path(symbols_dir),
      "--jobs=16",
      "--build-dir=" + rebase_path(root_out_dir),
      "--package-path=" + package_path,
      "--src-root=" + rebase_path("//"),
      "--clear",
      "--verbose",
    ]

    deps = [
      "//brave/build/android:brave",
      "//third_party/breakpad:dump_syms",
    ]
  }
} else {
  group("generate_breakpad_symbols") {
  }
}

group("symbol_dist_resources") {
  public_deps = [
    ":create_symbol_archive",
    ":generate_breakpad_symbols",
  ]
}

group("dist_resources") {
}

group("create_dist_zips") {
}

action("create_symbol_archive") {
  script = "//build/gn_run_binary.py"
  shell_script = "//brave/build/android/pack_symbols.sh"

  deps = [ "//brave/build/android:brave" ]

  archive_dir_path = "$root_out_dir/dist"

  # TODO(https://github.com/brave/brave-browser/issues/51200)
  # Cleanup Mono once cr144 reaches stable channel, CI also needs to be updated
  archive_file_name = "Default$target_cpu" + "mono.tar"
  archive_file_path = "$archive_dir_path/$archive_file_name"

  inputs = [
    script,
    shell_script,
    brave_android_output,
  ]

  outputs = [ archive_file_path ]
  args = [
    rebase_path(shell_script, root_build_dir),
    rebase_path(archive_dir_path, root_out_dir),
    rebase_path(archive_file_path, root_out_dir),
  ]
}
