# Copyright (c) 2018 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/components/brave_wayback_machine/buildflags/buildflags.gni")

source_set("infobars") {
  # Remove when https://github.com/brave/brave-browser/issues/10652 is resolved
  check_includes = false

  sources = [
    "brave_confirm_infobar.cc",
    "brave_confirm_infobar.h",
    "brave_global_infobar_manager.cc",
    "brave_global_infobar_manager.h",
    "brave_infobar_container_view.cc",
    "brave_infobar_container_view.h",
    "custom_styled_label.cc",
    "custom_styled_label.h",
    "web_discovery_infobar_content_view.cc",
    "web_discovery_infobar_content_view.h",
    "web_discovery_infobar_view.cc",
    "web_discovery_infobar_view.h",
  ]

  deps = [
    "//base",
    "//brave/app:brave_generated_resources_grit",
    "//brave/app/theme:brave_theme_resources",
    "//brave/app/vector_icons",
    "//brave/components/constants",
    "//brave/components/infobars/core",
    "//components/infobars/core",
    "//content/public/browser",
    "//third_party/abseil-cpp:absl",
    "//ui/base",
    "//ui/color",
    "//ui/gfx",
    "//ui/native_theme",
    "//ui/views",
  ]
}

if (enable_brave_wayback_machine) {
  source_set("brave_wayback_machine") {
    # Remove when https://github.com/brave/brave-browser/issues/10652 is resolved
    check_includes = false

    sources = [
      "brave_wayback_machine_infobar_button_container.cc",
      "brave_wayback_machine_infobar_button_container.h",
      "brave_wayback_machine_infobar_contents_view.cc",
      "brave_wayback_machine_infobar_contents_view.h",
      "brave_wayback_machine_infobar_throbber.cc",
      "brave_wayback_machine_infobar_throbber.h",
      "brave_wayback_machine_infobar_view.cc",
      "brave_wayback_machine_infobar_view.h",
    ]

    deps = [
      "//brave/app:brave_generated_resources_grit",
      "//brave/app/theme:brave_theme_resources",
      "//brave/app/vector_icons",
      "//brave/browser/themes",
      "//brave/components/brave_wayback_machine",
      "//chrome/browser:theme_properties",
      "//components/infobars/content",
      "//components/infobars/core",
      "//components/prefs:prefs",
      "//components/resources",
      "//components/user_prefs",
      "//content/public/browser",
      "//ui/base",
      "//ui/gfx",
      "//ui/native_theme",
      "//ui/views",
      "//url",
    ]

    # Below dep list are indirectly used by brave_wayback_machine target.
    # Only added to fix intermittent build failure.
    # chrome/common/buildflags.h is included indirectly from
    # brave_wayback_machine_infobar_contents_view.cc.
    # However, this buildflags.h sometimes not generated when
    # brave_wayback_machine_infobar_contents_view.cc is compiled.
    deps += [ "//chrome/common:buildflags" ]
  }
}

source_set("brave_sync_account_deleted_infobar") {
  # Remove when https://github.com/brave/brave-browser/issues/10652 is resolved
  check_includes = false
  sources = [
    "brave_sync_account_deleted_infobar.cc",
    "brave_sync_account_deleted_infobar.h",
  ]

  deps = [
    "//base",

    # "//chrome/browser/ui" - causes dependency cycle gn error
    "//ui/base",
    "//ui/views",
  ]
}
