import("//build/config/features.gni")
import("//chrome/common/features.gni")

source_set("ui") {
  sources = [
    "brave_browser_command_controller.cc",
    "brave_browser_command_controller.h",
    "brave_browser_content_setting_bubble_model_delegate.cc",
    "brave_browser_content_setting_bubble_model_delegate.h",
    "brave_pages.cc",
    "brave_pages.h",
    "content_settings/brave_content_setting_bubble_model.cc",
    "content_settings/brave_content_setting_bubble_model.h",
    "content_settings/brave_content_setting_image_models.cc",
    "content_settings/brave_content_setting_image_models.h",
    "content_settings/brave_widevine_blocked_image_model.cc",
    "content_settings/brave_widevine_blocked_image_model.h",
    "content_settings/brave_widevine_content_setting_bubble_model.cc",
    "content_settings/brave_widevine_content_setting_bubble_model.h",
    "toolbar/brave_app_menu_model.cc",
    "toolbar/brave_app_menu_model.h",
    "views/importer/brave_import_lock_dialog_view.cc",
    "views/importer/brave_import_lock_dialog_view.h",
    "webui/basic_ui.cc",
    "webui/basic_ui.h",
    "webui/brave_web_ui_controller_factory.cc",
    "webui/brave_web_ui_controller_factory.h",
    "webui/brave_welcome_ui.cc",
    "webui/brave_welcome_ui.h",
    "webui/new_tab_html_source.cc",
    "webui/new_tab_html_source.h",
  ]

  if (is_mac && !mac_views_browser) {
    sources += [
      "cocoa/importer/brave_import_lock_dialog_cocoa.mm",
    ]
  }
  public_deps = [
    "//content/public/browser",
  ]

  deps = [
    "//brave/app:command_ids",
    "//brave/app/theme:brave_unscaled_resources",
    "//brave/app/theme:brave_theme_resources",
    "//chrome/app:command_ids",
  ]

  if (is_win && is_official_build) {
    sources += [
      "//chrome/browser/ui/webui/help/version_updater_win.cc",
      "//chrome/browser/ui/webui/help/version_updater_win.h",
    ]
    deps += [ "//google_update" ]
  }

  if (is_linux) {
    sources += [
      "views/brave_browser_main_extra_parts_views_linux.cc",
      "views/brave_browser_main_extra_parts_views_linux.h",
      "views/brave_views_delegate_linux.cc",
      "views/brave_views_delegate_linux.h",
    ]
  }
}
