import("//brave/build/config.gni")
import("//build/config/features.gni")
import("//brave/components/brave_rewards/browser/buildflags/buildflags.gni")
import("//brave/components/brave_wallet/browser/buildflags/buildflags.gni")
import("//chrome/common/features.gni")

source_set("ui") {
  sources = [
    "webui/basic_ui.cc",
    "webui/basic_ui.h",
    "webui/brave_adblock_ui.cc",
    "webui/brave_adblock_ui.h",
    "webui/brave_new_tab_message_handler.cc",
    "webui/brave_new_tab_message_handler.h",
    "webui/brave_new_tab_ui.cc",
    "webui/brave_new_tab_ui.h",
    "webui/brave_web_ui_controller_factory.cc",
    "webui/brave_web_ui_controller_factory.h",
    "webui/brave_webui_source.cc",
    "webui/brave_webui_source.h",
    "webui/sync/sync_ui.cc",
    "webui/sync/sync_ui.h",
  ]

  if (!is_android) {
    sources += [
      "brave_browser_command_controller.cc",
      "brave_browser_command_controller.h",
      "brave_browser_content_setting_bubble_model_delegate.cc",
      "brave_browser_content_setting_bubble_model_delegate.h",
      "brave_layout_constants.cc",
      "brave_layout_constants.h",
      "brave_pages.cc",
      "brave_pages.h",
      "browser_commands.cc",
      "browser_commands.h",
      "content_settings/brave_autoplay_blocked_image_model.cc",
      "content_settings/brave_autoplay_blocked_image_model.h",
      "content_settings/brave_autoplay_content_setting_bubble_model.cc",
      "content_settings/brave_autoplay_content_setting_bubble_model.h",
      "content_settings/brave_content_setting_bubble_model.cc",
      "content_settings/brave_content_setting_bubble_model.h",
      "content_settings/brave_content_setting_image_models.cc",
      "content_settings/brave_content_setting_image_models.h",
      "omnibox/brave_omnibox_client.cc",
      "omnibox/brave_omnibox_client.h",
      "toolbar/brave_app_menu_model.cc",
      "toolbar/brave_app_menu_model.h",
      "webui/brave_settings_ui.cc",
      "webui/brave_settings_ui.h",
      "webui/navigation_bar_data_provider.cc",
      "webui/navigation_bar_data_provider.h",
      "webui/settings/brave_privacy_handler.cc",
      "webui/settings/brave_privacy_handler.h",
      "webui/settings/default_brave_shields_handler.cc",
      "webui/settings/default_brave_shields_handler.h",
    ]

    if (enable_sparkle) {
      sources += [
        "webui/settings/brave_relaunch_handler_mac.mm",
        "webui/settings/brave_relaunch_handler_mac.h",
      ]
    }
  }

  if (toolkit_views) {
    sources += [
      "views/brave_layout_provider.cc",
      "views/brave_layout_provider.h",
      "views/download/brave_download_item_view.cc",
      "views/download/brave_download_item_view.h",
      "views/frame/brave_browser_view.cc",
      "views/frame/brave_browser_view.h",
      "views/importer/brave_import_lock_dialog_view.cc",
      "views/importer/brave_import_lock_dialog_view.h",
      "views/rounded_separator.cc",
      "views/rounded_separator.h",
      "views/toolbar/bookmark_button.cc",
      "views/toolbar/bookmark_button.h",
    ]

    if (enable_sparkle) {
      sources += [
        "views/update_recommended_message_box_mac.mm",
        "views/update_recommended_message_box_mac.h",
      ]
    }
  }

  if (is_win || is_mac || is_desktop_linux) {
    sources += [
      "views/profiles/brave_incognito_menu_view.cc",
      "views/profiles/brave_incognito_menu_view.h",
      "views/profiles/brave_profile_chooser_view.cc",
      "views/profiles/brave_profile_chooser_view.h",
      "views/profiles/brave_profile_menu_view_helper.cc",
      "views/profiles/brave_profile_menu_view_helper.h",
      "webui/brave_welcome_ui.cc",
      "webui/brave_welcome_ui.h",
    ]
  }

  if (is_linux) {
    sources += [
      "views/brave_browser_main_extra_parts_views_linux.cc",
      "views/brave_browser_main_extra_parts_views_linux.h",
      "views/brave_views_delegate_linux.cc",
      "views/brave_views_delegate_linux.h",
    ]
  }

  deps = [
    "//base",
    "//brave/app:command_ids",
    "//brave/app/theme:brave_unscaled_resources",
    "//brave/app/theme:brave_theme_resources",
    "//brave/app/vector_icons:vector_icons",
    "//brave/common",
    "//brave/components/brave_adblock_ui:generated_resources",
    "//brave/components/brave_new_tab_ui:generated_resources",
    "//brave/components/brave_rewards/browser",
    "//brave/components/brave_rewards/resources",
    "//brave/components/brave_shields/browser",
    "//brave/components/brave_sync",
    "//brave/components/brave_sync:static_resources",
    "//brave/components/brave_sync:generated_resources",
    "//brave/components/brave_welcome_ui:generated_resources",
    "//brave/browser:sparkle_buildflags",
    "//brave/browser/devtools",
    "//brave/browser/tor",
    "//brave/browser/resources/settings:resources",
    "//chrome/app:command_ids",
    "//chrome/common",
    "//components/prefs",
    "//content/public/browser",
    "//content/public/common",
    "//skia",
    "//ui/accessibility",
    "//ui/base",
    "//ui/resources",
    "//ui/gfx",
  ]

  if (!is_android) {
    deps += [
      "//brave/app:brave_generated_resources_grit",
    ]
  }

  if (enable_extensions) {
    sources += [
      "brave_actions/brave_action_icon_with_badge_image_source.cc",
      "brave_actions/brave_action_icon_with_badge_image_source.h",
      "brave_actions/brave_action_view_controller.cc",
      "brave_actions/brave_action_view_controller.h",
      "toolbar/brave_location_bar_model_delegate.cc",
      "toolbar/brave_location_bar_model_delegate.h",
      "toolbar/brave_toolbar_actions_model.cc",
      "toolbar/brave_toolbar_actions_model.h",
      "views/brave_actions/brave_action_view.cc",
      "views/brave_actions/brave_action_view.h",
      "views/brave_actions/brave_actions_container.cc",
      "views/brave_actions/brave_actions_container.h",
      "views/location_bar/brave_location_bar_view.cc",
      "views/location_bar/brave_location_bar_view.h",
      "views/profiles/brave_avatar_toolbar_button.cc",
      "views/profiles/brave_avatar_toolbar_button.h",
      "views/tabs/brave_alert_indicator.cc",
      "views/tabs/brave_alert_indicator.h",
      "views/tabs/brave_new_tab_button.cc",
      "views/tabs/brave_new_tab_button.h",
      "views/toolbar/brave_toolbar_view.cc",
      "views/toolbar/brave_toolbar_view.h",
      "webui/settings/brave_default_extensions_handler.cc",
      "webui/settings/brave_default_extensions_handler.h",
    ]

    deps += [
      "//brave/browser/resources/extensions:resources",
      "//brave/components/brave_extension:static_resources",
      "//brave/components/brave_extension:generated_resources",
      "//chrome/browser/extensions",
      "//extensions/browser",
    ]
  }

  if (is_win && is_official_build) {
    sources += [
      "//chrome/browser/ui/webui/help/version_updater_win.cc",
      "//chrome/browser/ui/webui/help/version_updater_win.h",
    ]
    deps += [ "//google_update" ]
  }

  if (brave_rewards_enabled) {
    sources += [
      "webui/brave_tip_ui.cc",
      "webui/brave_tip_ui.h",
      "webui/brave_rewards_source.cc",
      "webui/brave_rewards_source.h",
      "webui/brave_rewards_internals_ui.cc",
      "webui/brave_rewards_internals_ui.h",
      "webui/brave_rewards_ui.cc",
      "webui/brave_rewards_ui.h",
    ]

    deps += [
      "//brave/components/brave_rewards/resources:tip_generated_resources",
      "//brave/components/brave_rewards/resources:ui_generated_resources",
    ]
  }

  if (brave_wallet_enabled) {
    sources += [
      "webui/brave_wallet_ui.cc",
      "webui/brave_wallet_ui.h",
    ]
    deps += [
      "//brave/components/brave_wallet_ui:generated_resources",
    ]
  }
}
