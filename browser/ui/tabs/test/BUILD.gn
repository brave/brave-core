# Copyright (c) 2020 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/. */

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  sources = [ "brave_tab_strip_model_browsertest.cc" ]

  deps = [
    "//brave/components/constants",
    "//chrome/app:command_ids",
    "//chrome/browser",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//chrome/browser/ui:browser_navigator_params_headers",
    "//components/prefs",
    "//content/test:test_support",
  ]

  if (!is_android) {
    sources += [
      "shared_pinned_tab_service_browsertest.cc",
      "shared_pinned_tab_service_browsertest.h",
    ]
    deps += [ "//chrome/test:test_support_ui" ]
  }

  if (is_mac) {
    sources += [ "shared_pinned_tab_service_browsertest_mac.mm" ]
  }
}

source_set("unit_tests") {
  testonly = true

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  if (!is_android) {
    sources = [
      "split_view_browser_data_unittest.cc",
      "split_view_tab_strip_model_adapter_unittest.cc",
    ]

    deps = [
      ":test_support",
      "//chrome/browser",
      "//chrome/browser/ui",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//components/tab_groups",
    ]
  }
}

# In order to resolve circular deps to the test_tab_strip_model_delegate,
# declares source set here instead of adding these files to //brave/test/BUILD.gn
source_set("test_support") {
  if (!is_android) {
    sources = [
      "//chrome/browser/ui/tabs/test_tab_strip_model_delegate.cc",
      "//chrome/browser/ui/tabs/test_tab_strip_model_delegate.h",
    ]

    deps = [
      "//base",
      "//chrome/browser/extensions",
      "//chrome/browser/ui",
      "//components/tab_groups",
    ]
  }
}
