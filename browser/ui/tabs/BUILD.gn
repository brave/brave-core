# Copyright (c) 2025 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

assert(is_win || is_mac || is_linux || is_chromeos || is_android)

# Note: Target names in this file should match those upstream where possible.

source_set("tabs_public") {
  sources = [ "public/brave_tab_features.h" ]

  public_deps = [
    "//brave/components/psst/buildflags",
    "//chrome/browser/ui/tabs:tabs_public",
  ]

  if (!is_android) {
    sources += [ "public/constants.h" ]
  }
}

if (!is_android) {
  source_set("impl") {
    visibility = [ "//chrome/browser" ]

    sources = [ "brave_tab_features.cc" ]

    deps = [
      ":tabs_public",
      "//brave/browser/ai_chat",
      "//brave/browser/psst",
      "//brave/browser/ui/side_panel",
      "//brave/components/psst/browser/content",
      "//chrome/browser/profiles",
      "//chrome/browser/ui/tabs:impl",
    ]
  }

  # This source set is counterpart to the upstream `tab_strip` source set.
  source_set("tab_strip") {
    sources = [
      "brave_tab_strip_model.cc",
      "brave_tab_strip_model.h",
    ]

    deps = [
      "//brave/browser/ui:brave_tab_features",
      "//brave/components/tabs",
      "//chrome/browser/ui/tabs:tab_strip",
    ]

    public_deps = [ "//chrome/browser:browser_public_dependencies" ]
  }
}

if (!is_android) {
  source_set("tab_model") {
    sources = [
      "brave_split_tab_menu_model.cc",
      "brave_split_tab_menu_model.h",
    ]

    deps = [
      "//brave/components/resources:strings_grit",
      "//brave/components/vector_icons",
      "//chrome/browser/ui/tabs",
      "//chrome/browser/ui/tabs:tab_model",
      "//ui/base",
    ]
  }
}
