# Copyright (c) 2025 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/components/ai_chat/core/common/buildflags/buildflags.gni")
import("//brave/components/brave_vpn/common/buildflags/buildflags.gni")
import("//brave/components/brave_wallet/common/buildflags/buildflags.gni")

source_set("customize_chrome") {
  sources = [
    "customize_toolbar/brave_action.h",
    "customize_toolbar/list_action_modifiers.cc",
    "customize_toolbar/list_action_modifiers.h",
  ]

  deps = [
    "//base",
    "//brave/browser/brave_rewards:util",
    "//brave/components/ai_chat/core/common/buildflags",
    "//brave/components/brave_news/common",
    "//brave/components/brave_rewards/core",
    "//brave/components/brave_vpn/common/buildflags",
    "//brave/components/brave_wallet/common/buildflags",
    "//brave/components/constants",
    "//brave/components/resources:strings_grit",
    "//brave/components/vector_icons",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui/webui/side_panel/customize_chrome:mojo_bindings",
    "//chrome/browser/ui/webui/util",
    "//components/prefs",
    "//components/user_prefs",
    "//content/public/browser",
  ]

  if (enable_brave_wallet) {
    deps += [
      "//brave/components/brave_wallet/browser:pref_names",
      "//brave/components/brave_wallet/common",
    ]
  }

  if (enable_ai_chat) {
    deps += [
      "//brave/components/ai_chat/core/browser",
      "//brave/components/ai_chat/core/common",
    ]
  }

  if (enable_brave_vpn_panel) {
    deps += [
      "//brave/browser/brave_vpn",
      "//brave/components/brave_vpn/common",
    ]
  }
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  sources = [ "customize_chrome_browsertest.cc" ]

  deps = [
    "//chrome/browser/ui/browser_window",
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui",
  ]
}

source_set("unit_tests") {
  testonly = true

  sources = [
    "customize_toolbar/customize_toolbar_handler_unittest.cc",
    "customize_toolbar/list_action_modifiers_unittest.cc",
  ]

  deps = [
    ":customize_chrome",
    "//base",
    "//brave/browser/brave_rewards:util",
    "//brave/components/ai_chat/core/common/buildflags",
    "//brave/components/brave_news/common",
    "//brave/components/brave_rewards/core",
    "//brave/components/brave_vpn/common/buildflags",
    "//brave/components/brave_wallet/common/buildflags",
    "//brave/components/resources:strings_grit",
    "//brave/components/vector_icons",
    "//chrome/browser/ui/webui/util",
    "//chrome/test:test_support",
    "//components/prefs",
    "//components/sync_preferences:test_support",
    "//components/user_prefs",
    "//content/public/browser",
    "//content/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/base",
  ]

  if (enable_brave_wallet) {
    deps += [ "//brave/components/brave_wallet/common" ]
  }

  if (enable_ai_chat) {
    deps += [
      "//brave/components/ai_chat/core/browser",
      "//brave/components/ai_chat/core/common",
    ]
  }

  if (enable_brave_vpn_panel) {
    deps += [
      "//brave/browser/brave_vpn",
      "//brave/components/brave_vpn/common",
    ]
  }
}
