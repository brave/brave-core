# Copyright (c) 2024 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//printing/buildflags/buildflags.gni")

if (!is_android) {
  source_set("ai_chat") {
    sources = [
      "utils.cc",
      "utils.h",
    ]

    deps = [
      "//chrome/browser/ui/browser_window",
      "//chrome/browser/ui/tabs",
      "//chrome/browser/ui/views/side_panel",
    ]
  }
}

if (enable_print_preview) {
  source_set("print_preview_extractor") {
    sources = [ "print_preview_extractor.h" ]
    public_deps = [
      "//base",
      "//brave/components/ai_chat/content/browser",
      "//chrome/browser/profiles",
      "//content/public/browser",
      "//printing/buildflags",
    ]
  }

  source_set("print_preview_extractor_impl") {
    visibility = [
      ":unit_tests",
      "//chrome/browser",
    ]
    sources = [
      "print_preview_extractor.cc",
      "print_preview_extractor_internal.cc",
      "print_preview_extractor_internal.h",
    ]
    deps = [
      ":print_preview_extractor",
      "//brave/components/ai_chat/core/browser",
      "//brave/components/text_recognition/common/buildflags",
      "//chrome/browser/pdf",
      "//chrome/browser/pdf:pdf_pref_names",
      "//chrome/browser/ui",
      "//chrome/common:constants",
      "//chrome/services/printing/public/mojom",
      "//components/pdf/browser",
      "//components/prefs",
      "//components/printing/browser",
      "//components/printing/common:mojo_interfaces",
      "//components/services/print_compositor/public/mojom",
      "//pdf:buildflags",
      "//pdf:features",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [ "ai_chat_tab_helper_unittest.cc" ]
  deps = []

  if (enable_print_preview) {
    sources += [ "print_preview_extractor_unittest.cc" ]
    deps += [
      ":print_preview_extractor",
      ":print_preview_extractor_impl",
      "//brave/components/text_recognition/common/buildflags",
      "//chrome/services/printing/public/mojom",
      "//components/printing/browser",
    ]
  }

  deps += [
    "//base/test:test_support",
    "//brave/components/ai_chat/content/browser",
    "//brave/components/ai_chat/core/browser",
    "//brave/components/ai_chat/core/common",
    "//brave/components/ai_chat/core/common/mojom",
    "//chrome/test:test_support",
    "//components/favicon/content",
    "//components/favicon/core/test:test_support",
    "//content/test:test_support",
    "//testing/gtest:gtest",
  ]
}
