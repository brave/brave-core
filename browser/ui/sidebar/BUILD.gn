# Copyright (c) 2021 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/components/ai_chat/core/common/buildflags/buildflags.gni")
import("//build/config/ui.gni")
import("//extensions/buildflags/buildflags.gni")

assert(toolkit_views)

source_set("sidebar") {
  # Set due to //chrome/browser and //chrome/browser/ui circular dependencies.
  # TODO(simonhong): Create issue for removing this. Remove when XXXXXXX is resolved
  check_includes = false
  sources = [
    "sidebar.h",
    "sidebar_controller.cc",
    "sidebar_controller.h",
    "sidebar_model.cc",
    "sidebar_model.h",
    "sidebar_service_factory.cc",
    "sidebar_service_factory.h",
    "sidebar_utils.cc",
    "sidebar_utils.h",
  ]

  deps = [
    "//base",
    "//brave/components/sidebar",
    "//chrome/browser/profiles",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//content/public/browser",
    "//content/public/common",
    "//extensions/buildflags",
    "//skia",
    "//third_party/abseil-cpp:absl",
    "//url",
  ]

  if (enable_extensions) {
    deps += [
      "//chrome/browser/extensions",
      "//extensions/browser",
      "//extensions/common",
    ]
  }

  # included from chrome/browser/ui/webui/new_tab_page/new_tab_page_ui.h
  # see check_includes comment above
  deps += [
    "//chrome/browser/cart:mojo_bindings",
    "//ui/webui/resources/js/browser_command:mojo_bindings",
  ]

  if (!is_android) {
    deps += [
      "//chrome/browser/new_tab_page/modules/drive:mojo_bindings",
      "//chrome/browser/new_tab_page/modules/recipes:mojo_bindings",
      "//chrome/browser/ui/webui/new_tab_page:mojo_bindings",
      "//components/omnibox/browser:mojo_bindings",
    ]
  }
}

source_set("browser_tests") {
  testonly = true
  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  sources = [
    "//brave/browser/ui/sidebar/sidebar_browsertest.cc",
    "//brave/browser/ui/views/sidebar/sidebar_container_view_browsertest.cc",
  ]

  deps = [
    "//base",
    "//brave/app:command_ids",
    "//brave/browser/ui/sidebar",
    "//brave/browser/ui/tabs",
    "//brave/components/ai_chat/core/common/buildflags",
    "//brave/components/playlist/common",
    "//brave/components/sidebar",
    "//chrome/browser",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//chrome/common:constants",
    "//chrome/test:test_support_ui",
    "//components/prefs",
    "//content/test:test_support",
    "//ui/events",
    "//ui/gfx",
  ]

  if (enable_ai_chat) {
    deps += [ "//brave/components/ai_chat/core/common" ]
  }
}

source_set("unit_tests") {
  testonly = true

  sources = [ "sidebar_unittest.cc" ]

  deps = [
    "//base",
    "//brave/browser/ui/sidebar",
    "//brave/common",
    "//brave/components/playlist/common",
    "//brave/components/sidebar",
    "//chrome/browser/ui",
    "//chrome/test:test_support",
    "//components/prefs",
    "//components/sync_preferences:test_support",
    "//content/test:test_support",
    "//testing/gmock",
    "//testing/gtest",
  ]
}
