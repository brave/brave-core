# Copyright (c) 2021 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//brave/components/sidebar/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("sidebar") {
  # Set due to //chrome/browser and //chrome/browser/ui circular dependencies.
  # TODO(simonhong): Create issue for removing this. Remove when XXXXXXX is resolved
  check_includes = false
  sources = [
    "sidebar.h",
    "sidebar_controller.cc",
    "sidebar_controller.h",
    "sidebar_model.cc",
    "sidebar_model.h",
    "sidebar_model_data.cc",
    "sidebar_model_data.h",
    "sidebar_service_factory.cc",
    "sidebar_service_factory.h",
    "sidebar_utils.cc",
    "sidebar_utils.h",
    "sidebar_web_contents_delegate.cc",
    "sidebar_web_contents_delegate.h",
  ]

  deps = [
    "//base",
    "//brave/components/sidebar",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//content/public/browser",
    "//extensions/buildflags",
    "//skia",
    "//url",
  ]

  if (enable_extensions) {
    deps += [
      "//chrome/browser/extensions",
      "//extensions/browser",
      "//extensions/common",
    ]
  }

  # included from chrome/browser/ui/webui/new_tab_page/new_tab_page_ui.h
  # see check_includes comment above
  deps += [
    "//chrome/browser/cart:mojo_bindings",
    "//chrome/browser/new_tab_page/modules/drive:mojo_bindings",
    "//chrome/browser/new_tab_page/modules/task_module:mojo_bindings",
    "//chrome/browser/promo_browser_command:mojo_bindings",
    "//chrome/browser/ui/webui/new_tab_page:mojo_bindings",
    "//chrome/browser/ui/webui/realbox:mojo_bindings",
  ]
}

source_set("browser_tests") {
  if (enable_sidebar) {
    testonly = true
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    sources = [ "sidebar_browsertest.cc" ]

    deps = [
      "//base",
      "//brave/browser/ui",
      "//brave/browser/ui/sidebar",
      "//brave/components/sidebar",
      "//chrome/browser",
      "//chrome/browser/ui",
      "//chrome/test:test_support_ui",
      "//content/test:test_support",
    ]
  }
}

source_set("unit_tests") {
  if (enable_sidebar) {
    testonly = true

    sources = [ "sidebar_unittest.cc" ]

    deps = [
      "//base",
      "//brave/browser/ui",
      "//brave/browser/ui/sidebar",
      "//brave/components/sidebar",
      "//chrome/test:test_support",
      "//components/prefs",
      "//components/sync_preferences:test_support",
      "//content/test:test_support",
      "//testing/gtest",
    ]
  }
}
