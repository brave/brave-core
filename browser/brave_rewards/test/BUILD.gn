# Copyright (c) 2022 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/. */

import("//testing/test.gni")

source_set("browser_tests") {
  testonly = true

  sources = [ "creator_detection_browsertest.cc" ]

  deps = [
    "//base/test:test_support",
    "//brave/browser/brave_rewards",
    "//brave/components/brave_rewards/core",
    "//brave/components/brave_rewards/core:features",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui",
    "//chrome/test:test_support",
    "//chrome/test:test_support_ui",
    "//components/prefs",
    "//content/test:test_support",
  ]

  defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

  # TODO(https://github.com/brave/brave-browser/issues/41833): Update these
  # tests to run on Android as well.
  if (!is_android) {
    sources += [
      "rewards_flag_browsertest.cc",
      "rewards_notification_browsertest.cc",
      "rewards_ofac_browsertest.cc",
      "rewards_p3a_browsertest.cc",
      "rewards_page_browsertest.cc",
      "rewards_policy_browsertest.cc",
      "util/rewards_browsertest_context_helper.cc",
      "util/rewards_browsertest_context_helper.h",
      "util/rewards_browsertest_context_util.cc",
      "util/rewards_browsertest_context_util.h",
      "util/rewards_browsertest_contribution.cc",
      "util/rewards_browsertest_contribution.h",
      "util/rewards_browsertest_network_util.cc",
      "util/rewards_browsertest_network_util.h",
      "util/rewards_browsertest_response.cc",
      "util/rewards_browsertest_response.h",
      "util/rewards_browsertest_util.cc",
      "util/rewards_browsertest_util.h",
    ]

    deps += [
      "//brave/app:command_ids",
      "//brave/browser",
      "//brave/browser/brave_ads",
      "//brave/browser/brave_rewards:util",
      "//brave/components/brave_rewards/content",
      "//brave/components/brave_rewards/core/buildflags",
      "//brave/components/brave_rewards/core/engine",
      "//brave/components/brave_rewards/core/engine:publishers_proto",
      "//brave/components/brave_rewards/core/mojom:engine",
      "//brave/components/l10n/common:test_support",
      "//brave/components/ntp_background_images/common",
      "//chrome/browser/ui/browser_window",
      "//components/policy/core/browser",
      "//components/policy/core/common:test_support",
      "//components/user_prefs",
    ]
  }
}
