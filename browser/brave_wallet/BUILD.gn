import("//brave/components/brave_wallet/common/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

assert(brave_wallet_enabled)

source_set("brave_wallet") {
  check_includes = false

  sources = [
    "asset_ratio_controller_factory.cc",
    "asset_ratio_controller_factory.h",
    "brave_wallet_context_utils.cc",
    "brave_wallet_context_utils.h",
    "eth_tx_controller_factory.cc",
    "eth_tx_controller_factory.h",
    "keyring_controller_factory.cc",
    "keyring_controller_factory.h",
    "rpc_controller_factory.cc",
    "rpc_controller_factory.h",
    "swap_controller_factory.cc",
    "swap_controller_factory.h",
  ]
  deps = [
    "//brave/components/brave_wallet/browser",
    "//brave/components/brave_wallet/common",
    "//brave/components/brave_wallet/common:mojom",
    "//chrome/browser/profiles:profiles",
    "//components/keyed_service/content:content",
    "//components/user_prefs",
    "//content/public/browser",
    "//extensions/buildflags",
    "//services/network/public/cpp",
  ]
  if (is_android) {
    sources += [
      "./android/asset_ratio_controller_factory_android.cc",
      "./android/erc_token_registry_factory_android.cc",
      "./android/keyring_controller_factory_android.cc",
    ]
    deps += [ "//brave/build/android:jni_headers" ]
  }
  if (enable_extensions) {
    deps += [
      "//brave/browser/extensions",
      "//chrome/browser/extensions:extensions",
      "//extensions/browser",
    ]
  }
}

source_set("brave_wallet_delegate") {
  sources = []
  deps = [
    "//brave/components/brave_wallet/browser",
    "//content/public/browser",
  ]
  if (is_android) {
    sources += [
      "brave_wallet_provider_delegate_impl_android.cc",
      "brave_wallet_provider_delegate_impl_android.h",
    ]
  } else {
    sources += [
      "brave_wallet_provider_delegate_impl.cc",
      "brave_wallet_provider_delegate_impl.h",
    ]
    deps += [
      "//brave/browser/brave_wallet",
      "//brave/browser/brave_wallet:tab_helper",
      "//brave/components/brave_wallet/common:mojom",
      "//components/content_settings/core/common",
      "//components/permissions",
      "//mojo/public/cpp/bindings",
    ]
  }
}

source_set("tab_helper") {
  sources = [
    "brave_wallet_tab_helper.cc",
    "brave_wallet_tab_helper.h",
  ]

  deps = [
    "//brave/common",
    "//brave/components/brave_wallet/browser:browser",
    "//brave/components/brave_wallet/browser:ethereum_permission_utils",
    "//brave/components/resources:strings_grit",
    "//components/permissions",
    "//components/sessions",
    "//content/public/browser",
    "//ui/base:base",
  ]

  if (!is_android && !is_ios) {
    deps += [ "//brave/browser/ui/brave_wallet" ]
  }
}
