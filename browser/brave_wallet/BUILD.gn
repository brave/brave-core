# Copyright (c) 2019 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/browser/ethereum_remote_client/buildflags/buildflags.gni")
import("//brave/components/ipfs/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")
import("//testing/test.gni")

source_set("brave_wallet") {
  sources = [
    "asset_ratio_service_factory.cc",
    "asset_ratio_service_factory.h",
    "bitcoin_wallet_service_factory.cc",
    "bitcoin_wallet_service_factory.h",
    "blockchain_images_source.cc",
    "blockchain_images_source.h",
    "brave_wallet_context_utils.cc",
    "brave_wallet_context_utils.h",
    "brave_wallet_ipfs_service_factory.cc",
    "brave_wallet_ipfs_service_factory.h",
    "brave_wallet_service_factory.cc",
    "brave_wallet_service_factory.h",
    "json_rpc_service_factory.cc",
    "json_rpc_service_factory.h",
    "keyring_service_factory.cc",
    "keyring_service_factory.h",
    "simulation_service_factory.cc",
    "simulation_service_factory.h",
    "swap_service_factory.cc",
    "swap_service_factory.h",
    "tx_service_factory.cc",
    "tx_service_factory.h",
  ]
  if (enable_ipfs_local_node) {
    sources += [
      "brave_wallet_auto_pin_service_factory.cc",
      "brave_wallet_auto_pin_service_factory.h",
      "brave_wallet_pin_service_factory.cc",
      "brave_wallet_pin_service_factory.h",
    ]
  }
  if (!is_android) {
    sources += [ "wallet_notification_helper.h" ]
  }
  deps = [
    "//brave/browser/profiles:util",
    "//brave/components/brave_wallet/browser",
    "//brave/components/brave_wallet/browser:constants",
    "//brave/components/brave_wallet/browser:utils",
    "//brave/components/brave_wallet/common",
    "//brave/components/brave_wallet/common:mojom",
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles:profiles",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//components/user_prefs",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
    "//services/network/public/cpp",
    "//third_party/abseil-cpp:absl",
  ]
  if (enable_extensions) {
    deps += [
      "//brave/browser/extensions",
      "//chrome/browser/extensions:extensions",
      "//extensions/browser",
    ]
  }
}

source_set("brave_wallet_delegate") {
  sources = [
    "brave_wallet_provider_delegate_impl.cc",
    "brave_wallet_provider_delegate_impl.h",
    "brave_wallet_service_delegate_helper.cc",
  ]
  deps = [
    "//brave/browser/brave_wallet",
    "//brave/browser/brave_wallet:tab_helper",
    "//brave/components/brave_wallet/browser",
    "//brave/components/brave_wallet/browser:constants",
    "//brave/components/brave_wallet/browser:permission_utils",
    "//brave/components/brave_wallet/browser:utils",
    "//brave/components/brave_wallet/common:mojom",
    "//brave/components/resources:strings_grit",
    "//chrome/browser/profiles:profile",
    "//components/content_settings/core/common",
    "//components/permissions",
    "//components/permissions:permissions",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
  ]
  if (is_android) {
    sources += [
      "brave_wallet_provider_delegate_impl_helper_android.cc",
      "brave_wallet_service_delegate_impl_android.cc",
      "brave_wallet_service_delegate_impl_android.h",
    ]
    deps += [
      "//brave/build/android:jni_headers",
      "//chrome/browser/ui:tab_model_enums_java",
      "//chrome/browser/ui:ui",
    ]
  } else {
    sources += [
      "brave_wallet_provider_delegate_impl_helper.cc",
      "brave_wallet_service_delegate_impl.cc",
      "brave_wallet_service_delegate_impl.h",
    ]
    deps += [ "//chrome/browser/ui" ]
    if (enable_extensions) {
      deps += [ ":external_wallets_importer" ]
    }
  }
}

source_set("external_wallets_importer") {
  sources = [
    "external_wallets_importer.cc",
    "external_wallets_importer.h",
  ]
  deps = [
    "//brave/browser/ethereum_remote_client/buildflags",
    "//brave/components/brave_wallet/browser",
    "//brave/components/brave_wallet/common",
    "//brave/components/brave_wallet/common:mojom",
    "//brave/third_party/argon2",
    "//components/value_store:value_store",
  ]
  if (enable_extensions) {
    deps += [
      "//extensions/browser",
      "//extensions/common",
    ]
  }
  if (ethereum_remote_client_enabled) {
    deps += [
      "//brave/browser/ethereum_remote_client",
      "//brave/browser/ethereum_remote_client:browser",
    ]
  }
}

source_set("tab_helper") {
  sources = [
    "brave_wallet_tab_helper.cc",
    "brave_wallet_tab_helper.h",
  ]

  deps = [
    "//brave/common",
    "//brave/components/brave_wallet/browser:permission_utils",
    "//components/permissions",
    "//components/sessions",
    "//content/public/browser",
  ]

  if (!is_android) {
    deps += [ "//brave/browser/ui/brave_wallet" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "asset_discovery_manager_unittest.cc",
    "asset_discovery_task_unittest.cc",
    "blockchain_images_source_unittest.cc",
    "brave_wallet_p3a_unittest.cc",
    "brave_wallet_prefs_unittest.cc",
    "brave_wallet_service_unittest.cc",
    "eth_allowance_manager_unittest.cc",
    "eth_pending_tx_tracker_unittest.cc",
    "filecoin_keyring_unittest.cc",
    "keyring_service_unittest.cc",
  ]
  deps = [
    "//base/test:test_support",
    "//brave/browser/brave_wallet",
    "//brave/browser/brave_wallet:tab_helper",
    "//brave/common",
    "//brave/components/brave_wallet/browser",
    "//brave/components/brave_wallet/browser:constants",
    "//brave/components/brave_wallet/browser:hd_keyring",
    "//brave/components/brave_wallet/browser:permission_utils",
    "//brave/components/brave_wallet/browser:pref_names",
    "//brave/components/brave_wallet/browser:test_support",
    "//brave/components/brave_wallet/browser:transaction",
    "//brave/components/brave_wallet/browser:utils",
    "//brave/components/brave_wallet/common",
    "//brave/components/brave_wallet/common:mojom",
    "//brave/components/brave_wallet/common:test_support",
    "//brave/components/filecoin/rs:rust_lib",
    "//brave/components/l10n/common",
    "//brave/components/resources:strings_grit",
    "//brave/components/version_info",
    "//chrome/browser",
    "//chrome/test:test_support",
    "//testing/gtest",
  ]
  if (!is_android) {
    sources += [
      "brave_wallet_tab_helper_unittest.cc",
      "ethereum_provider_impl_unittest.cc",
      "notifications/wallet_notification_service_unittest.cc",
      "solana_provider_impl_unittest.cc",
    ]
    deps += [
      ":brave_wallet_delegate",
      "//brave/components/brave_wallet/common",
      "//brave/components/brave_wallet/common:common_constants",
    ]
    if (enable_extensions) {
      sources += [ "external_wallets_importer_unittest.cc" ]
      deps += [ ":external_wallets_importer" ]
    }
  }
}

source_set("browser_tests") {
  testonly = true
  if (!is_android) {
    sources = [ "brave_wallet_policy_browsertest.cc" ]

    deps = [
      ":brave_wallet",
      "//base/test:test_support",
      "//brave/app:command_ids",
      "//brave/browser",
      "//brave/browser/ui/sidebar",
      "//brave/components/brave_wallet/common",
      "//brave/components/brave_wallet/common:pref_names",
      "//brave/components/sidebar",
      "//chrome/browser/ui",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//components/policy/core/browser",
      "//components/policy/core/common:test_support",
      "//components/prefs:prefs",
      "//components/user_prefs:user_prefs",
      "//content/test:test_support",
      "//testing/gtest",
      "//url",
    ]
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }
}  # source_set("browser_tests")
