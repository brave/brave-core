import("//third_party/widevine/cdm/widevine.gni")

assert(bundle_widevine_cdm || enable_widevine_cdm_component)

source_set("widevine") {
  deps = [
    "//base",
    "//brave/app:brave_generated_resources_grit",
    "//chrome/app/vector_icons",
    "//components/content_settings/core/common",
    "//content/public/browser",
    "//content/public/common",
    "//third_party/widevine/cdm:buildflags",
    "//url",
  ]

  sources = [
    "widevine_permission_request.cc",
    "widevine_permission_request.h",
    "widevine_utils.cc",
    "widevine_utils.h",
  ]

  if (bundle_widevine_cdm) {
    sources += [
      "brave_widevine_bundle_manager.cc",
      "brave_widevine_bundle_manager.h",
      "brave_widevine_bundle_unzipper.cc",
      "brave_widevine_bundle_unzipper.h",
    ]

    deps += [
      "//components/prefs",
      "//components/pref_registry",
      "//components/services/unzip/public/cpp",
      "//services/network/public/cpp",
      "//services/service_manager/public/cpp",
      "//third_party/widevine/cdm:headers",
    ]
  }
}
