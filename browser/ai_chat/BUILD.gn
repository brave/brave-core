# Copyright (c) 2023 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//printing/buildflags/buildflags.gni")

static_library("ai_chat") {
  sources = [
    "ai_chat_associated_tab_provider.cc",
    "ai_chat_associated_tab_provider.h",
    "ai_chat_service_factory.cc",
    "ai_chat_service_factory.h",
    "ai_chat_settings_helper.cc",
    "ai_chat_settings_helper.h",
    "ai_chat_urls.cc",
    "ai_chat_urls.h",
    "ai_chat_utils.cc",
    "ai_chat_utils.h",
  ]

  deps = [
    "//brave/brave_domains",
    "//brave/browser:browser_process",
    "//brave/browser/misc_metrics",
    "//brave/browser/skus",
    "//brave/components/ai_chat/content/browser",
    "//brave/components/ai_chat/core/browser",
    "//brave/components/ai_chat/core/common",
    "//brave/components/ai_chat/core/common/mojom",
    "//brave/components/constants",
    "//brave/components/resources:strings_grit",
    "//brave/net/base:utils",
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles:profile",
    "//chrome/common:channel_info",
    "//components/browsing_data/core",
    "//components/keyed_service/content",
    "//components/prefs",
    "//components/user_prefs",
    "//content/public/browser",
    "//ui/base",
  ]

  if (is_android) {
    deps += [ "//brave/build/android:jni_headers" ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "ai_chat_throttle_unittest.cc",
    "brave_open_ai_chat_permission_context_unittest.cc",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//brave/components/ai_chat/content/browser",
    "//brave/components/ai_chat/core/common",
    "//brave/components/constants",
    "//chrome/common",
    "//chrome/test:test_support",
    "//content/public/browser",
    "//content/test:test_support",
    "//testing/gtest",
    "//url",
  ]
}

source_set("browser_tests") {
  if (!is_android) {
    testonly = true
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
    sources = [
      "//chrome/browser/renderer_context_menu/render_view_context_menu_browsertest_util.cc",
      "//chrome/browser/renderer_context_menu/render_view_context_menu_browsertest_util.h",
      "ai_chat_brave_search_throttle_browsertest.cc",
      "ai_chat_browsertests.cc",
      "ai_chat_metrics_browsertest.cc",
      "ai_chat_policy_browsertest.cc",
      "ai_chat_profile_browsertest.cc",
      "ai_chat_render_view_context_menu_browsertest.cc",
      "ai_chat_ui_browsertest.cc",
      "page_content_fetcher_browsertest.cc",
    ]
    deps = [
      "//brave/app:generate_command_metadata",
      "//brave/browser/ai_chat",
      "//brave/browser/misc_metrics",
      "//brave/components/ai_chat/content/browser",
      "//brave/components/ai_chat/core/browser",
      "//brave/components/ai_chat/core/browser:test_support",
      "//brave/components/ai_chat/core/common",
      "//brave/components/ai_chat/core/common/mojom",
      "//brave/components/constants",
      "//brave/components/l10n/common:test_support",
      "//brave/components/sidebar/browser",
      "//brave/components/text_recognition/common/buildflags",
      "//chrome/browser",
      "//chrome/browser/screen_ai:screen_ai_install_state",
      "//chrome/browser/ui/browser_window",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//printing/buildflags",
      "//services/screen_ai/public/cpp:utilities",
    ]

    if (enable_print_preview) {
      sources += [
        "//chrome/browser/printing/test_print_preview_observer.cc",
        "//chrome/browser/printing/test_print_preview_observer.h",
      ]
      deps += [ "//printing:test_support" ]
    }
  }
}
