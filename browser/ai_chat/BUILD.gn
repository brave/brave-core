# Copyright (c) 2023 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/components/ai_chat/core/common/buildflags/buildflags.gni")
import("//build/buildflag_header.gni")
import("//printing/buildflags/buildflags.gni")

static_library("ai_chat") {
  sources = [
    "ai_chat_service_factory.cc",
    "ai_chat_service_factory.h",
    "ai_chat_settings_helper.cc",
    "ai_chat_settings_helper.h",
    "ai_chat_utils.cc",
    "ai_chat_utils.h",
    "browser_tool_provider.cc",
    "browser_tool_provider.h",
    "browser_tool_provider_factory.cc",
    "browser_tool_provider_factory.h",
    "tab_data_web_contents_observer.cc",
    "tab_data_web_contents_observer.h",
    "tab_tracker_service_factory.cc",
    "tab_tracker_service_factory.h",
    "upload_file_helper.cc",
    "upload_file_helper.h",
  ]

  deps = [
    "//brave/brave_domains",
    "//brave/browser:browser_process",
    "//brave/browser/misc_metrics",
    "//brave/browser/skus",
    "//brave/components/ai_chat/content/browser",
    "//brave/components/ai_chat/core/browser",
    "//brave/components/ai_chat/core/common",
    "//brave/components/ai_chat/core/common/buildflags",
    "//brave/components/ai_chat/core/common/mojom",
    "//brave/components/constants",
    "//brave/components/resources:strings_grit",
    "//brave/net/base:utils",
    "//chrome/browser:browser_process",
    "//chrome/browser/profiles:profile",
    "//chrome/browser/ui/tabs:tabs_public",
    "//chrome/common:channel_info",
    "//components/browsing_data/core",
    "//components/keyed_service/content",
    "//components/prefs",
    "//components/user_prefs",
    "//content/public/browser",
    "//printing:printing_base",
    "//services/data_decoder/public/cpp",
    "//ui/base",
    "//ui/shell_dialogs",
  ]

  if (is_android) {
    deps += [ "//brave/build/android:jni_headers" ]
  }

  if (enable_brave_ai_chat_agent_profile) {
    sources += [
      "content_agent_task_provider.h",
      "content_agent_tool_provider.h",
      "content_agent_tool_provider_factory.cc",
      "content_agent_tool_provider_factory.h",
      "page_content_blocks.cc",
      "page_content_blocks.h",
      "tools/click_tool.cc",
      "tools/click_tool.h",
      "tools/drag_and_release_tool.cc",
      "tools/drag_and_release_tool.h",
      "tools/history_tool.cc",
      "tools/history_tool.h",
      "tools/move_mouse_tool.cc",
      "tools/move_mouse_tool.h",
      "tools/navigation_tool.cc",
      "tools/navigation_tool.h",
      "tools/scroll_tool.cc",
      "tools/scroll_tool.h",
      "tools/select_tool.cc",
      "tools/select_tool.h",
      "tools/target_util.cc",
      "tools/target_util.h",
      "tools/type_tool.cc",
      "tools/type_tool.h",
      "tools/wait_tool.cc",
      "tools/wait_tool.h",
    ]

    deps += [
      "//chrome/browser/actor",
      "//components/optimization_guide/content/browser",
      "//components/optimization_guide/proto:optimization_guide_proto",
    ]
  }
}

if (enable_print_preview) {
  source_set("print_preview_extractor") {
    sources = [
      "print_preview_extractor.cc",
      "print_preview_extractor.h",
    ]
    deps = [
      "//base",
      "//brave/components/ai_chat/content/browser",
      "//content/public/browser",
      "//printing/buildflags",
    ]
  }

  source_set("print_preview_extractor_internal") {
    visibility = [
      "//brave/browser/ai_chat:unit_tests",
      "//chrome/browser",
    ]
    sources = [
      "print_preview_extractor_internal.cc",
      "print_preview_extractor_internal.h",
    ]
    deps = [
      "//brave/browser/ai_chat:print_preview_extractor",
      "//brave/components/ai_chat/content/browser",
      "//brave/components/ai_chat/core/browser",
      "//brave/components/text_recognition/common/buildflags",
      "//chrome/browser/pdf",
      "//chrome/browser/pdf:pdf_pref_names",
      "//chrome/browser/profiles",
      "//chrome/common:constants",
      "//chrome/services/printing/public/mojom",
      "//components/pdf/browser",
      "//components/prefs",
      "//components/printing/browser",
      "//components/printing/common:mojo_interfaces",
      "//components/services/print_compositor/public/mojom",
      "//pdf:buildflags",
      "//pdf:features",
    ]
  }
}

if (enable_brave_ai_chat_agent_profile) {
  source_set("ai_chat_agent_profile") {
    sources = [ "ai_chat_agent_profile_helper.h" ]

    deps = [
      "//base",
      "//brave/components/ai_chat/core/common/buildflags",
    ]
  }

  source_set("ai_chat_agent_profile_impl") {
    visibility = [
      ":browser_tests",
      "//chrome/browser",
    ]

    sources = [
      "ai_chat_agent_profile_helper.cc",
      "ai_chat_agent_profile_manager.cc",
      "ai_chat_agent_profile_manager.h",
      "content_agent_tool_provider.cc",
    ]

    public_deps = [ ":ai_chat_agent_profile" ]

    deps = [
      "//brave/browser/ai_chat",
      "//brave/components/ai_chat/core/browser",
      "//brave/components/ai_chat/core/common",
      "//brave/components/ai_chat/core/common/buildflags",
      "//brave/components/ai_chat/core/common/mojom",
      "//chrome/browser:browser_process",
      "//chrome/browser:browser_public_dependencies",
      "//chrome/browser/actor",
      "//chrome/browser/actor:types",
      "//chrome/browser/page_content_annotations:fetcher",
      "//chrome/browser/profiles:profile",
      "//chrome/browser/profiles:profile_util",
      "//chrome/browser/ui",
      "//chrome/browser/ui:browser_list",
      "//components/optimization_guide/content/browser",
    ]

    if (!is_android) {
      deps += [
        "//chrome/browser/themes",
        "//chrome/browser/ui/browser_window",
        "//chrome/browser/ui/views/side_panel",
      ]
    }
  }
}

if (enable_brave_ai_chat_agent_profile) {
  source_set("content_agent_test_support") {
    testonly = true
    sources = [
      "annotated_page_content_test_util.cc",
      "annotated_page_content_test_util.h",
      "tools/mock_content_agent_task_provider.cc",
      "tools/mock_content_agent_task_provider.h",
      "tools/target_test_util.cc",
      "tools/target_test_util.h",
    ]
    deps = [
      "//base",
      "//base/test:test_support",
      "//brave/browser/ai_chat",
      "//brave/components/ai_chat/core/browser",
      "//brave/components/ai_chat/core/common/mojom",
      "//chrome/browser/actor:types",
      "//chrome/common",
      "//components/optimization_guide/proto:optimization_guide_proto",
      "//components/tabs:public",
      "//testing/gtest",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  sources = [
    "ai_chat_throttle_unittest.cc",
    "brave_open_ai_chat_permission_context_unittest.cc",
    "full_screenshotter_unittest.cc",
    "upload_file_helper_unittest.cc",
  ]

  deps = [
    "//base",
    "//base/test:test_support",
    "//brave/browser/ai_chat",
    "//brave/components/ai_chat/content/browser",
    "//brave/components/ai_chat/core/common",
    "//brave/components/ai_chat/core/common/mojom",
    "//brave/components/constants",
    "//chrome/common",
    "//chrome/test:test_support",
    "//components/paint_preview/common:test_utils",
    "//components/paint_preview/common/mojom",
    "//components/services/paint_preview_compositor/public/mojom",
    "//content/public/browser",
    "//content/test:test_support",
    "//testing/gtest",
    "//url",
  ]

  if (enable_print_preview) {
    sources += [ "print_preview_extractor_unittest.cc" ]
    deps += [
      "//brave/browser/ai_chat:print_preview_extractor",
      "//brave/browser/ai_chat:print_preview_extractor_internal",
      "//brave/components/ai_chat/core/browser",
      "//brave/components/text_recognition/common/buildflags",
      "//chrome/services/printing/public/mojom",
      "//components/printing/browser",
    ]
  }

  if (enable_brave_ai_chat_agent_profile) {
    sources += [
      "page_content_blocks_unittest.cc",
      "tools/click_tool_unittest.cc",
      "tools/drag_and_release_tool_unittest.cc",
      "tools/history_tool_unittest.cc",
      "tools/move_mouse_tool_unittest.cc",
      "tools/navigation_tool_unittest.cc",
      "tools/scroll_tool_unittest.cc",
      "tools/select_tool_unittest.cc",
      "tools/target_util_unittest.cc",
      "tools/type_tool_unittest.cc",
      "tools/wait_tool_unittest.cc",
    ]

    deps += [
      ":content_agent_test_support",
      "//chrome/browser/actor",
      "//components/optimization_guide/proto:optimization_guide_proto",
    ]
  }
}

source_set("browser_tests") {
  if (!is_android) {
    testonly = true
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
    sources = [
      "//chrome/browser/renderer_context_menu/render_view_context_menu_browsertest_util.cc",
      "//chrome/browser/renderer_context_menu/render_view_context_menu_browsertest_util.h",
      "ai_chat_agent_profile_browsertest.cc",
      "ai_chat_brave_search_throttle_browsertest.cc",
      "ai_chat_browsertests.cc",
      "ai_chat_metrics_browsertest.cc",
      "ai_chat_policy_browsertest.cc",
      "ai_chat_profiles_enabled_browsertest.cc",
      "ai_chat_render_view_context_menu_browsertest.cc",
      "ai_chat_sidepanel_behavior_browsertest.cc",
      "ai_chat_ui_browsertest.cc",
      "content_agent_tools_browsertest.cc",
      "page_content_fetcher_browsertest.cc",
      "tools/target_test_util.cc",
      "tools/target_test_util.h",
    ]
    deps = [
      "//brave/app:generate_command_metadata",
      "//brave/browser/ai_chat",
      "//brave/browser/ai_chat:ai_chat_agent_profile",
      "//brave/browser/misc_metrics",
      "//brave/browser/ui/sidebar",
      "//brave/components/ai_chat/content/browser",
      "//brave/components/ai_chat/core/browser",
      "//brave/components/ai_chat/core/browser:test_support",
      "//brave/components/ai_chat/core/common",
      "//brave/components/ai_chat/core/common/mojom",
      "//brave/components/constants",
      "//brave/components/l10n/common:test_support",
      "//brave/components/sidebar/browser",
      "//brave/components/text_recognition/common/buildflags",
      "//chrome/browser",
      "//chrome/browser/actor",
      "//chrome/browser/pdf:pdf_extension_test_utils",
      "//chrome/browser/screen_ai:screen_ai_install_state",
      "//chrome/browser/ui",
      "//chrome/browser/ui/browser_window",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//printing/buildflags",
      "//services/screen_ai/public/cpp:utilities",
    ]

    if (enable_print_preview) {
      sources += [
        "//chrome/browser/printing/test_print_preview_observer.cc",
        "//chrome/browser/printing/test_print_preview_observer.h",
      ]
      deps += [ "//printing:test_support" ]
    }

    data = [
      "//brave/test/data/ai_chat/initial.xml",
      "//brave/test/data/ai_chat/navigated.xml",
      "//brave/test/data/ai_chat/youtube.html",
      "//brave/test/data/ai_chat/youtube-fallback.html",
      "//brave/test/data/ai_chat/youtubei/v1/player_dir/default.json",
      "//brave/test/data/ai_chat/youtubei/v1/player_dir/video_id_001.json",
      "//brave/test/data/ai_chat/youtubei/v1/player_dir/video_id_002.json",
    ]
  }
}
