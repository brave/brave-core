# Copyright (c) 2022 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/html_to_js.gni")
import("//tools/typescript/ts_library.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

preprocess_folder =
    "$root_gen_dir/brave/browser/resources/sidebar/bookmarks/preprocessed"

generate_grd("build_grdp") {
  grd_prefix = "sidebar_bookmarks"
  out_grd = "$target_gen_dir/resources.grdp"
  deps = [ ":build_ts" ]
  manifest_files = [
    "$root_gen_dir/brave/browser/resources/sidebar/bookmarks/tsconfig.manifest",
  ]
  input_files = [ "bookmarks.html" ]
  input_files_base_dir = rebase_path(".", "//")
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder = preprocess_folder
  in_files = [
    "bookmarks_api_proxy.ts",
    "bookmarks_drag_manager.ts",
  ]
}

preprocess_if_expr("preprocess_generated") {
  deps = [ ":web_components" ]
  in_folder = target_gen_dir
  out_folder = preprocess_folder
  in_files = [
    "bookmark_folder.ts",
    "bookmarks_list.ts",
  ]
}

preprocess_if_expr("preprocess_mojo") {
  deps = [ "//brave/browser/ui/webui/sidebar:mojo_bindings_webui_js" ]
  in_folder = "$root_gen_dir/mojom-webui/brave/browser/ui/webui/sidebar/"
  out_folder = preprocess_folder
  out_manifest = "$target_gen_dir/preprocessed_mojo_manifest.json"
  in_files = [ "sidebar.mojom-webui.js" ]
}

html_to_js("web_components") {
  js_files = [
    "bookmark_folder.ts",
    "bookmarks_list.ts",
  ]
}

ts_library("build_ts") {
  tsconfig_base = "tsconfig_base.json"
  root_dir = "$target_gen_dir/preprocessed"
  out_dir = "$target_gen_dir/tsc"
  in_files = [
    "bookmark_folder.ts",
    "bookmarks_list.ts",
    "bookmarks_api_proxy.ts",
    "bookmarks_drag_manager.ts",
    "sidebar.mojom-webui.js",
  ]
  definitions = [
    "//tools/typescript/definitions/bookmark_manager_private.d.ts",
    "//tools/typescript/definitions/bookmarks.d.ts",
    "//tools/typescript/definitions/chrome_event.d.ts",
  ]
  deps = [
    "//third_party/polymer/v3_0:library",
    "//ui/webui/resources:library",
    "//ui/webui/resources/mojo:library",
  ]
  extra_deps = [
    ":preprocess",
    ":preprocess_generated",
    ":preprocess_mojo",
  ]
}
