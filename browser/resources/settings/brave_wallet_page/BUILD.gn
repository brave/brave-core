# Copyright (c) 2021 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# you can obtain one at http://mozilla.org/MPL/2.0/.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../settings.gni")

# ES Module conversion from HTML Imports code

web_component_files = [
  "add_wallet_network_dialog.js",
  "brave_wallet_page.js",
  "wallet_networks_subpage.js",
  "wallet_networks_list.js",
]

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".js", ".html") ]
}

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files) {
  html_wrapper_files += [ f + ".js" ]
}

html_to_wrapper("html_wrapper_files") {
  use_js = true
  in_files = html_files
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":modules",
    ":wallet_networks",
  ]
}

group("web_modules") {
  public_deps = [
    ":html_wrapper_files",
    ":modules",
  ]
}

js_modulizer("modules") {
  input_files = [ "brave_wallet_browser_proxy.js" ]
  namespace_rewrites = settings_namespace_rewrites
}

js_library("wallet_networks") {
  deps = [
    "//ui/webui/resources/cr_elements:i18n_behavior",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
  externs_list = [ "$externs_path/settings_private.js" ]
  extra_deps = [ ":html_wrapper_files" ]
}
