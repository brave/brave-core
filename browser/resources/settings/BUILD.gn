# Copyright (c) 2020 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# you can obtain one at http://mozilla.org/MPL/2.0/.

import("//brave/browser/shell_integrations/buildflags/buildflags.gni")
import("//brave/build/config.gni")
import("//brave/components/brave_vpn/common/buildflags/buildflags.gni")
import("//brave/components/brave_wayback_machine/buildflags/buildflags.gni")
import("//brave/components/tor/buildflags/buildflags.gni")
import("//brave/resources/brave_grit.gni")
import("//chrome/common/features.gni")
import("//extensions/buildflags/buildflags.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("sources.gni")

preprocess_folder = "preprocessed"

# grit (and generate the GRD for) brave's own static resources, they don't need
# to be part of the build_webui.gni compile.
# TODO(petemill): It is no longer neccessary to make our own grd and pak, we can add to
# chromium's static_files list.
brave_grit("resources") {
  defines = chrome_grit_defines
  defines += [
    "is_release_channel=$is_release_channel",
    "enable_tor=$enable_tor",
    "enable_brave_wayback_machine=$enable_brave_wayback_machine",
  ]

  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/brave_settings_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/brave_settings_resources.h",
    "grit/brave_settings_resources_map.cc",
    "grit/brave_settings_resources_map.h",
    "brave_settings_resources.pak",
  ]
}

generate_grd("build_grd") {
  grd_prefix = "brave_settings"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = [
    "brave_sync_page/device_computer.svg",
    "brave_sync_page/device_mobile.svg",
    "brave_sync_page/qr_scanning.svg",
    "brave_sync_page/start_icon.svg",
    "images/cookies_banner.svg",
    "images/cookies_banner_dark.svg",
    "images/full_brave_brand.svg",
    "images/full_brave_brand_dark.svg",
    "images/permissions_banner.svg",
    "images/permissions_banner_dark.svg",
    "images/safe_browsing_banner.svg",
    "images/safe_browsing_banner_dark.svg",
    "images/warning_triangle.svg",
  ]
  input_files_base_dir = rebase_path(".", "//")

  deps = [ ":preprocess" ]
}

# Move brave files to the place where chromium settings page is compiled from.
# It should be the same directory that build_webui.gni uses after it performs
# its own preprocess_if_expr. Once chromium and brave frontend files are all in
# the same place, build_webui.gni will continue its typescript and rollup
# compile (etc.) from that directory.
preprocess_if_expr("preprocess") {
  defines = [
    "enable_tor=$enable_tor",
    "enable_brave_wayback_machine=$enable_brave_wayback_machine",
    "enable_brave_vpn=$enable_brave_vpn",
    "enable_brave_vpn_wireguard=$enable_brave_vpn_wireguard",
    "enable_extensions=$enable_extensions",
    "enable_pin_shortcut=$enable_pin_shortcut",
  ]
  out_folder =
      "$root_gen_dir/chrome/browser/resources/settings/$preprocess_folder"

  in_files = brave_settings_local_ts_files + brave_settings_local_html_files +
             brave_settings_local_css_files
}
