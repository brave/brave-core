# Copyright (c) 2020 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# you can obtain one at http://mozilla.org/MPL/2.0/.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../settings.gni")

web_component_files = [
  "brave_theme.js",
  "sidebar.js",
  "super_referral.js",
  "toolbar.js",
]

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".js", ".html") ]
}

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files) {
  html_wrapper_files += [ f + ".js" ]
}

html_to_wrapper("html_wrapper_files") {
  use_js = true
  in_files = html_files
}

js_type_check("closure_compile_module") {
  is_polymer3 = true
  deps = [
    ":brave_appearance_browser_proxy",
    ":brave_theme",
    ":sidebar",
    ":super_referral",
    ":toolbar",
  ]
}

js_library("brave_appearance_browser_proxy") {
  deps = [ "//ui/webui/resources/js:cr.m" ]
  externs_list = [
    "$externs_path/chrome_send.js",
    "$externs_path/management.js",
  ]
  extra_deps = [ ":html_wrapper_files" ]
}

js_library("brave_theme") {
  deps = [
    ":brave_appearance_browser_proxy",
    "//ui/webui/resources/cr_elements:web_ui_listener_behavior",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
  externs_list = [ "$externs_path/settings_private.js" ]
  extra_deps = [ ":html_wrapper_files" ]
}

js_library("super_referral") {
  deps = [
    "//ui/webui/resources/cr_elements:i18n_behavior",
    "//ui/webui/resources/js:cr.m",
    "//ui/webui/resources/js:load_time_data.m",
  ]
  externs_list = [ "$externs_path/settings_private.js" ]
  extra_deps = [ ":html_wrapper_files" ]
}

js_library("toolbar") {
  externs_list = [ "$externs_path/settings_private.js" ]
  extra_deps = [ ":html_wrapper_files" ]
}

js_library("sidebar") {
  externs_list = [ "$externs_path/settings_private.js" ]
  extra_deps = [ ":html_wrapper_files" ]
}

# make modules
group("web_modules") {
  public_deps = [ ":html_wrapper_files" ]
}
