# Copyright (c) 2021 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# you can obtain one at http://mozilla.org/MPL/2.0/.

import("//tools/polymer/html_to_wrapper.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/js_modulizer.gni")
import("../settings.gni")

# ES Module conversion from HTML Imports code

web_component_files = [
  "brave_ipfs_page.js",
  "change_ipfs_gateway_dialog.js",
  "add_ipfs_peer_dialog.js",
  "add_p2p_key_dialog.js",
  "rotate_p2p_key_dialog.js",
]

# Files that are passed as input to html_to_wrapper().
html_files = []
foreach(f, web_component_files) {
  html_files += [ string_replace(f, ".js", ".html") ]
}

# Files that are generated by html_to_wrapper().
html_wrapper_files = []
foreach(f, html_files) {
  html_wrapper_files += [ f + ".js" ]
}

html_to_wrapper("html_wrapper_files") {
  use_js = true
  in_files = html_files
}

group("web_modules") {
  public_deps = [
    ":html_wrapper_files",
    ":ipfs_peers_subpage_module",
    ":modules",
    ":p2p_keys_subpage_module",
  ]
}

polymer_modulizer("p2p_keys_subpage") {
  js_file = "p2p_keys_subpage.js"
  html_file = "p2p_keys_subpage.html"
  html_type = "dom-module"
  auto_imports = settings_auto_imports + [ "brave/browser/resources/settings/brave_ipfs_page/brave_ipfs_browser_proxy.html|BraveIPFSBrowserProxy, BraveIPFSBrowserProxyImpl" ]
  namespace_rewrites = settings_namespace_rewrites
}
polymer_modulizer("ipfs_peers_subpage") {
  js_file = "ipfs_peers_subpage.js"
  html_file = "ipfs_peers_subpage.html"
  html_type = "dom-module"
  auto_imports = settings_auto_imports + [ "brave/browser/resources/settings/brave_ipfs_page/brave_ipfs_browser_proxy.html|BraveIPFSBrowserProxy, BraveIPFSBrowserProxyImpl" ]
  namespace_rewrites = settings_namespace_rewrites
}
js_modulizer("modules") {
  input_files = [ "brave_ipfs_browser_proxy.js" ]
  namespace_rewrites = settings_namespace_rewrites
}
