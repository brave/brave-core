# Copyright (c) 2022 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# you can obtain one at http://mozilla.org/MPL/2.0/.

import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

preprocess_folder = "preprocessed"
preprocess_manifest = "brave_preprocessed_manifest.json"

grit("resources") {
  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, root_build_dir),
    "-E",
    "root_src_dir=" + rebase_path("//", root_build_dir),
  ]

  defines = chrome_grit_defines

  enable_input_discovery_for_gn_analyze = false
  defines +=
      [ "SHARED_INTERMEDIATE_DIR=" + rebase_path(root_gen_dir, root_build_dir) ]
  history_gen_dir = "$root_gen_dir/brave/browser/resources/history"
  source = "$history_gen_dir/brave_history_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/brave_history_resources.h",
    "grit/brave_history_resources_map.cc",
    "grit/brave_history_resources_map.h",
    "brave_history_resources.pak",
  ]

  output_dir = "$root_gen_dir/brave/browser/resources/history"
  resource_ids = "//brave/browser/resources/resource_ids"
}

generate_grd("build_grd") {
  grd_prefix = "brave_history"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = []
  input_files_base_dir = rebase_path(".", "//")

  deps = [ ":preprocess" ]
  manifest_files =
      [ "$root_gen_dir/chrome/browser/resources/history/$preprocess_manifest" ]
}

preprocess_if_expr("preprocess") {
  in_folder = "./"
  out_folder =
      "$root_gen_dir/chrome/browser/resources/history/$preprocess_folder"
  out_manifest =
      "$root_gen_dir/chrome/browser/resources/history/$preprocess_manifest"
  in_files = [
    "brave_overrides/side_bar.js",
    "brave_overrides/index.js",
  ]
}
