# Copyright 2022 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//brave/resources/brave_grit.gni")
import("//chrome/common/features.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/webui_features.gni")

preprocess_folder =
    "$root_gen_dir/brave/browser/resources/federated_internals/preprocessed"
preprocess_mojo_manifest = "preprocessed_mojo_manifest.json"

generate_grd("build_grd") {
  grd_prefix = "federated_internals"
  out_grd = "$target_gen_dir/${grd_prefix}_resources.grd"
  input_files = [
    "federated_internals.html",
    "federated_internals.css",
    "federated_internals.js",
    "federated_internals_browser_proxy.js",
  ]
  input_files_base_dir = rebase_path(".", "//")

  deps = [ ":preprocess_mojo" ]
  manifest_files = [ "$target_gen_dir/$preprocess_mojo_manifest" ]
}

preprocess_if_expr("preprocess_mojo") {
  deps = [ "//brave/browser/ui/webui/brave_federated:mojo_bindings_webui_js" ]
  in_folder =
      "$root_gen_dir/mojom-webui/brave/browser/ui/webui/brave_federated/"
  out_folder = "$target_gen_dir/$preprocess_folder"
  out_manifest = "$target_gen_dir/$preprocess_mojo_manifest"
  in_files = [ "federated_internals.mojom-webui.js" ]
}

brave_grit("resources") {
  defines = chrome_grit_defines

  enable_input_discovery_for_gn_analyze = false
  source = "$target_gen_dir/federated_internals_resources.grd"
  deps = [ ":build_grd" ]

  outputs = [
    "grit/federated_internals_resources.h",
    "grit/federated_internals_resources_map.cc",
    "grit/federated_internals_resources_map.h",
    "federated_internals_resources.pak",
  ]
}

js_type_check("closure_compile") {
  closure_flags = default_closure_args + mojom_js_args + [
                    "js_module_root=" + rebase_path(".", root_build_dir),
                    "js_module_root=" + rebase_path(
                            "$root_gen_dir/mojom-webui/brave/browser/ui/webui/brave_federated",
                            root_build_dir),
                  ]

  deps = [
    ":federated_internals",
    ":federated_internals_browser_proxy",
  ]
}

js_library("federated_internals") {
  deps = [
    ":federated_internals_browser_proxy",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:util.m",
  ]
}

js_library("federated_internals_browser_proxy") {
  deps = [
    "//brave/browser/ui/webui/brave_federated:mojo_bindings_webui_js",
    "//ui/webui/resources/js:assert.m",
    "//ui/webui/resources/js:util.m",
  ]
}
