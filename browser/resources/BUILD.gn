import("//chrome/browser/resources/optimize_webui.gni")
import("//chrome/common/features.gni")
import("//tools/grit/grit_rule.gni")

grit("brave_extension_grit") {
  source = "brave_extension.grd"

  grit_flags = [
    "-E",
    "root_gen_dir=" + rebase_path(root_gen_dir, "."),
  ]

  outputs = [
    "grit/brave_extension.h",
    "grit/brave_extension_resources_map.cc",
    "grit/brave_extension_resources_map.h",
    "brave_extension.pak",
  ]

  deps = [
    "//brave/vendor/brave-extension",
  ]

  resource_ids = "resource_ids"
}

grit("brave_settings_resources") {
  source = "settings/settings_resources.grd"

  source_is_generated = optimize_webui

  outputs = [
    "grit/brave_settings_resources.h",
    "grit/brave_settings_resources_map.cc",
    "grit/brave_settings_resources_map.h",
    "brave_settings_resources.pak",
  ]

  output_dir = "$root_gen_dir/brave/browser/resources"
  resource_ids = "resource_ids"
}

grit("brave_extensions_resources") {
  source = "md_extensions/extensions_resources.grd"

  source_is_generated = optimize_webui

  outputs = [
    "grit/brave_extensions_resources.h",
    "grit/brave_extensions_resources_map.cc",
    "grit/brave_extensions_resources_map.h",
    "brave_extensions_resources.pak",
  ]

  output_dir = "$root_gen_dir/brave/browser/resources"
  resource_ids = "resource_ids"
}

if (optimize_webui) {
  group("unpak") {
    deps = [
      ":unpak_settings_resources",
      ":unpak_extensions_resources",
    ]
  }

  action("unpak_settings_resources") {
    script = "//chrome/browser/resources/unpack_pak.py"

    pak_file = "brave_settings_resources.pak"
    out_folder = "$root_gen_dir/chrome/browser/resources/settings/settings_resources.unpak"

    inputs = [
      "$target_gen_dir/brave_settings_resources.pak",
    ]

    outputs = [
      "${out_folder}/brave_unpack.stamp",
    ]

    deps = [
      ":brave_settings_resources",
    ]

    args = [
      "--out_folder",
      rebase_path(out_folder, root_build_dir),
      "--pak_file",
      rebase_path("$target_gen_dir/${pak_file}", root_build_dir),
    ]
  }

  action("unpak_extensions_resources") {
    script = "//chrome/browser/resources/unpack_pak.py"

    pak_file = "brave_extensions_resources.pak"
    out_folder = "$root_gen_dir/chrome/browser/resources/md_extensions/extensions_resources.unpak"

    inputs = [
      "$target_gen_dir/brave_extensions_resources.pak",
    ]

    outputs = [
      "${out_folder}/brave_unpack.stamp",
    ]

    deps = [
      ":brave_extensions_resources",
    ]

    args = [
      "--out_folder",
      rebase_path(out_folder, root_build_dir),
      "--pak_file",
      rebase_path("$target_gen_dir/${pak_file}", root_build_dir),
    ]
  }
}
