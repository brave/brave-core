import("//build/buildflag_header.gni")
import("buildflags/buildflags.gni")

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "ENABLE_TOR=$enable_tor" ]
}

source_set("tor") {
  # Remove when https://github.com/brave/brave-browser/issues/10655 is resolved
  check_includes = false
  sources = [
    "tor_launcher_service_observer.h",
    "tor_profile_service_factory.cc",
    "tor_profile_service_factory.h",
  ]

  deps = [
    "//base",
    "//brave/common/tor",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//content/public/browser",
    "//mojo/public/cpp/bindings",
  ]

  public_deps = [
    ":buildflags",
  ]

  if (enable_tor) {
    sources += [
      "tor_launcher_factory.cc",
      "tor_launcher_factory.h",
      "tor_navigation_throttle.cc",
      "tor_navigation_throttle.h",
      "tor_profile_service.cc",
      "tor_profile_service.h",
      "tor_profile_service_impl.cc",
      "tor_profile_service_impl.h",
      "tor_tab_helper.cc",
      "tor_tab_helper.h",
    ]

    deps += [
      "//brave/components/services/tor/public/interfaces",
      "//content/public/browser",
      "//content/public/common",
      "//extensions/common:common_constants",
      "//net",
      "//services/service_manager",
      "//url",
    ]

    # Below dep list are not directly used tor target.
    # Only added to fix intermittent build failure.
    # Most of the case comes from including brave_browser_process_impl.h`
    # headers. That header includes some buildflags but sometimes they are
    # not generated before evaluating tor target.
    deps += [
      "//brave/components/brave_referrals/buildflags",
      "//brave/components/greaselion/browser/buildflags",
      "//brave/components/speedreader:buildflags",
    ]
  }
}
