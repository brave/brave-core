import("//brave/components/brave_ads/browser/buildflags/buildflags.gni")
import("//brave/components/tor/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("tor") {
  # Remove when https://github.com/brave/brave-browser/issues/10655 is resolved
  check_includes = false
  visibility = [
    ":*",
    "//brave/browser",
    "//brave/browser/decentralized_dns/test:unit_tests",
    "//brave/browser/ipfs/test:*",
    "//brave/browser/profiles",
    "//brave/browser/ui",
    "//brave/test:*",
    "//chrome/browser",
  ]

  if (enable_tor) {
    sources = [
      "onion_location_navigation_throttle_delegate.cc",
      "onion_location_navigation_throttle_delegate.h",
      "tor_profile_manager.cc",
      "tor_profile_manager.h",
      "tor_profile_service_factory.cc",
      "tor_profile_service_factory.h",
    ]

    deps = [
      "//base",
      "//brave/browser/translate/buildflags",
      "//brave/common:pref_names",
      "//brave/components/brave_webtorrent/browser/buildflags",
      "//brave/components/tor",
      "//brave/components/tor:pref_names",
      "//chrome/common",
      "//components/keyed_service/content",
      "//components/safe_browsing/core/common:safe_browsing_prefs",
      "//components/translate/core/browser",
      "//content/public/browser",
      "//third_party/blink/public/common",
    ]

    # Below dep list are not directly used tor target.
    # Only added to fix intermittent build failure.
    # Most of the case comes from including brave_browser_process_impl.h`
    # headers. That header includes some buildflags but sometimes they are
    # not generated before evaluating tor target.
    deps += [
      "//brave/components/brave_referrals/buildflags",
      "//brave/components/greaselion/browser/buildflags",
      "//brave/components/speedreader:buildflags",
    ]
  }
}

source_set("unit_tests") {
  testonly = true
  if (enable_tor) {
    sources = [
      "tor_navigation_throttle_unittest.cc",
      "tor_profile_manager_unittest.cc",
    ]

    deps = [
      "//base",
      "//base/test:test_support",
      "//brave/browser/tor",
      "//brave/browser/translate/buildflags",
      "//brave/common:pref_names",
      "//brave/components/brave_webtorrent/browser/buildflags",
      "//brave/components/tor",
      "//brave/components/tor:test_support",
      "//chrome/common",
      "//chrome/test:test_support",
      "//components/keyed_service/content",
      "//components/safe_browsing/core/common:safe_browsing_prefs",
      "//components/translate/core/browser",
      "//content/public/browser",
      "//content/test:test_support",
      "//testing/gtest",
      "//third_party/blink/public/common",
      "//url",
    ]
  }
}

source_set("browser_tests") {
  testonly = true
  if (enable_tor) {
    sources = [
      "brave_local_state_browsertest.cc",
      "brave_tor_client_updater_browsertest.cc",
      "onion_location_navigation_throttle_browsertest.cc",
      "tor_profile_manager_browsertest.cc",
    ]

    deps = [
      "//base",
      "//brave/app:brave_generated_resources_grit",
      "//brave/browser",
      "//brave/browser/tor",
      "//brave/browser/ui",
      "//brave/common",
      "//brave/components/ipfs/buildflags",
      "//brave/components/tor",
      "//brave/components/tor:pref_names",
      "//brave/components/tor:test_support",
      "//chrome/browser",
      "//chrome/browser:browser_process",
      "//chrome/test:test_support",
      "//chrome/test:test_support_ui",
      "//components/bookmarks/browser",
      "//components/bookmarks/common",
      "//content/public/browser",
      "//content/test:test_support",
      "//net:test_support",
      "//ui/views:test_support",
    ]

    if (enable_extensions) {
      deps += [
        "//extensions/browser",
        "//extensions/common",
      ]
    }

    if (brave_ads_enabled) {
      deps += [
        "//brave/browser/brave_ads",
        "//brave/components/brave_ads/browser",
      ]
    }

    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]
  }
}
