import("//build/buildflag_header.gni")
import("buildflags/buildflags.gni")

buildflag_header("buildflags") {
  header = "buildflags.h"
  flags = [ "ENABLE_TOR=$enable_tor" ]
}

source_set("tor") {
  sources = [
    "tor_launcher_service_observer.h",
    "tor_profile_service_factory.cc",
    "tor_profile_service_factory.h",
  ]

  deps = [
    "//base",
    "//brave/common/tor",
    "//brave/common/tor:tor_mojom_bindings",
    "//components/keyed_service/content",
    "//components/keyed_service/core",

    # for profile.h
    "//content/public/browser",
    "//services/service_manager",
  ]

  public_deps = [
    ":buildflags",
  ]

  if (enable_tor) {
    sources += [
      "tor_launcher_factory.cc",
      "tor_launcher_factory.h",
      "tor_profile_service.cc",
      "tor_profile_service.h",
      "tor_profile_service_impl.cc",
      "tor_profile_service_impl.h",
    ]
  }
}
