# Copyright (c) 2021 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//brave/components/web_discovery/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

if (enable_web_discovery_native) {
  static_library("web_discovery") {
    sources = [
      "web_discovery_service_factory.cc",
      "web_discovery_service_factory.h",
    ]
    deps = [
      "//base",
      "//brave/components/web_discovery/browser",
      "//brave/components/web_discovery/common",
      "//chrome/browser:browser_process",
      "//chrome/browser/profiles:profile",
      "//chrome/common:constants",
      "//components/keyed_service/content",
      "//components/user_prefs",
      "//content/public/browser",
      "//third_party/re2",
    ]
  }
}

source_set("unit_tests") {
  if (enable_extensions || enable_web_discovery_native) {
    testonly = true

    sources = [ "web_discovery_unittest.cc" ]

    deps = [
      "//brave/components/constants",
      "//brave/components/search_engines",
      "//chrome/browser",
      "//chrome/browser/prefs",
      "//chrome/test:test_support",
      "//components/prefs",
      "//components/search_engines",
      "//components/sync_preferences:test_support",
      "//content/public/browser",
      "//content/test:test_support",
      "//testing/gtest",
    ]
  }
}

source_set("browser_tests") {
  if (enable_extensions || enable_web_discovery_native) {
    testonly = true
    defines = [ "HAS_OUT_OF_PROC_TEST_RUNNER" ]

    sources = [ "content_scraper_browsertest.cc" ]

    if (!is_android) {
      sources += [ "web_discovery_browsertest.cc" ]
    }

    deps = [
      "//base/test:test_support",
      "//brave/components/constants",
      "//brave/components/search_engines",
      "//brave/components/web_discovery/browser",
      "//brave/components/web_discovery/browser:internal",
      "//brave/components/web_discovery/common",
      "//brave/components/web_discovery/common:mojom",
      "//chrome/browser",
      "//chrome/browser/profiles:profile",
      "//chrome/browser/ui",
      "//chrome/test:test_support",
      "//components/infobars/content",
      "//components/prefs",
      "//content/public/browser",
      "//content/test:test_support",
      "//net:test_support",
      "//services/service_manager/public/cpp",
      "//testing/gmock",
      "//testing/gtest",
    ]
  }
}
