import("//brave/browser/tor/buildflags/buildflags.gni")
import("//brave/components/binance/browser/buildflags/buildflags.gni")
import("//brave/components/brave_rewards/browser/buildflags/buildflags.gni")
import("//brave/components/brave_sync/buildflags/buildflags.gni")
import("//brave/components/brave_wallet/browser/buildflags/buildflags.gni")
import("//brave/components/brave_wayback_machine/buildflags/buildflags.gni")
import("//brave/components/brave_webtorrent/browser/buildflags/buildflags.gni")
import("//build/config/features.gni")
import("//components/gcm_driver/config.gni")

declare_args() {
  brave_infura_project_id = ""
}

source_set("extensions") {
  sources = [
    "api/brave_action_api.cc",
    "api/brave_action_api.h",
    "api/brave_extensions_api_client.cc",
    "api/brave_extensions_api_client.h",
    "api/brave_rewards_api.cc",
    "api/brave_rewards_api.h",
    "api/brave_shields_api.cc",
    "api/brave_shields_api.h",
    "api/brave_theme_api.cc",
    "api/brave_theme_api.h",
    "api/rewards_notifications_api.cc",
    "api/rewards_notifications_api.h",
    "api/settings_private/brave_prefs_util.cc",
    "api/settings_private/brave_prefs_util.h",
    "brave_component_extension_resource_manager.cc",
    "brave_component_extension_resource_manager.h",
    "brave_component_loader.cc",
    "brave_component_loader.h",
    "brave_extension_install_prompt.cc",
    "brave_extension_install_prompt.h",
    "brave_extension_management.cc",
    "brave_extension_management.h",
    "brave_extension_provider.cc",
    "brave_extension_provider.h",
    "brave_extension_service.cc",
    "brave_extension_service.h",
    "brave_extensions_browser_api_provider.cc",
    "brave_extensions_browser_api_provider.h",
    "brave_extensions_browser_client_impl.cc",
    "brave_extensions_browser_client_impl.h",
    "brave_theme_event_router.cc",
    "brave_theme_event_router.h",
    "updater/brave_update_client_config.cc",
    "updater/brave_update_client_config.h",
  ]

  if (brave_wallet_enabled) {
    sources += [
      "api/brave_wallet_api.cc",
      "api/brave_wallet_api.h",
      "brave_wallet_navigation_throttle.cc",
      "brave_wallet_navigation_throttle.h",
      "brave_wallet_util.cc",
      "brave_wallet_util.h"
    ]
    configs += [ ":infura_config" ]
  }

  if (binance_enabled) {
    sources += [
      "api/binance_api.cc",
      "api/binance_api.h",
    ]
  }

  deps = [
    "//base",
    "//brave/app:brave_generated_resources_grit",
    "//brave/browser/tor:buildflags",
    "//brave/browser/webcompat_reporter",
    "//brave/common",
    "//brave/common:pref_names",
    "//brave/common/extensions/api",
    "//brave/components/brave_component_updater/browser",
    "//brave/components/brave_extension:generated_resources",
    "//brave/components/brave_extension:static_resources",
    "//brave/components/brave_shields/browser",
    "//brave/components/brave_shields/common",
    "//brave/components/brave_wayback_machine:buildflags",
    "//chrome/browser/extensions",
    "//chrome/common",
    "//components/gcm_driver:gcm_driver",
    "//components/gcm_driver:gcm_buildflags",
    "//components/prefs",
    "//components/services/patch/content",
    "//components/services/unzip/content",
    "//components/update_client:patch_impl",
    "//components/update_client:unzip_impl",
    "//content/public/browser",
    "//extensions/browser",
    "//extensions/common",
    "//third_party/re2",
    "//url",
  ]

  if (brave_rewards_enabled) {
    deps += [
      "//brave/components/brave_rewards/browser",
      "//brave/components/brave_rewards/resources/extension:extension_generated_resources",
      "//brave/components/brave_rewards/resources/extension:static_resources",
    ]
  }
  if (enable_brave_sync) {
    sources += [
      "api/brave_sync_api.cc",
      "api/brave_sync_api.h",
      "api/brave_sync_event_router.cc",
      "api/brave_sync_event_router.h",
    ]

    deps += [
      "//brave/components/brave_sync",
      "//brave/components/brave_sync:generated_resources",
      "//brave/components/brave_sync:static_resources",
    ]
  }
  if (enable_brave_webtorrent) {
    deps += [
      "//brave/components/brave_webtorrent/browser",
      "//brave/components/brave_webtorrent:generated_resources",
      "//brave/components/brave_webtorrent:static_resources",
    ]
    sources += [
      "brave_webtorrent_navigation_throttle.cc",
      "brave_webtorrent_navigation_throttle.h",
    ]
  }
  if (enable_brave_wayback_machine) {
    deps += [
      "//brave/components/brave_wayback_machine",
    ]
  }
}

config("infura_config") {
  defines = [ "BRAVE_INFURA_PROJECT_ID=\"$brave_infura_project_id\"" ]
}
