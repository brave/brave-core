import("//brave/browser/translate/buildflags/buildflags.gni")
import("//brave/components/brave_referrals/buildflags/buildflags.gni")
import("//brave/components/brave_webtorrent/browser/buildflags/buildflags.gni")
import("//build/config/features.gni")

# Refer to the keyed API spec for more details about the Brave Services Key
defines = brave_service_key_defines

source_set("net") {
  # Remove when https://github.com/brave/brave-browser/issues/10659 is resolved
  check_includes = false
  configs += [ "//brave/build/geolocation" ]
  sources = [
    "brave_ad_block_tp_network_delegate_helper.cc",
    "brave_ad_block_tp_network_delegate_helper.h",
    "brave_block_safebrowsing_urls.cc",
    "brave_block_safebrowsing_urls.h",
    "brave_common_static_redirect_network_delegate_helper.cc",
    "brave_common_static_redirect_network_delegate_helper.h",
    "brave_httpse_network_delegate_helper.cc",
    "brave_httpse_network_delegate_helper.h",
    "brave_proxying_url_loader_factory.cc",
    "brave_proxying_url_loader_factory.h",
    "brave_proxying_web_socket.cc",
    "brave_proxying_web_socket.h",
    "brave_request_handler.cc",
    "brave_request_handler.h",
    "brave_site_hacks_network_delegate_helper.cc",
    "brave_site_hacks_network_delegate_helper.h",
    "brave_static_redirect_network_delegate_helper.cc",
    "brave_static_redirect_network_delegate_helper.h",
    "brave_stp_util.cc",
    "brave_stp_util.h",
    "brave_system_request_handler.cc",
    "brave_system_request_handler.h",
    "resource_context_data.cc",
    "resource_context_data.h",
    "url_context.cc",
    "url_context.h",
  ]

  deps = [
    "//base",
    "//brave/app:brave_generated_resources_grit",
    "//brave/browser/safebrowsing",
    "//brave/browser/translate/buildflags",
    "//brave/common",
    "//brave/components/brave_component_updater/browser",
    "//brave/components/brave_referrals/buildflags",
    "//brave/components/brave_shields/browser",
    "//brave/components/brave_webtorrent/browser/buildflags",
    "//brave/extensions:common",
    "//components/prefs",
    "//content/public/browser",
    "//content/public/common",
    "//components/content_settings/core/browser",
    "//extensions/common:common_constants",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//net",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
    "//third_party/blink/public/common",
    "//third_party/blink/public/mojom:mojom_platform_headers",
    "//third_party/re2",
    "//url",
  ]

  if (enable_brave_referrals) {
    sources += [
      "brave_referrals_network_delegate_helper.cc",
      "brave_referrals_network_delegate_helper.h",
    ]

    deps += [
      "//brave/components/brave_referrals/browser",
    ]
  }

  if (enable_brave_webtorrent) {
    sources += [
      "brave_torrent_redirect_network_delegate_helper.cc",
      "brave_torrent_redirect_network_delegate_helper.h",
    ]

    deps += [
      "//brave/components/brave_webtorrent/browser/",
      "//extensions/browser",
    ]
  }

  if (enable_brave_translate_go) {
    sources += [
      "brave_translate_redirect_network_delegate_helper.cc",
      "brave_translate_redirect_network_delegate_helper.h",
    ]
  }
}
