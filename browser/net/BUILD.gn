import("//brave/components/brave_referrals/buildflags/buildflags.gni")
import("//brave/components/brave_webtorrent/browser/buildflags/buildflags.gni")
import("//brave/components/decentralized_dns/buildflags/buildflags.gni")
import("//brave/components/ipfs/buildflags/buildflags.gni")
import("//build/config/features.gni")

source_set("net") {
  # Remove when https://github.com/brave/brave-browser/issues/10659 is resolved
  check_includes = false
  configs += [ "//brave/build/geolocation" ]
  sources = [
    "brave_ad_block_csp_network_delegate_helper.cc",
    "brave_ad_block_csp_network_delegate_helper.h",
    "brave_ad_block_tp_network_delegate_helper.cc",
    "brave_ad_block_tp_network_delegate_helper.h",
    "brave_block_safebrowsing_urls.cc",
    "brave_block_safebrowsing_urls.h",
    "brave_common_static_redirect_network_delegate_helper.cc",
    "brave_common_static_redirect_network_delegate_helper.h",
    "brave_httpse_network_delegate_helper.cc",
    "brave_httpse_network_delegate_helper.h",
    "brave_proxying_url_loader_factory.cc",
    "brave_proxying_url_loader_factory.h",
    "brave_proxying_web_socket.cc",
    "brave_proxying_web_socket.h",
    "brave_request_handler.cc",
    "brave_request_handler.h",
    "brave_service_key_network_delegate_helper.cc",
    "brave_service_key_network_delegate_helper.h",
    "brave_site_hacks_network_delegate_helper.cc",
    "brave_site_hacks_network_delegate_helper.h",
    "brave_static_redirect_network_delegate_helper.cc",
    "brave_static_redirect_network_delegate_helper.h",
    "brave_stp_util.cc",
    "brave_stp_util.h",
    "brave_system_request_handler.cc",
    "brave_system_request_handler.h",
    "global_privacy_control_network_delegate_helper.cc",
    "global_privacy_control_network_delegate_helper.h",
    "resource_context_data.cc",
    "resource_context_data.h",
    "url_context.cc",
    "url_context.h",
  ]

  deps = [
    "//base",
    "//brave/app:brave_generated_resources_grit",
    "//brave/browser/safebrowsing",
    "//brave/common",
    "//brave/components/brave_component_updater/browser",
    "//brave/components/brave_referrals/buildflags",
    "//brave/components/brave_shields/browser",
    "//brave/components/brave_shields/common",
    "//brave/components/brave_webtorrent/browser/buildflags",
    "//brave/components/decentralized_dns/buildflags",
    "//brave/components/ipfs/buildflags",
    "//brave/extensions:common",
    "//components/content_settings/core/browser",
    "//components/prefs",
    "//components/proxy_config",
    "//components/user_prefs",
    "//content/public/browser",
    "//content/public/common",
    "//mojo/public/cpp/bindings",
    "//mojo/public/cpp/system",
    "//net",
    "//services/network:network_service",
    "//services/network/public/cpp",
    "//services/network/public/mojom",
    "//third_party/blink/public/common",
    "//third_party/blink/public/mojom:mojom_platform_headers",
    "//third_party/re2",
    "//url",
  ]

  if (enable_extensions) {
    deps += [ "//extensions/common:common_constants" ]
  }

  if (enable_ipfs) {
    sources += [
      "ipfs_redirect_network_delegate_helper.cc",
      "ipfs_redirect_network_delegate_helper.h",
    ]
    deps += [
      "//brave/components/ipfs",
      "//chrome/common:channel_info",
    ]
  }

  if (enable_brave_referrals) {
    sources += [
      "brave_referrals_network_delegate_helper.cc",
      "brave_referrals_network_delegate_helper.h",
    ]

    deps += [ "//brave/components/brave_referrals/browser" ]
  }

  if (enable_brave_webtorrent) {
    sources += [
      "brave_torrent_redirect_network_delegate_helper.cc",
      "brave_torrent_redirect_network_delegate_helper.h",
    ]

    deps += [
      "//brave/components/brave_webtorrent/browser/",
      "//extensions/browser",
    ]
  }

  if (decentralized_dns_enabled) {
    sources += [
      "decentralized_dns_network_delegate_helper.cc",
      "decentralized_dns_network_delegate_helper.h",
    ]

    deps += [
      "//brave/components/brave_wallet/browser",
      "//brave/components/brave_wallet/common",
      "//brave/components/decentralized_dns",
    ]
  }
}
