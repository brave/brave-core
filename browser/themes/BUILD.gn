source_set("themes") {
  # Remove when https://github.com/brave/brave-browser/issues/10656 is resolved
  check_includes = false
  sources = [ "brave_dark_mode_utils.h" ]

  deps = [
    "//base",
  ]

  if (is_android) {
    sources += [ "brave_dark_mode_utils_android.cc" ]
  } else {
    sources += [
      "brave_dark_mode_utils.cc",
      "brave_dark_mode_utils_internal.cc",
      "brave_dark_mode_utils_internal.h",
      "brave_theme_service.cc",
      "brave_theme_service.h",
      "brave_theme_helper.cc",
      "brave_theme_helper.h",
      "theme_properties.cc",
      "theme_properties.h",
    ]

    if (is_linux) {
      sources += [ "brave_dark_mode_utils_linux.cc" ]
    }

    if (is_mac) {
      sources += [ "brave_dark_mode_utils_mac.mm" ]
    }

    if (is_win) {
      sources += [
        "brave_dark_mode_utils_win.cc",
        "brave_theme_helper_win.cc",
        "brave_theme_helper_win.h",
      ]
    }

    deps += [
      "//brave/app:brave_generated_resources_grit",
      "//brave/common",
      "//chrome/common",
      "//components/prefs",
      "//components/pref_registry",
      "//components/version_info",
      "//skia",
    ]
  }
}
