source_set("themes") {
  sources = [ "brave_dark_mode_utils.h" ]

  deps = [
    "//base",
    "//brave/common:pref_names",
    "//brave/common:switches",
  ]

  if (is_android) {
    sources += [ "brave_dark_mode_utils_android.cc" ]
  } else {
    sources += [
      "brave_dark_mode_utils_internal.cc",
      "brave_dark_mode_utils_internal.h",
      "theme_properties.cc",
      "theme_properties.h",
    ]

    if (is_linux) {
      sources += [ "brave_dark_mode_utils_linux.cc" ]
    }

    if (is_mac) {
      sources += [ "brave_dark_mode_utils_mac.mm" ]
    }

    if (is_win) {
      sources += [ "brave_dark_mode_utils_win.cc" ]
    }

    deps += [
      "//brave/components/brave_vpn/buildflags",
      "//brave/components/sidebar/buildflags",
      "//brave/components/speedreader:buildflags",
      "//chrome/browser:theme_properties",
      "//skia:skia",
      "//ui/gfx:color_utils",
      "//ui/native_theme:native_theme",
      "//ui/native_theme:native_theme_browser",
    ]
  }
}
