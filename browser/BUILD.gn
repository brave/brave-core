import("//brave/build/config.gni")
import("//brave/browser/tor/buildflags/buildflags.gni")
import("//brave/components/brave_ads/browser/buildflags/buildflags.gni")
import("//brave/components/brave_perf_predictor/browser/buildflags/buildflags.gni")
import("//brave/components/brave_sync/buildflags/buildflags.gni")
import("//brave/components/brave_wallet/browser/buildflags/buildflags.gni")
import("//brave/components/brave_wayback_machine/buildflags/buildflags.gni")
import("//brave/components/brave_webtorrent/browser/buildflags/buildflags.gni")
import("//brave/components/greaselion/browser/buildflags/buildflags.gni")
import("//brave/components/speedreader/buildflags.gni")
import("//build/buildflag_header.gni")
import("//build/config/features.gni")
import("//chrome/common/features.gni")
import("//components/gcm_driver/config.gni")
import("//extensions/buildflags/buildflags.gni")
import("//third_party/widevine/cdm/widevine.gni")
import("//third_party/protobuf/proto_library.gni")

source_set("browser_process") {
  visibility = [
    "//chrome/browser/*",
    "//brave/browser/*",
    "//brave:browser_dependencies",
    "//brave/test:*"
  ]

  sources = [
    "autocomplete/brave_autocomplete_provider_client.cc",
    "autocomplete/brave_autocomplete_provider_client.h",
    "autocomplete/brave_autocomplete_scheme_classifier.cc",
    "autocomplete/brave_autocomplete_scheme_classifier.h",
    "brave_rewards/rewards_tab_helper.cc",
    "brave_rewards/rewards_tab_helper.h",
    "brave_shields/ad_block_pref_service_factory.cc",
    "brave_shields/ad_block_pref_service_factory.h",
    "brave_shields/cookie_pref_service_factory.cc",
    "brave_shields/cookie_pref_service_factory.h",
    "brave_browser_main_extra_parts.cc",
    "brave_browser_main_extra_parts.h",
    "brave_browser_main_parts.cc",
    "brave_browser_main_parts.h",
    "brave_browser_process_impl.cc",
    "brave_browser_process_impl.h",
    "brave_content_browser_client.cc",
    "brave_content_browser_client.h",
    "brave_local_state_prefs.cc",
    "brave_local_state_prefs.h",
    "brave_profile_prefs.cc",
    "brave_profile_prefs.h",
    "brave_stats_updater.cc",
    "brave_stats_updater.h",
    "brave_stats_updater_params.cc",
    "brave_stats_updater_params.h",
    "brave_stats_updater_util.cc",
    "brave_stats_updater_util.h",
    "brave_tab_helpers.cc",
    "brave_tab_helpers.h",
    "browser_context_keyed_service_factories.cc",
    "browser_context_keyed_service_factories.h",
    "component_updater/brave_component_installer.cc",
    "component_updater/brave_component_installer.h",
    "component_updater/brave_component_updater_configurator.cc",
    "component_updater/brave_component_updater_configurator.h",
    "component_updater/brave_component_updater_delegate.cc",
    "component_updater/brave_component_updater_delegate.h",
    "geolocation/brave_geolocation_permission_context.cc",
    "geolocation/brave_geolocation_permission_context.h",
    "metrics/metrics_reporting_util.cc",
    "metrics/metrics_reporting_util.h",
    "search_engines/guest_window_search_engine_provider_service.cc",
    "search_engines/guest_window_search_engine_provider_service.h",
    "search_engines/private_window_search_engine_provider_service.cc",
    "search_engines/private_window_search_engine_provider_service.h",
    "search_engines/search_engine_provider_service_factory.cc",
    "search_engines/search_engine_provider_service_factory.h",
    "search_engines/search_engine_provider_util.cc",
    "search_engines/search_engine_provider_util.h",
    "search_engines/search_engine_tracker.cc",
    "search_engines/search_engine_tracker.h",
    "search_engines/tor_window_search_engine_provider_service.cc",
    "search_engines/tor_window_search_engine_provider_service.h",
    "search_engines/search_engine_provider_service.cc",
    "search_engines/search_engine_provider_service.h",
    "update_util.cc",
    "update_util.h",
  ]

  if (is_mac) {
    sources += [
      "brave_browser_main_parts_mac.h",
      "brave_browser_main_parts_mac.mm",
    ]
  }

  if (enable_tor) {
    sources += [
      # TODO(bridiver) - change the directory for these
      "extensions/brave_tor_client_updater.cc",
      "extensions/brave_tor_client_updater.h",
    ]
  }
  if (enable_sparkle) {
    sources += [
      "mac/sparkle_glue.h",
      "mac/sparkle_glue.mm",
      "mac/su_updater.h",
    ]
  }

  deps = [
    ":sparkle_buildflags",
    ":version_info",
    "autoplay",
    "content_settings",
    "download",
    "net",
    "permissions",
    "profiles",
    "renderer_context_menu",
    "ui",
    "//base",
    "//brave/browser/net",
    "//brave/browser/notifications",
    "//brave/browser/tor:buildflags",
    "//brave/browser/translate/buildflags",
    "//brave/common",
    "//brave/common:pref_names",
    "//brave/components/brave_ads/browser",
    "//brave/components/brave_component_updater/browser",
    "//brave/components/brave_drm",
    "//brave/components/brave_referrals/browser",
    "//brave/components/brave_rewards/browser",
    "//brave/components/brave_shields/browser",
    "//brave/components/brave_shields/common",
    "//brave/components/brave_wayback_machine:buildflags",
    "//brave/components/brave_webtorrent/browser/buildflags",
    "//brave/components/content_settings/core/browser",
    "//brave/components/greaselion/browser/buildflags",
    "//brave/components/ntp_background_images/browser",
    "//brave/components/ntp_tiles",
    "//brave/components/p3a",
    "//brave/components/resources",
    "//brave/components/services:brave_content_manifest_overlays",
    "//brave/services/network/public/cpp",
    "//chrome/common",
    "//components/autofill/core/common",
    "//components/browsing_data/core",
    "//components/component_updater",
    "//components/content_settings/core/common",
    "//components/content_settings/core/browser",
    "//components/embedder_support",
    "//components/gcm_driver:gcm_driver",
    "//components/gcm_driver:gcm_buildflags",
    "//components/keyed_service/content",
    "//components/password_manager/core/common",
    "//components/permissions",
    "//components/policy/core/browser",
    "//components/prefs",
    "//components/safe_browsing/core/common:safe_browsing_prefs",
    "//components/search_engines",
    "//components/services/patch/content",
    "//components/services/unzip/content",
    "//components/sessions",
    "//components/spellcheck/browser",
    "//components/unified_consent",
    "//components/update_client:patch_impl",
    "//components/update_client:unzip_impl",
    "//content/public/browser",
    "//content/public/common",
    "//extensions/buildflags",
    "//brave/chromium_src:browser",
    "browsing_data",
    "themes",
    "ntp_background_images",
    "//services/network/public/cpp",
    "//services/service_manager/embedder",
    "//third_party/widevine/cdm:buildflags",
    "//ui/base",
  ]

  if (brave_wallet_enabled) {
    deps += [
      "//brave/components/brave_wallet/browser",
      "brave_wallet",
    ]
  }

  if (enable_greaselion) {
    deps += [
      "greaselion",
      "//brave/components/greaselion/browser",
    ]
  }

  if (enable_brave_sync) {
    deps += [
      "//brave/components/brave_sync",
    ]
  }

  if (!is_android) {
    sources += [
      "brave_rewards/checkout_dialog.cc",
      "brave_rewards/checkout_dialog.h",
      "brave_rewards/tip_dialog.cc",
      "brave_rewards/tip_dialog.h",
      "p3a/p3a_core_metrics.cc",
      "p3a/p3a_core_metrics.h",
    ]

    deps += [
      "importer",
      "infobars",
      "//brave/app:brave_generated_resources_grit",
    ]
  } else {
    sources += [
      "android/brave_relaunch_utils.cc",
      "android/brave_shields_content_settings.cc",
      "android/brave_shields_content_settings.h",
      "android/brave_stats_updater_android.cc",
      "android/brave_sync_worker.cc",
      "android/brave_sync_worker.h",
    ]

    if (brave_rewards_enabled) {
      sources += [
        "brave_rewards/android/brave_rewards_native_worker.cc",
        "brave_rewards/android/brave_rewards_native_worker.h",
      ]

      deps += [
        "//brave/vendor/bat-native-ledger",
      ]
    }

    if (brave_ads_enabled) {
      sources += [
        "brave_ads/android/brave_ads_native_helper.cc",
      ]

      deps += [
        "brave_ads/android:jni_headers",
      ]
    }

    deps += [
      "//brave/browser/android/preferences",
      "//brave/build/android:jni_headers",
      "//chrome/android:jni_headers",
    ]
  }

  if (enable_tor) {
    deps += [
      "//brave/browser/tor",
      "//brave/components/services/tor/public/cpp:manifest",
      "//brave/common/tor:pref_names",
    ]
  }

  if (enable_extensions) {
    deps += [ "extensions" ]
  }

  if (brave_ads_enabled) {
    deps += [
      "//brave/components/services/bat_ads/public/cpp:manifest",
      "//components/dom_distiller/core",
    ]
  }

  if (brave_rewards_enabled) {
    deps += [
      "//brave/components/services/bat_ledger/public/cpp:manifest",
    ]
  }

  if (enable_brave_webtorrent) {
    deps += [
      "//brave/components/brave_webtorrent/browser",
    ]
  }

  if (enable_brave_wayback_machine) {
    deps += [
      "//brave/components/brave_wayback_machine",
    ]
  }

  if (enable_widevine) {
    sources += [
      "brave_drm_tab_helper.cc",
      "brave_drm_tab_helper.h",
    ]
    deps += [ "//brave/browser/widevine" ]
  }

  if (enable_brave_perf_predictor) {
    deps += [
      "//brave/components/brave_perf_predictor/browser",
    ]
  }

  if (enable_speedreader) {
    deps += [ "//brave/components/speedreader" ]
  }

  if (is_win && is_official_build) {
    sources += [
      "//chrome/browser/google/google_update_win.cc",
      "//chrome/browser/google/google_update_win.h",
    ]

    deps += [
      "//google_update",
    ]
  }
}

buildflag_header("sparkle_buildflags") {
  header = "sparkle_buildflags.h"
  flags = [
    "ENABLE_SPARKLE=$enable_sparkle",
  ]
}

source_set("version_info") {
  # these are only for version_info.cc and should not be exposed as public configs
  configs += [ "//brave/build:version" ]
  defines = [
    "BRAVE_BROWSER_VERSION=\"$brave_version\"",
  ]

  sources = [
    "version_info.cc",
    "version_info.h",
  ]
}

source_set("browser") {
  visibility = [
    "//chrome/browser/*",
    "//brave/browser/*",
    "//brave:browser_dependencies",
    "//brave/test:*"
  ]

  deps = [
    "//chrome/browser",
  ]
  if (enable_sparkle) {
    deps += [
      "//brave/vendor/sparkle:sparkle_framework_bundle_data"
    ]
  }
}
