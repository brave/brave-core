import("//brave/build/config.gni")
import("//build/config/features.gni")

source_set("browser_process") {
  sources = [
    "alternate_private_search_engine_controller.cc",
    "alternate_private_search_engine_controller.h",
    "alternate_private_search_engine_util.cc",
    "alternate_private_search_engine_util.h",
    "autocomplete/brave_autocomplete_scheme_classifier.cc",
    "autocomplete/brave_autocomplete_scheme_classifier.h",
    "brave_browser_main_extra_parts.cc",
    "brave_browser_main_extra_parts.h",
    "brave_browser_main_parts_mac.mm",
    "brave_browser_main_parts_mac.h",
    "brave_browser_process_impl.cc",
    "brave_browser_process_impl.h",
    "brave_local_state_prefs.cc",
    "brave_local_state_prefs.h",
    "brave_profile_prefs.cc",
    "brave_profile_prefs.h",
    "brave_stats_updater.cc",
    "brave_stats_updater.h",
    "brave_stats_updater_params.cc",
    "brave_stats_updater_params.h",
    "brave_tab_helpers.cc",
    "brave_tab_helpers.h",
    "browser_context_keyed_service_factories.cc",
    "browser_context_keyed_service_factories.h",
    "component_updater/brave_component_installer.cc",
    "component_updater/brave_component_installer.h",
    "component_updater/brave_component_updater_configurator.cc",
    "component_updater/brave_component_updater_configurator.h",
    "importer/brave_external_process_importer_client.cc",
    "importer/brave_external_process_importer_client.h",
    "importer/brave_external_process_importer_host.cc",
    "importer/brave_external_process_importer_host.h",
    "importer/brave_importer_lock_dialog.h",
    "importer/brave_in_process_importer_bridge.cc",
    "importer/brave_in_process_importer_bridge.h",
    "importer/brave_profile_writer.cc",
    "importer/brave_profile_writer.h",
    "importer/brave_profile_lock.cc",
    "importer/brave_profile_lock.h",
    "importer/browser_profile_lock.h",
    "importer/chrome_profile_lock.cc",
    "importer/chrome_profile_lock.h",
    "mac/sparkle_glue.mm",
    "mac/sparkle_glue.h",
    "mac/su_updater.h",
    "profile_creation_monitor.cc",
    "profile_creation_monitor.h",
    "update_util.cc",
    "update_util.h",

  ]

  deps = [
    ":version_info",
    "//brave/browser/payments",
    "//brave/components/brave_shields/browser:brave_shields",
    "//brave/components/content_settings/core/browser",
    "//brave/common",
    "//components/component_updater",
    "//components/safe_browsing/common:safe_browsing_prefs",
    "//components/search_engines",
    "//components/spellcheck/browser",
    "//content/public/browser",
    "//brave/chromium_src:browser",
    "themes",
  ]

  if (is_win && is_official_build) {
    sources += [
      "//chrome/browser/google/google_update_win.cc",
      "//chrome/browser/google/google_update_win.h",
    ]

    deps += [
      "//google_update",
    ]
  }
}

source_set("version_info") {
  # these are only for version_info.cc and should not be exposed as public configs
  configs += [ "//brave/build:version" ]
  defines = [
    "BRAVE_BROWSER_VERSION=\"$brave_version\"",
  ]

  sources = [
    "version_info.cc",
    "version_info.h",
  ]
}

source_set("browser") {
  sources = [
    "brave_content_browser_client.cc",
    "brave_content_browser_client.h",
  ]

  public_deps = [
    "//brave/components/resources:brave_components_resources_grit",
    "//brave/browser/resources:brave_extension_grit",
    "//chrome/browser",
  ]

  deps = [
    "autoplay",
    "//brave/components/brave_shields/browser:brave_shields",
    "extensions",
    "net",
    "permissions",
  ]

  if (is_mac) {
    deps += [
      "//brave/vendor/sparkle:sparkle_framework_bundle_data"
    ]
  }
}
