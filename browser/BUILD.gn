import("//brave/build/config.gni")
import("//brave/components/brave_ads/browser/buildflags/buildflags.gni")
import(
    "//brave/components/brave_perf_predictor/browser/buildflags/buildflags.gni")
import("//brave/components/brave_referrals/buildflags/buildflags.gni")
import("//brave/components/brave_sync/buildflags/buildflags.gni")
import("//brave/components/brave_together/buildflags/buildflags.gni")
import("//brave/components/brave_wallet/buildflags/buildflags.gni")
import("//brave/components/brave_wayback_machine/buildflags/buildflags.gni")
import("//brave/components/brave_webtorrent/browser/buildflags/buildflags.gni")
import("//brave/components/crypto_dot_com/browser/buildflags/buildflags.gni")
import("//brave/components/gemini/browser/buildflags/buildflags.gni")
import("//brave/components/greaselion/browser/buildflags/buildflags.gni")
import("//brave/components/ipfs/buildflags/buildflags.gni")
import("//brave/components/moonpay/browser/buildflags/buildflags.gni")
import("//brave/components/speedreader/buildflags.gni")
import("//brave/components/tor/buildflags/buildflags.gni")
import("//build/buildflag_header.gni")
import("//build/config/features.gni")
import("//chrome/common/features.gni")
import("//components/gcm_driver/config.gni")
import("//extensions/buildflags/buildflags.gni")
import("//third_party/protobuf/proto_library.gni")
import("//third_party/widevine/cdm/widevine.gni")

source_set("browser_process") {
  visibility = [
    "//chrome/browser/*",
    "//brave/browser/*",
    "//brave:browser_dependencies",
    "//brave/components/brave_ads/test:*",
    "//brave/components/brave_rewards/browser:testutil",
    "//brave/components/brave_rewards/test:*",
    "//brave/components/ipfs/test:*",
    "//brave/test:*",
  ]

  sources = [
    "component_updater/brave_component_installer.cc",
    "component_updater/brave_component_installer.h",
    "metrics/metrics_reporting_util.cc",
    "metrics/metrics_reporting_util.h",
    "update_util.cc",
    "update_util.h",
  ]

  if (is_mac) {
    sources += [
      "brave_browser_main_parts_mac.h",
      "brave_browser_main_parts_mac.mm",
    ]
  }

  if (enable_sparkle) {
    sources += [
      "mac/sparkle_glue.h",
      "mac/sparkle_glue.mm",
      "mac/su_updater.h",
    ]
  }

  deps = [
    ":sparkle_buildflags",
    ":version_info",
    "autoplay",
    "brave_stats:stats_updater",
    "browsing_data",
    "brave_stats:stats_updater",
    "browsing_data",
    "content_settings",
    "download",
    "ephemeral_storage",
    "farbling",
    "net",
    "ntp_background_images",
    "profiles",
    "renderer_context_menu",
    "search",
    "themes",
    "ui",
    "//base",
    "//brave/browser/gcm_driver:gcm_driver",
    "//brave/browser/net",
    "//brave/browser/notifications",
    "//brave/browser/translate/buildflags",
    "//brave/browser/ui/bookmark:bookmark",
    "//brave/chromium_src:browser",
    "//brave/common",
    "//brave/common:pref_names",
    "//brave/common:switches",
    "//brave/components/binance/browser/buildflags",
    "//brave/components/brave_component_updater/browser",
    "//brave/components/brave_drm",
    "//brave/components/brave_referrals/buildflags",
    "//brave/components/brave_rewards/common:common",
    "//brave/components/brave_shields/browser",
    "//brave/components/brave_shields/common",
    "//brave/components/brave_sync/buildflags:buildflags",
    "//brave/components/brave_sync",
    "//brave/components/brave_sync:features",
    "//brave/components/brave_sync:network_time_helper",
    "//brave/components/brave_sync:prefs",
    "//brave/components/brave_together/browser",
    "//brave/components/brave_wallet/buildflags",
    "//brave/components/brave_wayback_machine:buildflags",
    "//brave/components/brave_webtorrent/browser/buildflags",
    "//brave/components/content_settings/core/browser",
    "//brave/components/crypto_dot_com/browser/buildflags",
    "//brave/components/gemini/browser/buildflags",
    "//brave/components/greaselion/browser/buildflags",
    "//brave/components/ipfs/buildflags",
    "//brave/components/l10n/browser",
    "//brave/components/moonpay/browser/buildflags",
    "//brave/components/ntp_background_images/browser",
    "//brave/components/ntp_tiles",
    "//brave/components/p3a:buildflags",
    "//brave/components/p3a",
    "//brave/components/resources",
    "//brave/components/speedreader:buildflags",
    "//brave/components/tor/buildflags",
    "//brave/components/weekly_storage",
    "//brave/services/network/public/cpp",
    "//chrome/browser/extensions:extensions",
    "//chrome/browser/ui:ui",
    "//chrome/common",
    "//components/account_id:account_id",
    "//components/autofill/core/common",
    "//components/browsing_data/core",
    "//components/component_updater",
    "//components/content_settings/browser",
    "//components/content_settings/core/browser",
    "//components/content_settings/core/common",
    "//components/crx_file:crx_file",
    "//components/feed/core/shared_prefs:feed_shared_prefs",
    "//components/embedder_support",
    "//components/gcm_driver:gcm_buildflags",
    "//components/gcm_driver:gcm_driver",
    "//components/guest_view/browser:browser",
    "//components/keyed_service/content",
    "//components/metrics",
    "//components/password_manager/core/common",
    "//components/permissions",
    "//components/policy/core/browser",
    "//components/prefs",
    "//components/pref_registry:pref_registry",
    "//components/safe_browsing/core/common:safe_browsing_prefs",
    "//components/search_engines",
    "//components/services/patch/content",
    "//components/services/unzip/content",
    "//components/signin/public/base:base",
    "//components/sessions",
    "//components/sync_device_info",
    "//components/spellcheck/browser",
    "//components/unified_consent",
    "//components/update_client:network_impl",
    "//components/update_client:patch_impl",
    "//components/update_client:unzip_impl",
    "//components/update_client:update_client",
    "//components/web_modal:web_modal",
    "//content/public/browser",
    "//content/public/common",
    "//extensions/buildflags",
    "//services/network/public/cpp",
    "//third_party/blink/public/common",
    "//third_party/blink/public/mojom:mojom_platform_headers",
    "//third_party/widevine/cdm:buildflags",
    "//ui/base",
    "//ui/web_dialogs:web_dialogs",
  ]

  if (brave_wallet_enabled) {
    deps += [
      "//brave/components/brave_wallet",
      "brave_wallet",
    ]
  }

  if (enable_greaselion) {
    deps += [
      "greaselion",
      "//brave/components/greaselion/browser",
    ]
  }

  if (enable_brave_sync) {
    deps += [ "//brave/components/brave_sync" ]
  }

  if (brave_together_enabled) {
    deps += [ "//brave/components/brave_together/browser" ]
  }

  if (moonpay_enabled) {
    deps += [
      "//brave/components/moonpay/browser",
      "//brave/components/moonpay/common",
    ]
  }

  if (enable_tor) {
    deps += [
      "//brave/browser/tor",
      "//brave/components/tor",
    ]
  }

  if (enable_extensions) {
    deps += [
      "extensions",
      "//extensions/common:common_constants",
    ]
  }

  if (enable_brave_referrals) {
    sources += [
      "brave_referrals/brave_referrals_service_factory.cc",
      "brave_referrals/brave_referrals_service_factory.h",
    ]
    deps += [
      "//brave/components/brave_referrals/browser",
      "//brave/components/brave_referrals/common",
      "//brave/components/brave_stats/browser",
    ]
  }

  if (brave_ads_enabled) {
    deps += [
      "//brave/components/brave_ads/browser",
      "//brave/components/brave_user_model/browser",
      "//components/dom_distiller/core",
    ]
  }

  if (brave_rewards_enabled) {
    if (is_android) {
      sources += [
        "brave_rewards/android/brave_rewards_native_worker.cc",
        "brave_rewards/android/brave_rewards_native_worker.h",
      ]
    }

    deps += [
      "//brave/components/brave_rewards/browser",
      "//brave/vendor/bat-native-ledger:headers",
    ]
  }

  if (enable_brave_webtorrent) {
    deps += [ "//brave/components/brave_webtorrent/browser" ]
  }

  if (enable_brave_wayback_machine) {
    deps += [ "//brave/components/brave_wayback_machine" ]
  }

  if (enable_brave_perf_predictor) {
    deps += [ "//brave/components/brave_perf_predictor/browser" ]
  }

  if (is_win && is_official_build) {
    sources += [
      "//chrome/browser/google/google_update_win.cc",
      "//chrome/browser/google/google_update_win.h",
      "//chrome/browser/google/switches.cc",
      "//chrome/browser/google/switches.h",
    ]

    deps += [ "//google_update" ]
  }
}

buildflag_header("sparkle_buildflags") {
  header = "sparkle_buildflags.h"
  flags = [ "ENABLE_SPARKLE=$enable_sparkle" ]
}

source_set("version_info") {
  # these are only for version_info.cc and should not be exposed as public configs
  configs += [ "//brave/build:version" ]
  defines = [ "BRAVE_BROWSER_VERSION=\"$brave_version\"" ]

  sources = [
    "version_info.cc",
    "version_info.h",
  ]
}

source_set("browser") {
  visibility = [
    "//chrome/browser/*",
    "//brave/browser/*",
    "//brave:browser_dependencies",
    "//brave/test:*",
  ]

  deps = [ "//chrome/browser" ]
  if (enable_sparkle) {
    deps += [ "//brave/vendor/sparkle:sparkle_framework_bundle_data" ]
  }
}
