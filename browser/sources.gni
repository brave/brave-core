# Copyright (c) 2020 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//brave/browser/autocomplete/sources.gni")
import("//brave/browser/binance/sources.gni")
import("//brave/browser/brave_adaptive_captcha/sources.gni")
import("//brave/browser/brave_ads/sources.gni")
import("//brave/browser/brave_news/sources.gni")
import("//brave/browser/brave_rewards/sources.gni")
import("//brave/browser/brave_shields/sources.gni")
import("//brave/browser/brave_stats/sources.gni")
import("//brave/browser/brave_vpn/sources.gni")
import("//brave/browser/brave_wallet/android/sources.gni")
import("//brave/browser/browsing_data/sources.gni")
import("//brave/browser/component_updater/sources.gni")
import("//brave/browser/crypto_dot_com/sources.gni")
import("//brave/browser/debounce/sources.gni")
import("//brave/browser/decentralized_dns/sources.gni")
import("//brave/browser/download/sources.gni")
import("//brave/browser/ephemeral_storage/sources.gni")
import("//brave/browser/ethereum_remote_client/buildflags/buildflags.gni")
import("//brave/browser/gemini/sources.gni")
import("//brave/browser/greaselion/sources.gni")
import("//brave/browser/importer/sources.gni")
import("//brave/browser/ipfs/sources.gni")
import("//brave/browser/new_tab/sources.gni")
import("//brave/browser/ntp_background_images/sources.gni")
import("//brave/browser/permissions/sources.gni")
import("//brave/browser/search_engines/sources.gni")
import("//brave/browser/speedreader/sources.gni")
import("//brave/browser/themes/sources.gni")
import("//brave/browser/web_discovery/sources.gni")
import("//brave/build/rust/config.gni")
import("//brave/chromium_src/chrome/browser/prefs/sources.gni")
import("//brave/chromium_src/chrome/browser/sources.gni")
import("//brave/components/brave_referrals/buildflags/buildflags.gni")
import("//brave/components/brave_vpn/buildflags/buildflags.gni")
import("//brave/components/brave_wayback_machine/buildflags/buildflags.gni")
import("//brave/components/brave_webtorrent/browser/buildflags/buildflags.gni")
import("//brave/components/ftx/browser/buildflags/buildflags.gni")
import("//brave/components/sidebar/buildflags/buildflags.gni")
import("//brave/components/tor/buildflags/buildflags.gni")
import("//brave/components/translate/core/common/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

brave_chrome_browser_visibility = [
  "//brave/browser/*",
  "//brave/test:*",

  # these are layering violation and need to be fixed
  "//brave/components/brave_ads/test:*",
  "//brave/components/ipfs/test:*",

  # Chrome does not yet define a visibility list by itself, meaning we can't
  # append our list to them and that what we have here will become law not just
  # for Brave, but also for the rest of chrome. Thus, we need to provide our own
  # permissive list for chrome here too, to be removed in the future if Chrome
  # ever provides it's own list (at which point we'll append instead of set).
  "//chrome:browser_dependencies",
  "//chrome/app/*",
  "//chrome/test/*",
  "//extensions/*",
]

brave_chrome_browser_sources = [
  "//brave/browser/brave_browser_main_extra_parts.cc",
  "//brave/browser/brave_browser_main_extra_parts.h",
  "//brave/browser/brave_browser_main_parts.cc",
  "//brave/browser/brave_browser_main_parts.h",
  "//brave/browser/brave_browser_process.cc",
  "//brave/browser/brave_browser_process_impl.cc",
  "//brave/browser/brave_browser_process_impl.h",
  "//brave/browser/brave_content_browser_client.cc",
  "//brave/browser/brave_content_browser_client.h",
  "//brave/browser/brave_features_interal_names.h",
  "//brave/browser/brave_local_state_prefs.cc",
  "//brave/browser/brave_local_state_prefs.h",
  "//brave/browser/brave_profile_prefs.cc",
  "//brave/browser/brave_profile_prefs.h",
  "//brave/browser/brave_shell_integration.cc",
  "//brave/browser/brave_shell_integration.h",
  "//brave/browser/brave_tab_helpers.cc",
  "//brave/browser/brave_tab_helpers.h",
  "//brave/browser/browser_context_keyed_service_factories.cc",
  "//brave/browser/browser_context_keyed_service_factories.h",
  "//brave/browser/geolocation/brave_geolocation_permission_context_delegate.cc",
  "//brave/browser/geolocation/brave_geolocation_permission_context_delegate.h",
  "//brave/browser/metrics/brave_metrics_service_accessor.cc",
  "//brave/browser/metrics/brave_metrics_service_accessor.h",
  "//brave/browser/metrics/metrics_reporting_util.cc",
  "//brave/browser/metrics/metrics_reporting_util.h",
  "//brave/browser/update_util.cc",
  "//brave/browser/update_util.h",
]

brave_chrome_browser_deps = [
  "//base",
  "//brave/app:brave_generated_resources_grit",
  "//brave/browser:browser_process",
  "//brave/browser/brave_federated",
  "//brave/browser/brave_wallet",
  "//brave/browser/brave_wallet:brave_wallet_delegate",
  "//brave/browser/brave_wallet:tab_helper",
  "//brave/browser/component_updater",
  "//brave/browser/content_settings",
  "//brave/browser/gcm_driver",
  "//brave/browser/metrics/buildflags",
  "//brave/browser/net",
  "//brave/browser/profiles",
  "//brave/browser/renderer_context_menu",
  "//brave/browser/search",
  "//brave/browser/skus",
  "//brave/browser/sync",
  "//brave/browser/themes",
  "//brave/browser/ui",
  "//brave/common",
  "//brave/common:pref_names",
  "//brave/common:switches",
  "//brave/components/binance/browser/buildflags",
  "//brave/components/brave_adaptive_captcha/buildflags",
  "//brave/components/brave_ads/browser",
  "//brave/components/brave_ads/common",
  "//brave/components/brave_federated",
  "//brave/components/brave_perf_predictor/browser",
  "//brave/components/brave_referrals/buildflags",
  "//brave/components/brave_rewards/common/buildflags",
  "//brave/components/brave_search/browser",
  "//brave/components/brave_search/common",
  "//brave/components/brave_search/common:mojom",
  "//brave/components/brave_shields/common",
  "//brave/components/brave_sync",
  "//brave/components/brave_sync:features",
  "//brave/components/brave_sync:network_time_helper",
  "//brave/components/brave_sync:prefs",
  "//brave/components/brave_vpn/buildflags",
  "//brave/components/brave_wallet/browser",
  "//brave/components/brave_wallet/common",
  "//brave/components/brave_wallet/common:mojom",
  "//brave/components/brave_wayback_machine:buildflags",
  "//brave/components/brave_webtorrent/browser/buildflags",
  "//brave/components/cosmetic_filters/browser",
  "//brave/components/cosmetic_filters/common:mojom",
  "//brave/components/crypto_dot_com/browser/buildflags",
  "//brave/components/de_amp/browser",
  "//brave/components/de_amp/common",
  "//brave/components/decentralized_dns/buildflags",
  "//brave/components/gemini/browser/buildflags",
  "//brave/components/greaselion/browser/buildflags",
  "//brave/components/ipfs/buildflags",
  "//brave/components/l10n/browser",
  "//brave/components/l10n/common",
  "//brave/components/ntp_background_images/browser",
  "//brave/components/ntp_background_images/buildflags",
  "//brave/components/ntp_tiles",
  "//brave/components/p3a",
  "//brave/components/p3a:buildflags",
  "//brave/components/privacy_sandbox",
  "//brave/components/resources",
  "//brave/components/sidebar/buildflags",
  "//brave/components/skus/browser",
  "//brave/components/skus/common",
  "//brave/components/speedreader:buildflags",
  "//brave/components/tor/buildflags",
  "//brave/components/translate/core/common:buildflags",
  "//brave/components/version_info",
  "//brave/services/network/public/cpp",
  "//chrome/browser:browser_process",
  "//chrome/browser/profiles:profile",
  "//chrome/browser/ui",
  "//chrome/common",
  "//chrome/common:channel_info",
  "//components/account_id",
  "//components/autofill/core/common",
  "//components/browsing_data/core",
  "//components/content_settings/browser",
  "//components/content_settings/core/browser",
  "//components/content_settings/core/common",
  "//components/dom_distiller/core",
  "//components/embedder_support",
  "//components/feed/core/shared_prefs:feed_shared_prefs",
  "//components/gcm_driver:gcm_buildflags",
  "//components/metrics",
  "//components/ntp_tiles",
  "//components/password_manager/core/common",
  "//components/permissions",
  "//components/policy/core/common:common_constants",
  "//components/pref_registry",
  "//components/prefs",
  "//components/privacy_sandbox:privacy_sandbox_prefs",
  "//components/safe_browsing/core/common:safe_browsing_prefs",
  "//components/search_engines",
  "//components/services/heap_profiling/public/mojom",
  "//components/signin/public/base",
  "//components/sync/base",
  "//components/sync/driver",
  "//components/translate/core/browser:translate_pref_names",
  "//components/version_info",
  "//components/version_info:channel",
  "//content/public/browser",
  "//content/public/common",
  "//extensions/buildflags",
  "//mojo/public/cpp/bindings",
  "//net",
  "//services/metrics/public/cpp:metrics_cpp",
  "//services/network/public/cpp",
  "//third_party/blink/public/mojom:mojom_platform_headers",
  "//third_party/widevine/cdm:buildflags",
  "//ui/base",
  "//url",
]

if (is_mac) {
  brave_chrome_browser_sources += [
    "//brave/browser/brave_browser_main_parts_mac.h",
    "//brave/browser/brave_browser_main_parts_mac.mm",
  ]
}

if (enable_sparkle) {
  brave_chrome_browser_sources += [
    "//brave/browser/mac/sparkle_glue.h",
    "//brave/browser/mac/sparkle_glue.mm",
    "//brave/browser/mac/su_updater.h",
  ]
}

if (enable_brave_vpn) {
  brave_chrome_browser_deps += [ "//brave/components/brave_vpn" ]
}

if (ethereum_remote_client_enabled) {
  brave_chrome_browser_deps +=
      [ "//brave/browser/ethereum_remote_client:browser" ]
}

if (is_android) {
  brave_chrome_browser_sources += [
    "//brave/browser/android/brave_stats_updater_android.cc",
    "//brave/browser/sync/brave_sync_devices_android.cc",
    "//brave/browser/sync/brave_sync_devices_android.h",
  ]

  brave_chrome_browser_sources += brave_browser_brave_wallet_android_sources

  brave_chrome_browser_deps += [
    "//brave/browser/android:android_browser_process",
    "//brave/build/android:jni_headers",
    "//chrome/android:jni_headers",
    "//components/sync_device_info",
  ]
} else {
  brave_chrome_browser_sources += [
    "//brave/browser/p3a/p3a_core_metrics.cc",
    "//brave/browser/p3a/p3a_core_metrics.h",
  ]

  brave_chrome_browser_deps += [
    "//brave/app:brave_generated_resources_grit",
    "//brave/browser/importer",
    "//brave/browser/infobars",
    "//brave/browser/ui/bookmark",
    "//brave/components/weekly_storage",
    "//components/infobars/content",
  ]
}

if (brave_adaptive_captcha_enabled) {
  brave_chrome_browser_deps += [ "//brave/browser/brave_ads/tooltips" ]
}

if (enable_sidebar) {
  brave_chrome_browser_deps += [ "//brave/components/sidebar" ]
}

if (enable_tor) {
  brave_chrome_browser_deps += [
    "//brave/browser/tor",
    "//brave/components/tor",
    "//brave/components/tor:pref_names",
    "//brave/components/tor:utils",
  ]
}

if (enable_extensions) {
  brave_chrome_browser_deps += [
    "//brave/browser/extensions",
    "//chrome/browser/extensions",
    "//extensions/browser",
    "//extensions/common:common_constants",
  ]

  if (enable_brave_webtorrent) {
    brave_chrome_browser_deps +=
        [ "//brave/components/brave_webtorrent/browser" ]
  }
}

if (enable_brave_referrals) {
  brave_chrome_browser_deps += [ "//brave/components/brave_referrals/browser" ]
}

if (enable_brave_wayback_machine) {
  brave_chrome_browser_deps += [ "//brave/components/brave_wayback_machine" ]
}

if (enable_widevine) {
  brave_chrome_browser_sources += [
    "//brave/browser/brave_drm_tab_helper.cc",
    "//brave/browser/brave_drm_tab_helper.h",
  ]
  brave_chrome_browser_deps += [
    "//brave/browser/widevine",
    "//brave/browser/widevine:constants",
    "//brave/components/brave_drm",
  ]
}

if (enable_ftx) {
  brave_chrome_browser_deps += [ "//brave/browser/ftx" ]
}

if (enable_brave_translate_go || enable_brave_translate_extension) {
  brave_chrome_browser_sources += [
    "//brave/browser/translate/brave_translate_utils.cc",
    "//brave/browser/translate/brave_translate_utils.h",
  ]
}

if (is_win) {
  brave_chrome_browser_sources += [
    "//brave/browser/default_protocol_handler_utils_win.cc",
    "//brave/browser/default_protocol_handler_utils_win.h",
  ]
  brave_chrome_browser_deps += [
    "//chrome/install_static:install_static_util",
    "//chrome/installer/util:with_no_strings",
  ]
}

if (is_win && is_official_build) {
  brave_chrome_browser_sources += [
    "//chrome/browser/google/google_update_win.cc",
    "//chrome/browser/google/google_update_win.h",
    "//chrome/browser/google/switches.cc",
    "//chrome/browser/google/switches.h",
  ]

  brave_chrome_browser_deps += [ "//google_update" ]
}

brave_chrome_browser_public_deps = [
  "//brave/browser:browser_process",
  "//brave/components/brave_sync:constants",
  "//brave/components/variations:constants",
]

if (is_mac) {
  brave_chrome_framework_public_deps = [ "//brave:framework_bundle_data" ]
  brave_chrome_framework_bundle_deps = [ "//brave:framework_bundle_data" ]
  brave_chrome_framework_contents = [ "Frameworks" ]
  brave_chrome_framework_ldflags = []

  # this needs to match the guards on //chrome:verify_chrome_framework_order
  if (!is_component_build && chrome_pgo_phase != 1) {
    brave_chrome_framework_public_deps += [
      # TODO(bridiver) - this doesn't have to be a public dep
      "//brave/build/rust:generate_unexport_list",
    ]
    brave_chrome_framework_ldflags += [
      # prevent export of all rust symbols
      "-Wl,-unexported_symbols_list," +
          rebase_path(unexport_list_path, root_build_dir),
    ]
  }
  if (!is_component_build && !use_lld) {
    brave_chrome_framework_ldflags += [
      # https://bugzilla.mozilla.org/show_bug.cgi?id=1188030#c14
      # Supposedly this bug was fixed, but it's still happening for us
      "-Wl,-no_compact_unwind",
    ]
  }
}

brave_chrome_browser_defines = []

brave_chrome_browser_sources += brave_browser_autocomplete_sources
brave_chrome_browser_sources += brave_browser_binance_sources
brave_chrome_browser_sources += brave_browser_brave_adaptive_captcha_sources
brave_chrome_browser_sources += brave_browser_brave_ads_sources
brave_chrome_browser_sources += brave_browser_brave_news_sources
brave_chrome_browser_sources += brave_browser_brave_vpn_sources
brave_chrome_browser_sources += brave_browser_brave_rewards_sources
brave_chrome_browser_sources += brave_browser_brave_shields_sources
brave_chrome_browser_sources += brave_browser_brave_stats_updater_sources
brave_chrome_browser_sources += brave_browser_browsing_data_sources
brave_chrome_browser_sources += brave_browser_component_updater_sources
brave_chrome_browser_sources += brave_browser_crypto_dot_com_sources
brave_chrome_browser_sources += brave_browser_debounce_sources
brave_chrome_browser_sources += brave_browser_decentralized_dns_sources
brave_chrome_browser_sources += brave_browser_download_sources
brave_chrome_browser_sources += brave_browser_ephemeral_storage_sources
brave_chrome_browser_sources += brave_browser_gemini_sources
brave_chrome_browser_sources += brave_browser_greaselion_sources
brave_chrome_browser_sources += brave_browser_importer_sources
brave_chrome_browser_sources += brave_browser_ipfs_sources
brave_chrome_browser_sources += brave_browser_new_tab_sources
brave_chrome_browser_sources += brave_browser_ntp_background_images_sources
brave_chrome_browser_sources += brave_browser_permissions_sources
brave_chrome_browser_sources += brave_browser_search_engines_sources
brave_chrome_browser_sources += brave_browser_speedreader_sources
brave_chrome_browser_sources += brave_browser_themes_sources
brave_chrome_browser_sources += brave_browser_web_discovery_sources

brave_chrome_browser_deps += brave_browser_autocomplete_deps
brave_chrome_browser_deps += brave_browser_binance_deps
brave_chrome_browser_deps += brave_browser_brave_adaptive_captcha_deps
brave_chrome_browser_deps += brave_browser_brave_ads_deps
brave_chrome_browser_deps += brave_browser_brave_news_deps
brave_chrome_browser_deps += brave_browser_brave_vpn_deps
brave_chrome_browser_deps += brave_browser_brave_rewards_deps
brave_chrome_browser_deps += brave_browser_brave_shields_deps
brave_chrome_browser_deps += brave_browser_brave_stats_updater_deps
brave_chrome_browser_deps += brave_browser_browsing_data_deps
brave_chrome_browser_deps += brave_browser_component_updater_deps
brave_chrome_browser_deps += brave_browser_crypto_dot_com_deps
brave_chrome_browser_deps += brave_browser_debounce_deps
brave_chrome_browser_deps += brave_browser_decentralized_dns_deps
brave_chrome_browser_deps += brave_browser_download_deps
brave_chrome_browser_deps += brave_browser_ephemeral_storage_deps
brave_chrome_browser_deps += brave_browser_gemini_deps
brave_chrome_browser_deps += brave_browser_greaselion_deps
brave_chrome_browser_deps += brave_browser_importer_deps
brave_chrome_browser_deps += brave_browser_ipfs_deps
brave_chrome_browser_deps += brave_browser_new_tab_deps
brave_chrome_browser_deps += brave_browser_ntp_background_images_deps
brave_chrome_browser_deps += brave_browser_permissions_deps
brave_chrome_browser_deps += brave_browser_search_engines_deps
brave_chrome_browser_deps += brave_browser_speedreader_deps
brave_chrome_browser_deps += brave_browser_themes_deps
brave_chrome_browser_deps += brave_browser_web_discovery_deps
brave_chrome_browser_deps += brave_chromium_src_chrome_browser_deps
brave_chrome_browser_deps += brave_chromium_src_chrome_browser_prefs_deps

brave_chrome_browser_defines += brave_browser_brave_stats_updater_defines
