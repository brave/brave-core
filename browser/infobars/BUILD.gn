# Copyright (c) 2022 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//brave/components/brave_wayback_machine/buildflags/buildflags.gni")
import("//brave/components/ipfs/buildflags/buildflags.gni")
import("//build/config/features.gni")

source_set("infobars") {
  # Remove when https://github.com/brave/brave-browser/issues/10649 is resolved
  check_includes = false
  sources = [
    "brave_confirm_p3a_infobar_delegate.cc",
    "brave_confirm_p3a_infobar_delegate.h",
    "brave_sync_account_deleted_infobar_delegate.cc",
    "brave_sync_account_deleted_infobar_delegate.h",
    "sync_cannot_run_infobar_delegate.cc",
    "sync_cannot_run_infobar_delegate.h",
    "sync_v2_migrate_infobar_delegate.cc",
    "sync_v2_migrate_infobar_delegate.h",
  ]

  deps = [
    "//base",
    "//brave/app:brave_generated_resources_grit",
    "//brave/browser/ui",
    "//brave/common:common",
    "//brave/components/brave_sync:features",
    "//brave/components/brave_sync:prefs",
    "//brave/components/p3a",
    "//chrome/app:chromium_strings",
    "//chrome/browser/ui",
    "//components/infobars/content",
    "//components/infobars/core",
    "//components/prefs",
    "//components/strings",
    "//ui/base",
    "//ui/views",
    "//url",
  ]

  if (enable_ipfs) {
    deps += [
      "//brave/components/ipfs",
      "//components/user_prefs",
    ]
  }

  if (enable_brave_wayback_machine) {
    sources += [
      "brave_wayback_machine_delegate_impl.cc",
      "brave_wayback_machine_delegate_impl.h",
    ]

    deps += [
      "//brave/components/brave_wayback_machine",
      "//brave/components/constants",
    ]
  }
}
