import("//brave/browser/ethereum_remote_client/buildflags/buildflags.gni")
import("//brave/components/brave_sync/buildflags/buildflags.gni")
import("//brave/components/brave_wayback_machine/buildflags/buildflags.gni")
import("//brave/components/ipfs/buildflags/buildflags.gni")
import("//build/config/features.gni")

source_set("infobars") {
  # Remove when https://github.com/brave/brave-browser/issues/10649 is resolved
  check_includes = false
  sources = [
    "brave_confirm_p3a_infobar_delegate.cc",
    "brave_confirm_p3a_infobar_delegate.h",
  ]

  deps = [
    "//base",
    "//brave/app:brave_generated_resources_grit",
    "//brave/browser/ui",
    "//brave/components/p3a",
    "//chrome/app:chromium_strings",
    "//chrome/browser/ui",
    "//components/infobars/core",
    "//components/prefs",
    "//components/strings",
    "//ui/base",
    "//ui/views",
    "//url",
  ]

  if (ethereum_remote_client_enabled) {
    sources += [
      "crypto_wallets_infobar_delegate.cc",
      "crypto_wallets_infobar_delegate.h",
    ]

    deps += [
      "//brave/browser/ethereum_remote_client",
      "//brave/common:pref_names",
      "//components/user_prefs:user_prefs",
      "//extensions/common:common_constants",
    ]
  }

  if (ipfs_enabled) {
    deps += [
      "//brave/components/ipfs",
      "//components/user_prefs",
    ]
  }

  if (enable_brave_wayback_machine) {
    sources += [
      "brave_wayback_machine_delegate_impl.cc",
      "brave_wayback_machine_delegate_impl.h",
    ]

    deps += [
      "//brave/common:switches",
      "//brave/components/brave_wayback_machine",
    ]
  }

  if (enable_brave_sync) {
    sources += [
      "sync_v2_migrate_infobar_delegate.cc",
      "sync_v2_migrate_infobar_delegate.h",
    ]

    deps += [
      "//brave/common:common",
      "//brave/components/brave_sync:features",
      "//brave/components/brave_sync:prefs",
    ]
  }
}
