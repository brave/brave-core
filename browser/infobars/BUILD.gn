import("//build/config/features.gni")
import("//brave/components/brave_wayback_machine/buildflags/buildflags.gni")
import("//brave/components/brave_wallet/browser/buildflags/buildflags.gni")
import("//brave/components/brave_sync/buildflags/buildflags.gni")
import("//brave/components/ipfs/browser/buildflags/buildflags.gni")

source_set("infobars") {
  # Remove when https://github.com/brave/brave-browser/issues/10649 is resolved
  check_includes = false
  sources = [
    "brave_confirm_p3a_infobar_delegate.cc",
    "brave_confirm_p3a_infobar_delegate.h",
  ]

  deps = [
    "//base",
    "//brave/browser/ui",
    "//brave/components/p3a",
    "//brave/app:brave_generated_resources_grit",
    "//chrome/app:chromium_strings",
    "//chrome/browser/ui",
    "//components/infobars/core",
    "//components/prefs",
    "//components/strings",
    "//ui/base",
    "//ui/views",
    "//url",
  ]

  if (brave_wallet_enabled) {
    sources += [
      "crypto_wallets_infobar_delegate.cc",
      "crypto_wallets_infobar_delegate.h",
    ]

    deps += [
      "//brave/browser/brave_wallet:brave_wallet",
      "//brave/common:pref_names",
      "//brave/components/brave_wallet/browser:browser",
      "//components/user_prefs:user_prefs",
      "//extensions/common:common_constants",
    ]
  }

  if (ipfs_enabled) {
    sources += [
      "ipfs_infobar_delegate.cc",
      "ipfs_infobar_delegate.h",
    ]
    deps += [
      "//brave/components/ipfs/browser",
      "//brave/components/ipfs/common",
      "//components/user_prefs"
    ]
  }

  if (enable_brave_wayback_machine) {
    sources += [
      "brave_wayback_machine_delegate_impl.cc",
      "brave_wayback_machine_delegate_impl.h",
    ]

    deps += [
      "//brave/components/brave_wayback_machine",
      "//brave/common:switches",
    ]
  }

  if (enable_brave_sync) {
    sources += [
      "sync_v2_migrate_infobar_delegate.cc",
      "sync_v2_migrate_infobar_delegate.h",
    ]

    deps += [
      "//brave/common:common",
      "//brave/components/brave_sync:features",
      "//brave/components/brave_sync:prefs",
    ]
  }
}
