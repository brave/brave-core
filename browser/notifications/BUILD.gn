import("//chrome/common/features.gni")
if (is_android) {
  import("//build/config/android/rules.gni")
}

source_set("notifications") {
  # Remove when https://github.com/brave/brave-browser/issues/10657 is resolved
  check_includes = false

  sources = [
    "ads_notification_handler.cc",
    "ads_notification_handler.h",
  ]

  deps = [
    "//base",
    "//brave/components/brave_adaptive_captcha/buildflags",
    "//chrome/browser/notifications",
    "//third_party/abseil-cpp:absl",
    "//url",
  ]

  if (is_android) {
    sources += [
      "brave_notification_platform_bridge_helper_android.cc",
      "brave_notification_platform_bridge_helper_android.h",
    ]
  }
}

if (is_android) {
  android_library("java") {
    sources = [
      "android/java/src/org/chromium/chrome/browser/notifications/BraveAdsNotificationBuilder.java",
      "android/java/src/org/chromium/chrome/browser/notifications/BraveNotificationManagerProxyImpl.java",
    ]

    deps = [
      ":brave_java",
      ":java_resources",
      "//base:base_java",
      "//chrome/android:chrome_base_module_resources",
      "//chrome/browser/notifications:java",
      "//chrome/browser/notifications:java_resources",
      "//components/browser_ui/notifications/android:java",
      "//third_party/androidx:androidx_annotation_annotation_java",
      "//ui/android:ui_no_recycler_view_java",
    ]

    resources_package = "org.chromium.chrome.browser.notifications"
  }

  # TODO(samartnik): we are unable to avoid this patch since we directly patch
  # `ChromeChannelDefinitions.java`. There is no straightforward conversion of
  # those patches to bytecode patching so we need to find a way to
  # workaround those patches https://github.com/brave/brave-browser/issues/15867.
  android_library("brave_java") {
    sources = [ "android/java/src/org/chromium/chrome/browser/notifications/channels/BraveChannelDefinitions.java" ]

    deps = [
      ":java_resources",
      "//components/browser_ui/notifications/android:java",
      "//third_party/androidx:androidx_annotation_annotation_java",
    ]

    resources_package = "org.chromium.chrome.browser.notifications"
  }

  android_resources("java_resources") {
    sources = [
      "android/java/res/layout/web_notification_big_brave_ads.xml",
      "android/java/res/layout/web_notification_brave_ads.xml",
    ]

    deps = [ "//brave/build/android:android_brave_strings_grd" ]
  }
}
