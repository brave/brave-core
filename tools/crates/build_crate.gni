# Copyright (c) 2025 The Brave Authors. All rights reserved.
# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at https://mozilla.org/MPL/2.0/.

import("//build/config/rust.gni")

template("build_crate") {
  crate = target_name
  assert(
      defined(invoker["${crate}_exe"]),
      "Please add ${crate}_exe to //brave/tools/crates/config.gni (or to $crate itself)!")
  crate_exe = invoker["${crate}_exe"]
  crate_target_dir = get_path_info(get_path_info(crate_exe, "dir"), "dir")

  action("build_$crate") {
    assert(current_toolchain == host_toolchain)

    forward_variables_from(invoker, [ "deps" ])

    script = "//brave/script/build_crate.py"

    rust_path = "$rust_sysroot/bin"
    cargo_exe = "$rust_path/cargo"
    if (host_os == "win") {
      cargo_exe += ".exe"
    }

    inputs = [
      cargo_exe,
      ".cargo/config.toml",
      "Cargo.lock",
      "Cargo.toml",
    ]

    outputs = [
      crate_exe,
      "$crate_target_dir/.stamp",
    ]

    # To avoid irreproducible builds,
    # `build_crate`s MUST always build with `cargo vendor`ed deps,
    # hence they always have to pass:
    #   --config .cargo/config.toml
    #   --frozen (--locked + --offline)
    # when running `cargo build`.
    args = [
      "--temp_dir_path",
      rebase_path(crate_target_dir),
      "--rust_path",
      rebase_path(rust_path),
      rebase_path(cargo_exe),
      "build",
      "--quiet",
      "--release",
      "--manifest-path",
      rebase_path("Cargo.toml"),
      "--config",
      rebase_path(".cargo/config.toml"),
      "--target-dir",
      rebase_path(crate_target_dir),
      "--frozen",
      "--package",
      string_replace(crate, "_", "-"),
    ]
  }
}
