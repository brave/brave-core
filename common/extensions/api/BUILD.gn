import("//brave/browser/ethereum_remote_client/buildflags/buildflags.gni")
import("//brave/components/binance/browser/buildflags/buildflags.gni")
import("//brave/components/crypto_dot_com/browser/buildflags/buildflags.gni")
import("//brave/components/ftx/browser/buildflags/buildflags.gni")
import("//brave/components/gemini/browser/buildflags/buildflags.gni")
import("//brave/components/ipfs/buildflags/buildflags.gni")
import("//tools/grit/grit_rule.gni")
import("//tools/json_schema_compiler/json_features.gni")
import("//tools/json_schema_compiler/json_schema_api.gni")
json_features("api_features") {
  feature_type = "APIFeature"
  method_name = "AddBraveAPIFeatures"
  sources = [
    "//chrome/common/extensions/api/_api_features.json",
    "//extensions/common/api/_api_features.json",
    "_api_features.json",
    "_brave_talk_api_features.json",
  ]

  if (ethereum_remote_client_enabled) {
    sources += [ "_brave_wallet_api_features.json" ]
  }

  if (enable_ipfs) {
    sources += [ "_ipfs_api_features.json" ]
  }

  if (binance_enabled) {
    sources += [ "_binance_api_features.json" ]
  }

  if (gemini_enabled) {
    sources += [ "_gemini_api_features.json" ]
  }

  if (enable_ftx) {
    sources += [ "_ftx_api_features.json" ]
  }

  if (crypto_dot_com_enabled) {
    sources += [ "_crypto_dot_com_features.json" ]
  }
}

json_features("permission_features") {
  feature_type = "PermissionFeature"
  method_name = "AddBravePermissionFeatures"
  sources = [
    "//chrome/common/extensions/api/_permission_features.json",
    "//extensions/common/api/_permission_features.json",
    "_permission_features.json",
  ]
}

json_features("manifest_features") {
  feature_type = "ManifestFeature"
  method_name = "AddBraveManifestFeatures"
  sources = [
    "//chrome/common/extensions/api/_manifest_features.json",
    "//extensions/common/api/_manifest_features.json",
    "_manifest_features.json",
  ]
}

json_features("behavior_features") {
  feature_type = "BehaviorFeature"
  method_name = "AddBraveBehaviorFeatures"
  sources = [ "//extensions/common/api/_behavior_features.json" ]
}

brave_extensions_api_schema_sources = [
  "brave_rewards.json",
  "brave_shields.json",
  "brave_talk.json",
  "brave_theme.json",
  "greaselion.json",
  "rewards_notifications.json",
]

if (enable_ipfs) {
  brave_extensions_api_schema_sources += [ "ipfs.json" ]
}

if (ethereum_remote_client_enabled) {
  brave_extensions_api_schema_sources += [ "brave_wallet.json" ]
}

if (binance_enabled) {
  brave_extensions_api_schema_sources += [ "binance.json" ]
}

if (gemini_enabled) {
  brave_extensions_api_schema_sources += [ "gemini.json" ]
}

if (enable_ftx) {
  brave_extensions_api_schema_sources += [ "ftx.json" ]
}

if (crypto_dot_com_enabled) {
  brave_extensions_api_schema_sources += [ "crypto_dot_com.json" ]
}

brave_extensions_api_uncompiled_sources = []
brave_extensions_api_root_namespace = "extensions::api::%(namespace)s"
brave_extensions_api_schema_include_rules =
    "//brave/common/extensions/api:extensions::api::%(namespace)s"

brave_extensions_api_deps = [
  ":api_features",
  ":behavior_features",
  ":manifest_features",
  ":permission_features",
  ":resources",
  "//chrome/common/extensions/api",
  "//extensions/common/api",
  "//skia",
]

grit("resources") {
  source = "brave_api_resources.grd"
  outputs = [
    "grit/brave_api_resources.h",
    "brave_api_resources.pak",
  ]
  resource_ids = "//brave/browser/resources/resource_ids"
}

function_registration("generated_api_registration") {
  sources = brave_extensions_api_schema_sources
  impl_dir = "//brave/common/extensions/api/"
  bundle_name = "Brave"
  root_namespace = brave_extensions_api_root_namespace

  deps = [ "//extensions/common" ]

  visibility = [ ":api" ]
}

generated_json_strings("generated_api_json_strings") {
  sources = brave_extensions_api_schema_sources +
            brave_extensions_api_uncompiled_sources

  configs = [ "//build/config:precompiled_headers" ]
  bundle_name = "Brave"
  schema_include_rules = brave_extensions_api_schema_include_rules

  root_namespace = brave_extensions_api_root_namespace
  deps = brave_extensions_api_deps

  visibility = [ ":api" ]
}

generated_types("generated_api_types") {
  sources = brave_extensions_api_schema_sources
  configs = [ "//build/config:precompiled_headers" ]
  schema_include_rules = brave_extensions_api_schema_include_rules

  root_namespace = brave_extensions_api_root_namespace
  deps = brave_extensions_api_deps
  visibility = [ ":api" ]
}

# Public Targets

group("api") {
  public_deps = [
    ":generated_api_json_strings",
    ":generated_api_registration",
    ":generated_api_types",
  ]
}
