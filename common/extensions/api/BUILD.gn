import("//tools/json_schema_compiler/json_features.gni")
import("//tools/json_schema_compiler/json_schema_api.gni")
import("//tools/grit/grit_rule.gni")
json_features("api_features") {
  feature_type = "APIFeature"
  method_name = "AddBraveAPIFeatures"
  sources = [
    "//chrome/common/extensions/api/_api_features.json",
    "//extensions/common/api/_api_features.json",
    "_api_features.json",
  ]
}

json_features("permission_features") {
  feature_type = "PermissionFeature"
  method_name = "AddBravePermissionFeatures"
  sources = [
    "//chrome/common/extensions/api/_permission_features.json",
    "//extensions/common/api/_permission_features.json",
  ]
}

json_features("manifest_features") {
  feature_type = "ManifestFeature"
  method_name = "AddBraveManifestFeatures"
  sources = [
    "//chrome/common/extensions/api/_manifest_features.json",
    "//extensions/common/api/_manifest_features.json",
  ]
}

json_features("behavior_features") {
  feature_type = "BehaviorFeature"
  method_name = "AddBraveBehaviorFeatures"
  sources = [
    "//extensions/common/api/_behavior_features.json",
  ]
}

brave_extensions_api_schema_sources = [ "brave_shields.json" ]
brave_extensions_api_root_namespace = "extensions::api::%(namespace)s"
brave_extensions_api_schema_include_rules =
    "//brave/common/extensions/api:extensions::api::%(namespace)s"

brave_extensions_api_deps = [
  "//chrome/common/extensions/api",
  "//extensions/common/api",
  "//skia",
  ":api_features",
  ":permission_features",
  ":manifest_features",
  ":behavior_features",
  ":resources",
]

grit("resources") {
  source = "brave_api_resources.grd"
  outputs = [
    "grit/brave_api_resources.h",
    "brave_api_resources.pak",
  ]
  resource_ids = "//brave/browser/resources/resource_ids"
}

json_schema_api("generated_api_bundles") {
  sources = brave_extensions_api_schema_sources
  deps = brave_extensions_api_deps
  visibility = [ ":api" ]
  root_namespace = brave_extensions_api_root_namespace
  bundle = true
  bundle_registration = true
  bundle_name = "Brave"
  impl_dir = "//brave/common/extensions/api"
  schema_include_rules = brave_extensions_api_schema_include_rules
}

generated_types("generated_api_types") {
  sources = brave_extensions_api_schema_sources
  deps = brave_extensions_api_deps
  visibility = [ ":api" ]
  configs = [ "//build/config:precompiled_headers" ]
  root_namespace = brave_extensions_api_root_namespace
  schema_include_rules = brave_extensions_api_schema_include_rules
}

# Public Targets

group("api") {
  public_deps = [
    ":generated_api_bundles",
    ":generated_api_types",
  ]
}
