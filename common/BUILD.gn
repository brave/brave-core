import("//build/util/branding.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("channel_info") {
  if (is_linux) {
    sources = [
      "brave_channel_info_posix.cc",
      "brave_channel_info_posix.h",
    ]

    deps = [
      "//base:base",
      "//base:logging_buildflags",
      "//components/version_info:version_info",
    ]

    configs += [
      "//brave/build/linux:linux_channel_names",
    ]
  }
}

source_set("network_constants") {
  sources = [
    "network_constants.cc",
    "network_constants.h",
  ]
}

source_set("pref_names") {
  sources = [
    "pref_names.cc",
    "pref_names.h",
  ]

  deps = [ "//components/gcm_driver:gcm_buildflags" ]
}

source_set("switches") {
  sources = [
    "brave_switches.cc",
    "brave_switches.h",
  ]

  deps = [
    "//base",
  ]
}

source_set("common") {
  deps = []
  sources = []

  if (!is_android && !is_ios) {
    deps += [
      "//brave/common/importer",
    ]
  }

  if (enable_extensions) {
    sources += [
      "extensions/brave_extensions_api_provider.cc",
      "extensions/brave_extensions_api_provider.h",
      "extensions/whitelist.cc",
      "extensions/whitelist.h",
    ]

    deps += [
      "//brave/components/brave_wallet/buildflags:buildflags",
      "//extensions/common:common_constants",
    ]

    public_deps = [
      "//brave/components/brave_component_updater/browser",
      "//brave/common/extensions/api",
      "//extensions/common:common"
    ]
  }

  if (!is_ios) {
    deps += [
      "//chrome/common:channel_info",
      "//chrome/common:constants",
      "//content/public/common",
    ]
  }
}

source_set("shield_exceptions") {
  # Remove when https://github.com/brave/brave-browser/issues/10653 is resolved
  check_includes = false
  sources = [
    "shield_exceptions.cc",
    "shield_exceptions.h",
  ]

  deps = [
    "//brave/extensions:common",
    "//url",
  ]
}

config("constants_configs") {
  defines = []
  if (is_mac) {
    defines += [
      "BRAVE_PRODUCT_STRING=\"$chrome_product_full_name\"",
    ]
  }
}
