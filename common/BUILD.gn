import("//build/util/branding.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("channel_info") {
  if (is_linux) {
    sources = [
      "brave_channel_info_posix.cc",
      "brave_channel_info_posix.h",
    ]

    deps = [
      "//base:logging_buildflags",
    ]

    configs += [
      "//brave/build/linux:linux_channel_names",
    ]
  }
}

source_set("network_constants") {
  sources = [
    "network_constants.cc",
    "network_constants.h",
  ]
}

source_set("pref_names") {
  sources = [
    "pref_names.cc",
    "pref_names.h",
  ]

  deps = [ "//components/gcm_driver:gcm_buildflags" ]
}

source_set("switches") {
  sources = [
    "brave_switches.cc",
    "brave_switches.h",
  ]

  deps = [
    "//base",
  ]
}

source_set("common") {
  sources = [
    "brave_channel_info.cc",
    "brave_channel_info.h",
    "brave_content_client.cc",
    "brave_content_client.h",
    "brave_features.cc",
    "brave_features.h",
    "brave_isolated_worlds.h",
    "brave_paths.cc",
    "brave_paths.h",
    "common_message_generator.cc",
    "common_message_generator.h",
    "resource_bundle_helper.cc",
    "resource_bundle_helper.h",
    "translate_network_constants.cc",
    "translate_network_constants.h",
    "url_constants.cc",
    "url_constants.h",
    "webui_url_constants.cc",
    "webui_url_constants.h",
  ]

  if (!is_android) {
    sources += [
      "importer/chrome_importer_utils.cc",
      "importer/chrome_importer_utils.h",
      "importer/importer_constants.h",
    ]
  }

  if (enable_extensions) {
    sources += [
      "extensions/brave_extensions_api_provider.cc",
      "extensions/brave_extensions_api_provider.h",
      "extensions/extension_constants.cc",
      "extensions/extension_constants.h",
      "extensions/whitelist.cc",
      "extensions/whitelist.h",
    ]

    public_deps = [
      "extensions/api",
    ]
  }

  deps = [
    ":network_constants",
    ":pref_names",
    ":shield_exceptions",
    ":switches",
    "//brave/chromium_src:common",
    "//content/public/common",
    "//extensions/buildflags",
  ]

  if (is_mac) {
    sources += [
      "importer/chrome_importer_utils_mac.mm",
    ]
  }

  if (is_linux) {
    sources += [
      "importer/chrome_importer_utils_linux.cc",
    ]
  }

  if (is_win) {
    sources += [
      "importer/chrome_importer_utils_win.cc",
    ]
  }
}

source_set("shield_exceptions") {
  sources = [
    "shield_exceptions.cc",
    "shield_exceptions.h",
  ]

  deps = [
    "//brave/extensions:common",
    "//url",
  ]
}

config("constants_configs") {
  defines = []
  if (is_mac) {
    defines += [
      "BRAVE_PRODUCT_STRING=\"$chrome_product_full_name\"",
    ]
  }
}
